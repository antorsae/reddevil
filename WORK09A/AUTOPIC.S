	BRA PROGRAM
	DC.B 'HEY!!! WHAT ARE YOU DOING????? '
	DC.B 'IF YOU ARE LOOKING THIS FOR HELP '
	DC.B 'CONTACT ME AND I WILL GIVE YOU '
	DC.B 'SOME 68000 HINTS! BYE: '
	DC.B 'THE DELIVERANCE, PO BOX 566, '
	DC.B '03080, ALICANTE, SPAIN! '   	
PROGRAM	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	MOVE.L A7,OLDSP2
	LEA $2000,A7
	MOVE.B $FFFF8260.W,D0
	AND.B #%11,D0
	CMP.B #3,D0
	BEQ.S ALTA
	BSR RITI
FUERA	MOVEA.L OLDSP2,A7
	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	ADDQ.L #6,A7
	CLR.L -(A7)
	TRAP #1
ALTA	PEA HIGHR
	MOVE.W #9,-(A7)
	TRAP #1
	ADDQ.L #6,A7
KWAIT	CMP.B #$39,$FFFFFC02.W
	BNE.S KWAIT
	BRA.S FUERA 
RITI	BSR SMUS
	LEA $FFFF8240.W,A0
	MOVEM.L (A0),D0-D7
	MOVEM.L D0-D7,OLDCOL
	MOVEQ #0,D0
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)
	MOVE.B $FFFF8201,D0
	SWAP D0
	MOVE.B $FFFF8203,D0
	LSL.W #8,D0
	MOVE.L D0,OLDSC
	MOVE.L #$68000,D0
	MOVE.L D0,SCREEN
	BSR READFIL
	TST.W D0
	BMI VETE
	MOVE.L SCREEN,D0
	MOVEA.L D0,A2
	SUB.L #(208*160),D0
	MOVE.L D0,D1
	MOVEA.L D0,A0
	ADD.L #(208*160)+(200*160),D0
	MOVEA.L D0,A1
BORRAL	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S BORRAL
	LEA EPRG+34(PC),A0
	MOVE.W #(32000/8)-1,D0
COPPAN	MOVE.L (A0)+,(A2)+
	MOVE.L (A0)+,(A2)+
	DBF D0,COPPAN
	MOVEM.L EPRG+2,D0/D2-D7/A0
	MOVEM.L D0/D2-D7/A0,$FFFF8240.W
	BSR SINT
	BSR SSCREEN
	MOVE.L SCREEN,D0
WAIT1	CLR.B SYNC
VSYNC1	TST.B SYNC
	BEQ.S VSYNC1
	ADD.L #$500,D1
	BSR SSCREEN
	CMP.L D1,D0
	BGT.S WAIT1
WAIT2	CLR.B SYNC
VSYNC2	TST.B SYNC
	BEQ.S VSYNC2
	MOVEQ #0,D0
	MOVE.B $FFFFFC02.W,D0
	LEA KEYTBL(PC),A0
FINDCOM	MOVE.W (A0),D1
	BMI.S WAIT2
	ADDQ.L #8,A0
	CMP.W D0,D1
	BNE.S FINDCOM
	MOVEA.L -6(A0),A1
	MOVE.W -2(A0),D0
	JMP (A1)
LETSGO	BSR GINT	
	MOVE.L OLDSC,D1
	BSR SSCREEN
VETE	MOVEM.L OLDCOL,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	RTS
CHGM	MOVE.W MUS,D1
	CMP.W D0,D1
	BEQ.S WAIT2
	MOVE.W D0,MUS
	JSR $7E000
	BRA.S WAIT2
KEYTBL	DC.W $39
	DC.L LETSGO
	DC.W 0
	DC.W $3B
	DC.L CHGM
	DC.W 2
	DC.W $3C
	DC.L CHGM
	DC.W 3
	DC.W $3D
	DC.L CHGM
	DC.W 4
	DC.L -1 
SINT	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.W #$2700,SR
	MOVE.L $70.W,(A0)+
	MOVE.B 7(A1),(A0)+
	MOVE.B 9(A1),(A0)+
	MOVE.L #VBL,$70.W
	CLR.B 7(A1)
	CLR.B 9(A1)
	MOVE.W #$2300,SR
	RTS
GINT	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.W #$2700,SR
	MOVE.L (A0)+,$70.W
	MOVE.B (A0)+,7(A1)
	MOVE.B (A0)+,9(A1)
	MOVE.W #$2300,SR
	LEA $FFFF8800.W,A0
	MOVE.L #$8080000,(A0)
	MOVE.L #$9090000,(A0)
	MOVE.L #$A0A0000,(A0)
	RTS
VBL	ST SYNC
	JSR $7E000+(6*3)
	RTE
SSCREEN	MOVE.L D1,-(A7)
	LSR.L #8,D1
	MOVE.B D1,$FFFF8203.W
	LSR.W #8,D1
	MOVE.B D1,$FFFF8201.W
	MOVE.L (A7)+,D1
	RTS
READFIL	CLR.W -(A7)
	PEA PATHNM
	MOVE.W #$3D,-(A7)
	TRAP #1
	ADDQ.L #8,A7
	TST.W D0
	BMI VAMONOS
	MOVE.W D0,-(A7)
	PEA EPRG
	PEA $FFFFFF
	MOVE.W D0,-(A7)
	MOVE.W #$3F,-(A7)
	TRAP #1
	LEA 12(A7),A7
	MOVE.W #$3E,-(A7)
	TRAP #1
	ADDQ.L #4,A7
VAMONOS	RTS
SMUS	LEA MUZAK(PC),A0
	LEA $7E000,A1
	MOVE.W #((EPRG-MUZAK)/4)-1,D0
COPMUS	MOVE.L (A0)+,(A1)+
	DBF D0,COPMUS
	MOVEQ #4,D0
	MOVE.W D0,MUS
	JSR $7E000
	RTS
OLDCOL	DS.W 16
MUS	DC.W 0
SYNC	DC.W 0
INTS	DS.W 3
OLDSC	DC.L 0
SCREEN	DC.L 0
OLDSP1	DS.L 1
OLDSP2	DS.L 1
HIGHR	DC.B 'THIS PROGRAM ONLY RUNS IN LOW REZ... ',0 
PATHNM	DC.B 'CONRADO.PI1',0
MUZAK	INCBIN 'MUZAK.BIN'
EPRG	