VIDEO	EQU $90000
XTRA	EQU $100
LONG	EQU ($A0*200)+($100*256)
LOGOL	EQU 80
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,A7
	MOVE.L #VIDEO,D1
	LSR.L #8,D1
	MOVE.B D1,$FFFF8203.W
	LSR.W #8,D1
	MOVE.B D1,$FFFF8201.W
	BSR SWAVES
	BSR GENROX
	BSR GFFVDO
	* BSR GFFSND
	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	BSR WAVEFX
ME	MOVE.W #$25,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	MOVE.W #$700,$FFFF8240.W
	BSR RESTOFF
	BSR WAVEFX
	BSR CSC
	MOVE.W #$0,$FFFF8240.W
	MOVEM.L COOLDAT,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	MOVEM.L OLDCOL,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	PEA 0
	TRAP #1
CSC	LEA V_COUNT(PC),A0
	ADD.W #$C00,(A0)
	AND.W #$FFFF,(A0)
	MOVEQ #0,D0
	MOVE.W (A0),D0
	MOVE.L #VIDEO,D1
	ADD.L D0,D1
	MOVE.L D1,SCREEN
	LSR.L #8,D1
	MOVE.B D1,$FFFF8203.W
	LSR.W #8,D1
	MOVE.B D1,$FFFF8201.W
	RTS
GENROX	LEA LOGOR(PC),A1
	BSR PRINTL
	MOVEQ #16-1,D7
GENLOGO	BSR SAVECOM
	BSR ROTALO
	DBF D7,GENLOGO
	RTS
SAVECOM	LEA VIDEO,A0
	MOVEQ #LOGOL-1,D0
LINECOP	MOVEQ #13-1,D6
ROWMASK	MOVE.L (A0)+,D1
	MOVE.L (A0)+,D2
	MOVE.W D1,D3
	SWAP D1
	OR.W D1,D3
	SWAP D1
	OR.W D2,D3
	SWAP D2
	OR.W D2,D3
	SWAP D2
	NOT.W D3
	MOVE.W D3,(A1)+
	MOVE.W D3,(A1)+
	MOVE.L D1,(A1)+
	MOVE.L D2,(A1)+
	DBF D6,ROWMASK
	LEA 160-(13*8)(A0),A0
	DBF D0,LINECOP
	RTS
ROTALO	LEA VIDEO,A0
	MOVEQ #LOGOL-1,D2
LINROX	MOVE.W #0,CCR
	MOVEQ #4-1,D1
PLANOS	ROXR (A0)
	ROXR 8*1(A0)
	ROXR 8*2(A0)
	ROXR 8*3(A0)
	ROXR 8*4(A0)
	ROXR 8*5(A0)
	ROXR 8*6(A0)
	ROXR 8*7(A0)
	ROXR 8*8(A0)
	ROXR 8*9(A0)
	ROXR 8*10(A0)
	ROXR 8*11(A0)
	ROXR 8*12(A0)
	ADDQ.L #2,A0
	DBF D1,PLANOS
	LEA 160-(2*4)(A0),A0
	DBF D2,LINROX
	RTS
PRINTL	LEA COOLDAT+32(PC),A0
	LEA VIDEO,A2
	MOVEQ #LOGOL-1,D0
COPLO	REPT 12
	MOVE.L (A0)+,(A2)+
	MOVE.L (A0)+,(A2)+
	ENDR
	CLR.L (A2)+
	CLR.L (A2)+
	LEA 160-(13*8)(A2),A2
	DBF D0,COPLO
	RTS
GFFVDO	LEA VIDEO,A0
	MOVE.L #LONG,D7
	LSR.L #2,D7
	MOVE.W #$E456,D0
GENLOP1	
RND	MACRO
	MOVE.W D0,D1
	ADD.W D1,D2
	NOT.W D2
	ADD.W D2,D1
	ADD.W D7,D1
	NOT.W D1
	ADD.W D1,D0
	SUB.W D2,D0
	ADD.W D7,D2
	AND.W #$FFFF,D2
	MULU D0,D2
	NOT.W D2
	SUB.W D2,D0 
	SWAP D2
	ADD.W D2,D0
	MOVE.W D0,(A0)+
	ENDM
	RND
	RND
	CLR.L (A0)+
	DBF D7,GENLOP1 
	RTS
SWAVES	LEA WAVETBL(PC),A0
	MOVEQ #0,D0
LOKKK	MOVE.L (A0),D1
	CMP.W #55,D1
	BNE.S NOYET
	LEA WAVETBL(PC),A1
	MOVEQ #LOGOL-1,D0
COPR	MOVE.L (A1)+,(A0)+
	DBF D0,COPR
	MOVE.L #-1,(A0)
	RTS
NOYET	ADD.L D1,D0
	MOVE.L D0,D2
	DIVU #16,D2
	LSL.W #3,D2
	MOVE.W D2,(A0)+
	CLR.W D2
	SWAP D2
	MULU #LOGOL*13*3,D2
	MOVE.W D2,(A0)+
	BRA.S LOKKK
WAVEFX	LEA CWAVE(PC),A0
	LEA GBUF+(LOGOL*13*4)+(4*LOGOL),A5
	ADDQ.L #4,(A0)
	MOVEA.L (A0),A0
	LEA LOGOR(PC),A1
	MOVEA.L SCREEN,A2
	LEA (160*50)+(4*8)(A2),A2
	MOVEQ #LOGOL-1,D7
WAVELOP	MOVEQ #0,D0
	MOVE.W (A0)+,D0
	LEA 0(A2,D0.W),A4
	MOVE.W (A0)+,D0
	ADD.L D0,D0
	ADD.L D0,D0
	LEA 0(A1,D0.L),A3
	MOVE.L A4,-(A5)
PSOP	MACRO ROW
	MOVEM.L \1*8(A4),D0-D5
	MOVEM.L D0/D2/D4,-(A5)
	AND.L (A3)+,D0
	OR.L (A3)+,D0
	MOVE.L (A3)+,D1
	AND.L (A3)+,D2
	OR.L (A3)+,D2
	MOVE.L (A3)+,D3
	AND.L (A3)+,D4
	OR.L (A3)+,D4
	MOVE.L (A3)+,D5
	MOVEM.L D0-D5,\1*8(A4)
	ENDM
	MOVEM.L (A4),D0-D5
	MOVEM.L D0/D2/D4,-(A5)
	AND.L (A3)+,D0
	OR.L (A3)+,D0
	MOVE.L (A3)+,D1
	AND.L (A3)+,D2
	OR.L (A3)+,D2
	MOVE.L (A3)+,D3
	AND.L (A3)+,D4
	OR.L (A3)+,D4
	MOVE.L (A3)+,D5
	MOVEM.L D0-D5,(A4)
	PSOP 3
	PSOP 6
	MOVEM.L 9*8(A4),D0-D5
	MOVEM.L D0/D2/D4,-(A5)
	AND.L (A3)+,D0
	OR.L (A3)+,D0
	MOVE.L (A3)+,D1
	AND.L (A3)+,D2
	OR.L (A3)+,D2
	MOVE.L (A3)+,D3
	AND.L (A3)+,D4
	OR.L (A3)+,D4
	MOVE.L (A3),D5
	MOVEM.L D0-D5,9*8(A4)
	LEA 160(A2),A2
	LEA 13*12(A1),A1
	DBF D7,WAVELOP
	MOVE.L (A0),D0
	BPL.S FUERA
	MOVE.L #WAVETBL,CWAVE
FUERA	RTS
RESTOFF	LEA GBUF+(LOGOL*13*4)+(4*LOGOL)(PC),A0
	MOVEQ #0,D0
	MOVEQ #LOGOL-1,D7
RESTLOP	MOVEA.L -(A0),A1
	REPT 12
	MOVE.L -(A0),(A1)+
	MOVE.L D0,(A1)+
	ENDR
	DBF D7,RESTLOP
	RTS
CWAVE	DC.L WAVETBL
OLDRND	DC.W $D456
V_COUNT	DC.W 0
SCREEN	DC.L VIDEO
OLDCOL	DS.W 16
WAVETBL	REPT 10
	DS.L LOGOL
	DC.L 0,1,2,2,2,3,3,3,3,4,4,4,4,4,3,3,3,3,2,2,2,1,0
	DC.L 0,-1,-2,-2,-2,-3,-3,-3,-3,-4,-4,-4,-4,-4,-3,-3,-3,-3,-2,-2,-2,-1,0
	ENDR
	DC.L 55
	DS.L LOGOL
COOLDAT	INCBIN 'COOL.BIN'
ENDPA
GBUF	EQU ENDPA
LOGOR	EQU GBUF+(LOGOL*13*4)+(4*LOGOL)
	