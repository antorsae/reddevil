** INTRO COOL DEMO: SECUENCIA DE LOGOS 
** EN LOS QUE SE ENCUNTRAN LOS CREADORES
** DE LA 'COOL DEMO', LA MUSICA ES DFORCE_2.MOD
** Y LAS RUTINAS DE PLAYBACK SON DE ST_CNX

** (C) CODING+DIBUJOS-MUSICA: RED DEVIL '90

ANCH	EQU $A0
BASE	EQU $E0000	
SPDUP	EQU 4

SSC	MACRO 
	MOVEA.L CHSC,A0
	MOVE.L (A0)+,CONTLR
	MOVE.L SCREEN,D0
	MOVE.L D0,SCREEN
	MOVE.L (A0),D1
	SUB.L D1,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	ENDM

OTRAHO	MACRO C1,C2,C3,C4,C5,C6
	MOVEM.L \1,D0-D7
	MOVEM.L D0-D7,CNOW
	MOVE.L #\2,CANIM
	MOVE.W #\3,VECD1
	MOVE.W #\4,VECD2
	MOVE.L #\5,SCREEN
	MOVE.W #\6,DELAY
	MOVE.L #SCDATA,CHSC
	MOVE.W #SPDUP,SPEED
	CLR.W STEPNOW
	BSR PLOGO
	ENDM

COLOCA	MACRO C1,C2,R1,R2
	BSR BSC
	LEA \1+(16*2)(PC),A0
	LEA BASE+(\2*ANCH)+\3,A1
	MOVEQ #\4-1,D0
	MOVE.W #\5-1,D1
	BSR PREPSC
	ENDM
	

	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,SP
	COLOCA PIC1,200,8,18,131
	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	BSR SINT
	OTRAHO COLS1,COLANI1,(200-35)/8,176/8,BASE,100
	COLOCA PIC2,200,0,20,59
	OTRAHO COLS2,COLANI2,(200-70)/8,136/8,BASE,100
	BSR RINT
	MOVEM.L OLDCOL,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CLR.L -(A7)
	TRAP #1
PLOGO
VSYNC1	CLR.B SYNC
WAIT1	TST.B SYNC
	BEQ.S WAIT1
	SSC
	ADD.L #8,CHSC
	MOVE.W 4(A0),D0
	BPL.S KWAIT1
	ADD.L #$500,SCREEN
	MOVE.L #SCDATA,CHSC
	SUBQ.W #1,VECD1
	BEQ.S VSYNC2
KWAIT1	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC1
VSYNC2	CLR.B SYNC
WAIT2	TST.B SYNC
	BEQ.S WAIT2
	SSC
	MOVEA.L CANIM,A0
	LEA CNOW+(12*2)(PC),A1
	ADDA.W STEPNOW,A0
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0),(A1)
	SUBQ.W #1,SPEED
	BNE.S KWAIT2  
	MOVE.W #SPDUP,SPEED
	ADDQ.W #8,STEPNOW
	MOVE.W 4(A0),D0
	BMI.S VSYNC3
KWAIT2	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC2
VSYNC3	CLR.B SYNC
WAIT3	TST.B SYNC
	BEQ.S WAIT3
	SSC
	SUBQ.W #1,DELAY
	BEQ.S VSYNC4
	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC3
VSYNC4	CLR.B SYNC
WAIT4	TST.B SYNC
	BEQ.S WAIT4
	SSC
	ADD.L #8,CHSC
	MOVE.W 4(A0),D0
	BPL.S KWAIT4
	ADD.L #$500,SCREEN
	MOVE.L #SCDATA,CHSC
	SUBQ.W #1,VECD2
	BEQ.S ESYNC
KWAIT4	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC4
ESYNC	RTS
OLDCOL	DS.W 16
DELAY	DS.W 1
VECD1	DS.W 1
VECD2	DS.W 1
SCREEN 	DS.L 1
SINT	MOVE.W #$2700,SR
	LEA INTS,A0
	MOVE.L $70.W,(A0)+
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA09.W,(A0)+
	MOVE.B $FFFF8260.W,(A0)+
	MOVE.B $FFFF820A.W,(A0)+
	MOVE.W $FFFF8240.W,(A0)+
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	MOVE.L #FULLVBL,$70.W
	MOVE.W #$2300,SR
	RTS
RINT	MOVE.W #$2700,SR
	LEA INTS,A0
	MOVE.L (A0)+,$70.W
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA09.W
	MOVE.B (A0)+,$FFFF8260.W
	MOVE.B (A0)+,$FFFF820A.W
	MOVE.W (A0)+,$FFFF8240.W
	MOVE.W #$2300,SR
	RTS
INTS	DS.L 3
FULLVBL	MOVEM.L D0-D7/A0-A6,-(A7)
	LEA $FFFF8240.W,A0
	MOVEQ #0,D1
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)
	LEA $FFFF8209.W,A0
	LEA $FFFF8260.W,A1
	MOVEQ #16,D2
	MOVEQ #2,D3
	MOVEQ #0,D4
	MOVEQ #28-1,D5
	MOVE.W CONTLR,D6
	MOVE.W CONTLR+2,D7
	SUB.W D6,D5
	SUB.W D7,D5
	MOVE.W #$2700,SR
	MOVEQ #10-1,D0
SWAIT1	DBF D0,SWAIT1
	CLR.B $FFFF820A.W
	MOVEQ #3,D0
SWAIT2	NOP
	DBF D0,SWAIT2
	MOVE.W #2,$FFFF820A.W
COUNT1	MOVE.B (A0),D0
	BEQ.S COUNT1
	SUB.W D0,D2
	LSL.W D2,D1
	LEA $FFFF820A.W,A0
	REPT 85
	NOP
	ENDR
MAINLO1	REPT 10
	NOP
	ENDR
	MOVE.B D3,(A1)
	MOVE.B D4,(A1)
	REPT 89
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	DBF D6,MAINLO1
	BRA.S COLOCA1
MAINLO2	REPT 4
	NOP
	ENDR
COLOCA1	REPT 99
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	DBF D7,MAINLO2
	BRA.S COLOCA2
MAINLO3	REPT 4
	NOP
	ENDR
COLOCA2	REPT 121
	NOP
	ENDR
	DBF D5,MAINLO3
	MOVEM.L CNOW,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
EVBL	MOVEM.L (A7)+,D0-D7/A0-A6
	ST SYNC
	RTE
BSC	LEA BASE,A0
	LEA BASE+(200*ANCH*3),A1
SS	CLR.L (A0)+
	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S SS
	RTS
PREPSC	MOVEQ #(ANCH/8)-1,D3
	SUB.W D0,D3
	LSL.W #3,D3
COPV	MOVE.W D0,D2
COPH	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D2,COPH
	LEA 0(A1,D3.W),A1
	DBF D1,COPV
	RTS	
STEPNOW	DS.W 1
SPEED	DS.W 1
SYNC	DS.W 1
CONTLR	DS.W 1
	DS.W 1
CHSC	DS.L 1
SCDATA	INCBIN 'VERTI.DAT'
	DC.W -1
CANIM	DS.L 1
COLORES	DS.L 1
COLS1	DC.W $000,$745,$735,$725,$715
	DC.W $705,$605,$504,$403,$302
	DC.W $201,$100,$302,$403,$504
	DC.W $606
COLANI1	DC.W $302,$403,$504,$605
	DC.W $413,$514,$615,$716
	DC.W $524,$625,$726,$727
	DC.W $635,$736,$737,$737
	DC.W $746,$747,$747,$747
	DC.W $757,$767,$767,$766
	DC.W $677,$767,$776,$775
	DC.W $567,$676,$775,$774
	DC.W $456,$565,$674,$773
	DC.W $345,$454,$563,$672
	DC.W $234,$343,$452,$561
	DC.W $123,$232,$341,$450
	DC.W -1
COLS2	DC.W $000,$657,$546,$435,$324
	DC.W $213,$102,$002,$037,$027
	DC.W $017,$107,$107,$017,$027
	DC.W $037
COLANI2	DC.W $107,$017,$027,$037
	DC.W $217,$127,$137,$147
	DC.W $327,$237,$247,$257
	DC.W $437,$347,$357,$367
	DC.W $547,$457,$467,$477
	DC.W $657,$567,$577,$577
	DC.W $767,$677,$677,$677
	DC.W $777,$777,$777,$777
	DC.W $767,$777,$777,$777
	DC.W $757,$767,$777,$777
	DC.W $747,$757,$777,$777
	DC.W $737,$747,$767,$777
	DC.W $626,$737,$757,$777
	DC.W $516,$626,$646,$666
	DC.W $405,$515,$535,$555
	DC.W -1
CNOW	DS.W 16
PIC1	INCBIN 'REDLOGO.BIN'
PIC2	INCBIN 'C23.BIN'
