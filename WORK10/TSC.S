BASE	EQU $F8000
SLI	EQU 25
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,A7
	BSR GFONT
	BSR DTEXT
	BSR CLS
	LEA MAXROW(PC),A0
	MOVE.W MTEXT(PC),(A0)
	MOVE.W #-1,-2(A0)
ME	MOVE.W #$25,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	*MOVE.W #$700,$FFFF8240.W
	BSR CHGSC
	BSR TSC
	*MOVE.W #$777,$FFFF8240.W
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	PEA 0
	TRAP #1
GFONT	LEA FNT+32(PC),A0
	LEA FONT(PC),A2
	LEA CHARNU(PC),A3
	BSR QUITAM
	MOVEQ #0,D5
	MOVE.B (A3)+,D5
OTROM	BSR PLETRA
	MOVEQ #2-1,D6
SROTA	BSR SAVEL
	BSR ROTAL
	DBF D6,SROTA
	MOVE.B (A3)+,D5
	BPL.S OTROM
	RTS
SAVEL	LEA BASE,A1
	MOVE.W D5,D1
SROW	MOVEQ #SLI-1,D0
SLIN	MOVE.L (A1),(A2)+
	MOVE.W 4(A1),(A2)+
	LEA 160(A1),A1
	DBF D0,SLIN
	LEA (-160*SLI)+8(A1),A1
	DBF D1,SROW
	RTS
ROTAL	LEA BASE,A1
	MOVE.W D5,D3
	ADDQ.W #1,D3
	LSL.W #3,D3
	NEG.W D3
	ADD.W #160,D3
	MOVEQ #8-1,D7
VECESR	MOVEQ #3-1,D0
PLANOSR	MOVEQ #SLI-1,D1
LINEASR	MOVE.B #0,CCR
	MOVE.W D5,D2
ROWSR	ROXR (A1)
	ADDQ.L #8,A1
	DBF D2,ROWSR
	LEA 0(A1,D3.W),A1
	DBF D1,LINEASR
	LEA (-160*SLI)+2(A1),A1
	DBF D0,PLANOSR
	LEA BASE,A1
	DBF D7,VECESR
	RTS
PLETRA	LEA BASE,A1
	MOVE.W D5,D1
	SUBQ.W #1,D1
FLI	MOVEQ #SLI-1,D0
DLI	MOVE.L (A0)+,(A1)
	MOVE.W (A0)+,4(A1)
	LEA 160(A1),A1
	DBF D0,DLI
	LEA (-160*SLI)+8(A1),A1
	DBF D1,FLI
	MOVEQ #0,D1
	MOVEQ #SLI-1,D0
BDLI	MOVE.L D1,(A1)
	MOVE.W D1,4(A1)
	LEA 160(A1),A1
	DBF D0,BDLI
	RTS
QUITAM	LEA (A3),A4
QUITARS	MOVE.B (A4),D0
	SUB.B #"0",D0
	MOVE.B D0,(A4)+
	MOVE.B (A4),D0
	BPL.S QUITARS
	RTS
DTEXT	LEA TEXT(PC),A0
	LEA MTEXT(PC),A1
	LEA CHARCH(PC),A2
	LEA CHARNU(PC),A3
	MOVE.B (A0)+,D0
SETTXT	BSR SEARCH
	BSR CALCIN
	MOVE.W D2,(A1)+
	MOVE.W D0,(A1)+
	MOVE.B (A0)+,D0
	BPL.S SETTXT
	MOVE.L #0,(A1)
	RTS
SEARCH	LEA (A2),A4
	MOVE.W #-1,D1
SRCHLOP	MOVE.B (A4)+,D2
	BMI.S FUERA
	ADDQ.W #1,D1
	CMP.B D0,D2
	BNE.S SRCHLOP
FUERA	RTS
CALCIN	LEA (A3),A5
	MOVEQ #0,D0
	MOVEQ #0,D2
	MOVEQ #0,D4
	MOVE.B 0(A3,D1.W),D2
	SUBQ.W #1,D1
	TST.W D1
	BMI.S ESEL0
SUMALOP	MOVE.B (A5)+,D4
	ADDQ.B #1,D4
	ADD.W D4,D0
	DBF D1,SUMALOP
ESEL0	MULU #6*SLI*2,D0
	RTS
CHGSC	LEA SCREEN(PC),A0
	MOVE.L (A0),D0
	BCHG #15,D0
	MOVE.L D0,(A0)
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	RTS
TSC	MOVEA.L SCREEN,A3
	BSR BARRE
	LEA CTEXT(PC),A0
	MOVEA.L (A0),A1
	LEA FONT(PC),A2 
	MOVE.W #6*SLI,D3
	EOR.W D3,4(A0)
	BEQ.S AUMROW
	ADDQ.W #1,6(A0)
AUMROW	MOVEQ #0,D0
	MOVEQ #0,D2
	MOVE.W 6(A0),D0
	MOVE.W 8(A0),D1
	MOVE.W 4(A0),D2
	CMP.W D1,D0
	BEQ.S OTRAL
	ADDQ.W #1,D1
	MULU D2,D1
	MULU D3,D0
	ADD.W D1,D0
	ADD.W 2(A1),D0
	LEA 0(A2,D0.L),A4
	BSR PRTROW
	RTS
OTRAL	EOR.W D3,10(A0)
	ADDQ.L #4,(A0)
SOLO1	ADDQ.W #1,D1
	MULU D2,D1
	MULU D3,D0
	ADD.W D1,D0
	ADD.W 2(A1),D0
	LEA 0(A2,D0.L),A4
	BSR PRTROW
	MOVE.W 4(A1),D0
	MOVE.W D0,8(A0)
	ADDQ.W #1,D0
	MULU D2,D0
	ADD.W 6(A1),D0
	CLR.W 6(A0)
	LEA 0(A2,D0.L),A4
	BSR ORROW
	MOVE.W 8(A1),D0
	BNE.S ENSANEM
	MOVE.L #MTEXT,(A0)
	LEA MAXROW(PC),A0
	MOVE.W MTEXT(PC),(A0)
	CLR.W -2(A0)
ENSANEM	RTS
PRTROW	
PLIN	MACRO LI
	MOVE.L (A4)+,(\1*160)(A3)
	MOVE.W (A4)+,(\1*160)+4(A3)
	ENDM
	PLIN 0
	PLIN 1
	PLIN 2
	PLIN 3
	PLIN 4
	PLIN 5
	PLIN 6
	PLIN 7
	PLIN 8
	PLIN 9
	PLIN 10
	PLIN 11
	PLIN 12
	PLIN 13
	PLIN 14
	PLIN 15
	PLIN 16
	PLIN 17
	PLIN 18
	PLIN 19
	PLIN 20
	PLIN 21
	PLIN 22
	PLIN 23
	PLIN 24
	RTS
ORROW	
LIN	MACRO LI
	MOVE.L (\1*160)(A3),D0
	OR.L (A4)+,D0
	MOVE.L D0,(\1*160)(A3)
	MOVE.W (\1*160)+4(A3),D0
	OR.W (A4)+,D0
	MOVE.W D0,(\1*160)+4(A3)
	ENDM
	LIN 0
	LIN 1
	LIN 2
	LIN 3
	LIN 4
	LIN 5
	LIN 6
	LIN 7
	LIN 8
	LIN 9
	LIN 10
	LIN 11
	LIN 12
	LIN 13
	LIN 14
	LIN 15
	LIN 16
	LIN 17
	LIN 18
	LIN 19
	LIN 20
	LIN 21
	LIN 22
	LIN 23
	LIN 24
	RTS
BARRE	ADDQ.L #8,A3
	MOVEQ #SLI-1,D0
BROW	MACRO PA
	MOVE.L (8*\1)(A3),-8+(8*\1)(A3)
	MOVE.W (8*\1)+4(A3),-8+(8*\1)+4(A3)
	ENDM
BARROW	BROW 0
	BROW 1
	BROW 2
	BROW 3
	BROW 4
	BROW 5
	BROW 6
	BROW 7
	BROW 8
	BROW 9
	BROW 10
	BROW 11
	BROW 12
	BROW 13
	BROW 14
	BROW 15
	BROW 16
	BROW 17
	BROW 18
	LEA 160(A3),A3
	DBF D0,BARROW
	LEA (-160*SLI)+18*8(A3),A3
	RTS
CLS	LEA BASE-$8000,A0
	LEA BASE+$7D00,A1
CLSLOP	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S CLSLOP
	RTS
SCREEN	DC.L BASE
CHARCH	DC.B 'ABCDEFGHIJKLMNOPQRSTUVWXYZ!?:;0123456789"(),-.',"' ",-1
CHARNU	DC.B '222222222222222222222222222222222222222222222221',-1
TEXT	DC.B "HELLO AND WELCOME TO THE TSC SCROLL  "
	DC.B -1
MTEXT	DS.L MTEXT-TEXT
CTEXT	DC.L MTEXT
INDSUM	DS.W 1
ROWNOW	DS.W 1
MAXROW	DS.W 1
ONETWO	DS.W 1
FNT	INCBIN 'FONT.BIN'
FONT	