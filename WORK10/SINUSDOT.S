** SINUS DOTS: RUTINA DE MOVIMIENTOS
** DADOS POR FUNCIONES TRIGONOMETRICAS
** UN SEN(X) OTRO DE (Y) Y UNA TABLA
** DE CIRCULOS A A¥ADIR A LA POSICION
** DADA. 

** (C) 1990 RED DEVIL

PI	EQU $2000
GRAD	EQU PI/4
MAX	EQU PI/2
MAY	EQU PI/4
LBAL	EQU 2
NBALLS	EQU 540
BASE 	EQU $F0000
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,A7
	BSR SETSIN
	BSR P_BALL
	BSR CLS
	MOVE.W #0,$FFFF8240.W
	MOVE.W #$777,$FFFF8242.W
ME	MOVE.W #$25,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	*MOVE.W #$070,$FFFF8240.W
	BSR INPUTK
	BSR CHGSC
	BSR BOSPR
	BSR PLAT
	BSR CALCTR
	*MOVE.W #$00,$FFFF8240.W
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	CLR.L -(A7)
	TRAP #1
PLAT	LEA BOR(PC),A0
	LEA BORR+(NBALLS*2)(PC),A1
	REPT NBALLS/20
	MOVEM.L (A0)+,D0-D7/A2-A3
	MOVEM.L D0-D7/A2-A3,-(A1)
	ENDR
	RTS
CHGSC	LEA SCREEN(PC),A0
	MOVE.L (A0),D0
	BCHG #15,D0
	MOVE.L D0,(A0)
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	RTS
BOSPR	MOVEA.L SCREEN,A1
	MOVEQ #0,D1
	LEA BORR(PC),A2
	REPT NBALLS
	MOVE.W (A2)+,D0
	MOVE.W D1,0(A1,D0.W) 
	ENDR
	RTS	
INPUTK	MOVE.B $FFFFFC02.W,D0
	LEA FUNC(PC),A0
SEARCHX	MOVE.W (A0)+,D1
	BPL.S NVETE
	RTS
NVETE	ADDQ.L #4,A0
	CMP.B D0,D1
	BNE.S SEARCHX
	MOVE.L -4(A0),A0
	LEA SINUSVAR(PC),A1
	JMP (A0)
FUNC	
KEU	MACRO T,F
	DC.W \1
	DC.L \2
	ENDM
	KEU $3B,SUXP
	KEU $3C,REXP
	KEU $3D,SUXI
	KEU $3E,REXI
	KEU $3F,SUYP
	KEU $40,REYP
	KEU $41,SUYI
	KEU $42,REYI
	KEU $43,SUCP
	KEU $44,RECP
	KEU $62,SUCI
	KEU $61,RECI
	DC.W -1
SUXP	ADDQ.W #4,3*2(A1)
	RTS
REXP	SUBQ.W #4,3*2(A1)
	RTS
SUXI	ADDQ.W #4,8*2(A1)
	RTS
REXI	SUBQ.W #4,8*2(A1)
	RTS
SUYP	ADDQ.W #2,2*2(A1)
	RTS
REYP	SUBQ.W #2,2*2(A1)
	RTS
SUYI	ADDQ.W #2,7*2(A1)
	RTS
REYI	SUBQ.W #2,7*2(A1)
	RTS
SUCP	ADDQ.W #4,5*2(A1)
	RTS
RECP	SUBQ.W #4,5*2(A1)
	RTS
SUCI	ADDQ.W #4,6*2(A1)
	RTS
RECI	SUBQ.W #4,6*2(A1)
	RTS
SINUSVAR
POSX	DC.W 0
POSY	DC.W 0
INCX	DC.W 0*2
INCY	DC.W 0*4
POCC	DC.W 0
INKC	DC.W 0*4
INDC	DC.W 0*4
INDX	DC.W 0*2
INDY	DC.W 0*4
BOR	REPT NBALLS
	DC.W 0
	ENDR
BORR	REPT NBALLS
	DC.W 0
	ENDR
CALCTR	LEA TABLE(PC),A0
	LEA (MAX*2)(A0),A1
	LEA (MAY*4)(A1),A2
	LEA SINUSVAR(PC),A4
	LEA BOR(PC),A5
	LEA BALL(PC),A3
	MOVEA.L SCREEN,A6
	MOVE.L 2*2(A4),D1
	MOVE.L (A4),D0
	MOVE.W #(PI-1),D4
	SWAP D4
	MOVE.W #(PI-1),D4
	ADD.W D1,D0
	SWAP D0
	SWAP D1
	ADD.W D1,D0
	SWAP D0
	SWAP D1
	AND.L D4,D0
	MOVE.L D0,(A4)
	MOVE.W 5*2(A4),D3
	MOVE.W 4*2(A4),D1
	ADD.W D3,D1
	AND.W D4,D1
	MOVE.W D1,4*2(A4)
	MOVE.L 7*2(A4),D2
	MOVE.W 6*2(A4),D3
	MOVE.W #LBAL*16,D7
	REPT NBALLS
	BSR CLALC
	ENDR
	RTS
CLALC	ADD.W D2,D0
	AND.W D4,D0
	MOVE.W 0(A1,D0.W),D6
	MOVE.W 2(A1,D0.W),D5
	SWAP D0
	SWAP D2
	ADD.W D2,D0
	AND.W D4,D0
	ADD.W 0(A0,D0.W),D5 
	SWAP D2
	SWAP D0
	ADD.W D3,D1
	AND.W D4,D1
	CMP.W D1,D7
	BGT.S NSUC
	SUB.W D7,D1
NSUC	ADD.W 0(A2,D1.W),D6
	ADD.W 2(A2,D1.W),D5
	CMP.W D6,D7
	BGT.S NORES
	SUB.W D7,D6
	ADDQ.W #8,D5
NORES	MOVE.W D5,(A5)+ 
	MOVE.W 0(A3,D6.W),D6
	OR.W D6,0(A6,D5.W)
	RTS
SETSIN	LEA TABLE(PC),A6
	LEA (A6),A0
	MOVE.W #MAX-1,D0
	BSR SETYSIN
	LEA (MAX*2)(A6),A0
	MOVE.W #MAY-1,D0
	BSR SETXSIN
	LEA (MAX*2)+(MAY*4)(A6),A0
	MOVE.W #GRAD-1,D0
	BSR S_XYSIN
	RTS
SETYSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	MULU #$A0,D7
	MOVE.W D7,(A0)+
	DBF D0,SETYSIN
	RTS
SETXSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	DIVU #16,D7
	MOVE.W D7,D6
	CLR.W D7
	SWAP D7
	MULU #LBAL,D7
	MOVE.W D7,(A0)+
	LSL.W #3,D6
	MOVE.W D6,(A0)+
	DBF D0,SETXSIN
	RTS
S_XYSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	DIVU #16,D7
	MOVE.W D7,D6
	CLR.W D7
	SWAP D7
	MULU #LBAL,D7
	MOVE.W D7,(A0)+
	MULU #8,D6
	MOVEQ #0,D7
	MOVE.W (A0),D7
	MULU #160,D7
	ADD.W D7,D6
	MOVE.W D6,(A0)+
	DBF D0,S_XYSIN
	RTS
P_BALL	LEA BALL(PC),A1
	MOVE.W #$8000,D0
	MOVEQ #16-1,D1
ROTPONT	MOVE.W D0,(A1)+
	LSR.W #1,D0
	DBF D1,ROTPONT
	RTS
CLS	LEA BASE,A0
	LEA BASE+($8000)+($7D00),A1
LKJ	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S LKJ
	RTS
SCREEN	DC.L BASE
TABLE	INCBIN 'MEGASIN.RED'
BALL	
	