MAX	EQU 320-48
MAY	EQU 200-55-25
LINS	EQU 55
NBALLS	EQU 5
BASE 	EQU $F0000
LBAL	EQU LINS*4*5*2
LLOGO	EQU 12*2*16
SLI	EQU 25
CV	EQU 4
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,A7
	BSR GFONT
	BSR DTEXT
	BSR SETMLOG
	BSR P_BALL
	BSR SETWLOG
	BSR SETSIN
	BSR CLS
	BSR PREPALL
	BSR SINT
ME	CLR.B SYNC
VSYNC	TST.B SYNC
	BEQ.S VSYNC
	MOVEM.L COLS1(PC),D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	MOVE.L #C1+2,IND1+2
	MOVE.W C1,$FFFF8240+(CV*2).W
	BSR MUSIX+8
	BSR CHGSC
	BSR BOSPR
	BSR PLAT
	BSR CALCTR
	BSR PSPRIT
	BSR PLOGO
	BSR TSC
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	BSR GINT
	BSR REPALL
	CLR.L -(A7)
	TRAP #1
PLOGO	LEA LOGOR,A0
	MOVEA.L SCREEN,A1
	LEA CWAVE(PC),A3
	MOVEA.L (A3),A2
	MOVE.W (A2)+,D0
	LEA 8+4(A1,D0.W),A1
	ADDA.W (A2)+,A0 
	MOVEQ #12-1,D0
D0LOP	MOVEQ #0,D7
	MOVE.W D7,-8(A1)
	MOVE.W (A0)+,(A1)
	MOVE.W (A0)+,8*1(A1)
	MOVE.W (A0)+,8*2(A1)
	MOVE.W (A0)+,8*3(A1)
	MOVE.W (A0)+,8*4(A1)
	MOVE.W (A0)+,8*5(A1)
	MOVE.W (A0)+,8*6(A1)
	MOVE.W (A0)+,8*7(A1)
	MOVE.W (A0)+,8*8(A1)
	MOVE.W (A0)+,8*9(A1)
	MOVE.W (A0)+,8*10(A1)
	MOVE.W (A0)+,8*11(A1)
	MOVE.W (A0)+,8*12(A1)
	MOVE.W (A0)+,8*13(A1)
	MOVE.W (A0)+,8*14(A1)
	MOVE.W (A0)+,8*15(A1)
	MOVE.W D7,8*16(A1)
	LEA 160(A1),A1
	DBF D0,D0LOP
	ADDQ.L #4,(A3)
	MOVE.W (A2),D0
	BPL.S VAMONOS
	MOVE.L #WAVETBL,(A3)
VAMONOS	RTS
	
	
	
	
BOSPR	LEA BORR(PC),A0
	MOVEA.L SCREEN,A1
	MOVEQ.L #0,D1
	MOVEQ.L #0,D2
	MOVEQ.L #0,D3
	MOVEQ.L #0,D4
	MOVEQ.L #0,D5
	MOVEQ.L #0,D6
	MOVEQ.L #0,D7
	REPT NBALLS
	MOVE.W (A0)+,D0
	LEA 0(A1,D0.W),A2
	BSR BORRA
	ENDR
	RTS
BORRA	MOVEQ #0,D0
BO	MACRO LI
	MOVEM.L D0-D7,\1*160(A2)
	ENDM
BOO	MACRO LI
	BO \1
	BO (\1+1)
	BO (\1+2)
	BO (\1+3)
	BO (\1+4)
	ENDM
	BOO 0
	BOO 1*5
	BOO 2*5
	BOO 3*5
	BOO 4*5
	BOO 5*5
	BOO 6*5
	BOO 7*5
	BOO 8*5
	BOO 9*5
	BOO 10*5
	RTS
PSPRIT	LEA TRAY(PC),A0
	LEA BALL,A1
	MOVEA.L SCREEN,A2
	MOVEQ #0,D0
	MOVE.W (A0)+,D0
	LEA 0(A1,D0.L),A3
	MOVE.W (A0)+,D0
	LEA 0(A2,D0.L),A4
	BSR PSPRR
	REPT NBALLS-1
	MOVEQ #0,D0
	MOVE.W (A0)+,D0
	LEA 0(A1,D0.L),A3
	MOVE.W (A0)+,D0
	LEA 0(A2,D0.L),A4
	BSR PSPR
	ENDR
	RTS
PSPRR	
MSPRI	MACRO LI
	MOVE.L 4(A3),(\1*160)+(8*0)(A4)
	MOVE.W 8(A3),(\1*160)+(8*0)+6(A4)
	MOVE.L 14(A3),(\1*160)+(8*1)(A4)
	MOVE.W 18(A3),(\1*160)+(8*1)+6(A4)
	MOVE.L 24(A3),(\1*160)+(8*2)(A4)
	MOVE.W 28(A3),(\1*160)+(8*2)+6(A4)
	MOVE.L 34(A3),(\1*160)+(8*3)(A4)
	MOVE.W 38(A3),(\1*160)+(8*3)+6(A4)
	LEA LBAL/LINS(A3),A3
	ENDM
MMSPRI	MACRO LI
	MSPRI \1
	MSPRI (\1+1)
	MSPRI (\1+2)
	MSPRI (\1+3)
	MSPRI (\1+4)
	ENDM
	MMSPRI 0*5
	MMSPRI 1*5
	MMSPRI 2*5
	MMSPRI 3*5
	MMSPRI 4*5
	MMSPRI 5*5
	MMSPRI 6*5
	MMSPRI 7*5
	MMSPRI 8*5
	MMSPRI 9*5
	MMSPRI 10*5
	RTS
	
PSPR	
PPART	MACRO LI
	MOVEM.L \1*160(A4),D0-D7
	AND.L (A3),D0
	AND.L (A3)+,D1
	OR.L (A3)+,D0
	OR.W (A3)+,D1
	AND.L (A3),D2
	AND.L (A3)+,D3
	OR.L (A3)+,D2
	OR.W (A3)+,D3
	AND.L (A3),D4
	AND.L (A3)+,D5
	OR.L (A3)+,D4
	OR.W (A3)+,D5
	AND.L (A3),D6
	AND.L (A3)+,D7
	OR.L (A3)+,D6
	OR.W (A3)+,D7
	MOVEM.L D0-D7,\1*160(A4)
	ENDM
PART	MACRO LI
	PPART \1
	PPART (\1+1)
	PPART (\1+2)
	PPART (\1+3)
	PPART (\1+4)
	ENDM
	MOVEM.L (A4),D0-D7
	AND.L (A3),D0
	AND.L (A3)+,D1
	OR.L (A3)+,D0
	OR.W (A3)+,D1
	AND.L (A3),D2
	AND.L (A3)+,D3
	OR.L (A3)+,D2
	OR.W (A3)+,D3
	AND.L (A3),D4
	AND.L (A3)+,D5
	OR.L (A3)+,D4
	OR.W (A3)+,D5
	AND.L (A3),D6
	AND.L (A3)+,D7
	OR.L (A3)+,D6
	OR.W (A3)+,D7
	MOVEM.L D0-D7,(A4)
	PPART 1
	PPART 2
	PPART 3
	PPART 4
	PART 1*5
	PART 2*5
	PART 3*5
	PART 4*5
	PART 5*5
	PART 6*5
	PART 7*5
	PART 8*5
	PART 9*5
	PART 10*5
	RTS
PLAT	LEA TRAY+2(PC),A0
	LEA BOR(PC),A1
	MOVEQ #NBALLS-1,D0
LOKJU	MOVE.W (A0),(A1)+
	ADDQ.L #4,A0
	DBF D0,LOKJU
	LEA BOR(PC),A0
	LEA BORR(PC),A1
	MOVEQ #NBALLS-1,D0
LOJU	MOVE.W (A0)+,(A1)+
	DBF D0,LOJU
	RTS
CHGSC	LEA SCREEN(PC),A0
	MOVE.L (A0),D0
	BCHG #15,D0
	MOVE.L D0,(A0)
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	RTS
TRAY	REPT NBALLS
	DS.W 2
	ENDR
BOR	REPT NBALLS
	DC.W 0
	ENDR
BORR	REPT NBALLS
	DC.W 0
	ENDR
SINUSVAR
POSX	DC.W 0
POSY	DC.W 0
INCX	DC.W 1*2
INCY	DC.W 1*4
INDX	DC.W 23*2
INDY	DC.W 12*4
CALCTR	LEA TABLE(PC),A0
	LEA (MAX*2)(A0),A1
	LEA SINUSVAR(PC),A3
	LEA TRAY(PC),A4
	MOVE.L 2*2(A3),D1
	MOVE.L (A3),D0
	MOVE.W #MAY*4,D4
	ADD.L D1,D0
	CMP.W D0,D4
	BGT.S NOXX1
	SUB.W D4,D0
NOXX1	SWAP D0
	SWAP D4
	MOVE.W #MAX*2,D4
	CMP.W D0,D4
	BGT.S NOXX2
	SUB.W D4,D0
NOXX2	SWAP D4
	SWAP D0
	MOVE.L D0,(A3)
	MOVE.L 4*2(A3),D2
	REPT NBALLS
	BSR CLALC
	ENDR
	RTS
CLALC	ADD.L D2,D0
	CMP.W D0,D4
	BGT.S NOZONY
	SUB.W D4,D0
NOZONY	MOVE.W 0(A1,D0.W),D6
	MOVE.W 2(A1,D0.W),D5
	SWAP D0
	SWAP D4
	CMP.W D0,D4
	BGT.S NOZONX
	SUB.W D4,D0
NOZONX	ADD.W 0(A0,D0.W),D5 
	SWAP D0
	SWAP D4
	MOVE.W D6,(A4)+
	MOVE.W D5,(A4)+
	RTS
SETSIN	LEA TABLE(PC),A6
	LEA (A6),A0
	MOVE.W #MAX-1,D0
	BSR SETYSIN
	LEA (MAX*2)(A6),A0
	MOVE.W #MAY-1,D0
	BSR SETXSIN
	RTS
SETYSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	MULU #$A0,D7
	MOVE.W D7,(A0)+
	DBF D0,SETYSIN
	RTS
SETXSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	DIVU #16,D7
	MOVE.W D7,D6
	CLR.W D7
	SWAP D7
	MULU #LBAL,D7
	MOVE.W D7,(A0)+
	LSL.W #3,D6
	MOVE.W D6,(A0)+
	DBF D0,SETXSIN
	RTS
P_BALL	LEA AUG+(16*2)+(15*12*2)(PC),A0
	LEA BASE,A1
	BSR PBALL
	LEA BALL,A1
	MOVEQ #16-1,D0
ROXL	BSR SAVEIT
	BSR ROXXIT
	DBF D0,ROXL
	RTS
SAVEIT	LEA BASE,A0
	MOVEQ #LINS-1,D1
SAVELOP	REPT 4
	MOVE.W (A0),D2
	OR.W 2(A0),D2
	OR.W 4(A0),D2
	NOT.W D2
	MOVE.W D2,(A1)+
	MOVE.W D2,(A1)+
	MOVE.L (A0),(A1)+
	MOVE.W 4(A0),(A1)+
	ADDQ.L #8,A0 
	ENDR
	LEA 160-(8*4)(A0),A0
	DBF D1,SAVELOP
	RTS
ROXXIT	LEA BASE,A0
	MOVEQ #LINS-1,D1
ROXLOP1	MOVEQ #3-1,D2	
ROXLOP0	ROXR (A0)
	ROXR 8*1(A0)
	ROXR 8*2(A0)
	ROXR 8*3(A0)
	ADDQ.L #2,A0
	DBF D2,ROXLOP0
	LEA 160-(2*3)(A0),A0
	DBF D1,ROXLOP1
	RTS
PBALL	MOVEQ #0,D7
	MOVEQ #LINS-1,D0
LKJHG	REPT 3
	MOVE.L (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	MOVE.W D7,(A1)+
	ENDR
	MOVE.L D7,(A1)+
	MOVE.L D7,(A1)+
	LEA 160-(8*4)(A1),A1
	DBF D0,LKJHG
	RTS
CLS	LEA BASE,A0
	LEA BASE+$FD00,A1
JNK	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S JNK
	RTS
SETMLOG	LEA AUG+(16*2),A0
	LEA BASE,A1
	MOVEQ #12-1,D0
COLKIO	REPT 15
	MOVE.W (A0)+,(A1)
	ADDQ.L #8,A1
	ENDR
	MOVE.W #0,(A1)
	LEA 160-(15*8)(A1),A1
	DBF D0,COLKIO
	LEA LOGOR(PC),A1
	MOVEQ #16-1,D0
LKJEG	BSR SAVEL
	BSR ROTAL
	DBF D0,LKJEG
	RTS
SAVEL	LEA BASE,A0
	MOVEQ #12-1,D1
LKIO	REPT 16
	MOVE.W (A0),(A1)+
	ADDQ.L #8,A0
	ENDR
	LEA 160-(16*8)(A0),A0
	DBF D1,LKIO
	RTS
ROTAL	LEA BASE,A0
	MOVEQ #12-1,D1
	MOVE.W #0,CCR
LKIR	REPT 16
	ROXR (A0)
	ADDQ.L #8,A0
	ENDR
	LEA 160-(16*8)(A0),A0
	DBF D1,LKIR
	RTS
SETWLOG	LEA WAVETBL(PC),A0
	MOVEQ #0,D0
WAXLOK	MOVE.L (A0),D1
	CMP.L #55,D1
	BNE.S NOEND
	MOVE.L #-1,(A0)
	RTS
NOEND	ADD.L D1,D0
	MOVE.L D0,D2
	DIVU #16,D2
	LSL.W #3,D2
	MOVE.W D2,(A0)+
	CLR.W D2
	SWAP D2
	MULU #LLOGO,D2
	MOVE.W D2,(A0)+
	BRA.S WAXLOK
GFONT	LEA FNT+32(PC),A0
	LEA FONT,A2
	LEA CHARNU(PC),A3
	BSR QUITAM
	MOVEQ #0,D5
	MOVE.B (A3)+,D5
OTROM	BSR PLETRA
	MOVEQ #2-1,D6
SROTA	BSR SAVET
	BSR ROTAT
	DBF D6,SROTA
	MOVE.B (A3)+,D5
	BPL.S OTROM
	RTS
SAVET	LEA BASE,A1
	MOVE.W D5,D1
SROW	MOVEQ #SLI-1,D0
SLIN	MOVE.L (A1),(A2)+
	MOVE.W 4(A1),(A2)+
	LEA 160(A1),A1
	DBF D0,SLIN
	LEA (-160*SLI)+8(A1),A1
	DBF D1,SROW
	RTS
ROTAT	LEA BASE,A1
	MOVE.W D5,D3
	ADDQ.W #1,D3
	LSL.W #3,D3
	NEG.W D3
	ADD.W #160,D3
	MOVEQ #8-1,D7
VECESR	MOVEQ #3-1,D0
PLANOSR	MOVEQ #SLI-1,D1
LINEASR	MOVE.B #0,CCR
	MOVE.W D5,D2
ROWSR	ROXR (A1)
	ADDQ.L #8,A1
	DBF D2,ROWSR
	LEA 0(A1,D3.W),A1
	DBF D1,LINEASR
	LEA (-160*SLI)+2(A1),A1
	DBF D0,PLANOSR
	LEA BASE,A1
	DBF D7,VECESR
	RTS
PLETRA	LEA BASE,A1
	MOVE.W D5,D1
	SUBQ.W #1,D1
FLI	MOVEQ #SLI-1,D0
DLI	MOVE.L (A0)+,(A1)
	MOVE.W (A0)+,4(A1)
	LEA 160(A1),A1
	DBF D0,DLI
	LEA (-160*SLI)+8(A1),A1
	DBF D1,FLI
	MOVEQ #0,D1
	MOVEQ #SLI-1,D0
BDLI	MOVE.L D1,(A1)
	MOVE.W D1,4(A1)
	LEA 160(A1),A1
	DBF D0,BDLI
	RTS
QUITAM	LEA (A3),A4
QUITARS	MOVE.B (A4),D0
	SUB.B #"0",D0
	MOVE.B D0,(A4)+
	MOVE.B (A4),D0
	BPL.S QUITARS
	RTS
DTEXT	LEA TEXT(PC),A0
	LEA MTEXT(PC),A1
	LEA CHARCH(PC),A2
	LEA CHARNU(PC),A3
	MOVE.B (A0)+,D0
SETTXT	BSR SEARCH
	BSR CALCIN
	MOVE.W D2,(A1)+
	MOVE.W D0,(A1)+
	MOVE.B (A0)+,D0
	BPL.S SETTXT
	MOVE.L #0,(A1)
	RTS
SEARCH	LEA (A2),A4
	MOVE.W #-1,D1
SRCHLOP	MOVE.B (A4)+,D2
	BMI.S FUERA
	ADDQ.W #1,D1
	CMP.B D0,D2
	BNE.S SRCHLOP
FUERA	RTS
CALCIN	LEA (A3),A5
	MOVEQ #0,D0
	MOVEQ #0,D2
	MOVEQ #0,D4
	MOVE.B 0(A3,D1.W),D2
	SUBQ.W #1,D1
	TST.W D1
	BMI.S ESEL0
SUMALOP	MOVE.B (A5)+,D4
	ADDQ.B #1,D4
	ADD.W D4,D0
	DBF D1,SUMALOP
ESEL0	MULU #6*SLI*2,D0
	RTS
TSC	MOVEA.L SCREEN,A3
	LEA 160*(200-SLI)(A3),A3
	BSR BARRE
	LEA CTEXT(PC),A0
	MOVEA.L (A0),A1
	LEA FONT,A2 
	MOVE.W #6*SLI,D3
	EOR.W D3,4(A0)
	BEQ.S AUMROW
	ADDQ.W #1,6(A0)
AUMROW	MOVEQ #0,D0
	MOVEQ #0,D2
	MOVE.W 6(A0),D0
	MOVE.W 8(A0),D1
	MOVE.W 4(A0),D2
	CMP.W D1,D0
	BEQ.S OTRAL
	ADDQ.W #1,D1
	MULU D2,D1
	MULU D3,D0
	ADD.W D1,D0
	ADD.W 2(A1),D0
	LEA 0(A2,D0.L),A4
	BSR PRTROW
	RTS
OTRAL	EOR.W D3,10(A0)
	ADDQ.L #4,(A0)
SOLO1	ADDQ.W #1,D1
	MULU D2,D1
	MULU D3,D0
	ADD.W D1,D0
	ADD.W 2(A1),D0
	LEA 0(A2,D0.L),A4
	BSR PRTROW
	MOVE.W 4(A1),D0
	MOVE.W D0,8(A0)
	ADDQ.W #1,D0
	MULU D2,D0
	ADD.W 6(A1),D0
	CLR.W 6(A0)
	LEA 0(A2,D0.L),A4
	BSR ORROW
	MOVE.W 8(A1),D0
	BNE.S ENSANEM
	MOVE.L #MTEXT,(A0)
	LEA MAXROW(PC),A0
	MOVE.W MTEXT(PC),(A0)
	CLR.W -2(A0)
ENSANEM	RTS
PRTROW	
PLIN	MACRO LI
	MOVE.L (A4)+,(\1*160)(A3)
	MOVE.W (A4)+,(\1*160)+4(A3)
	ENDM
	PLIN 0
	PLIN 1
	PLIN 2
	PLIN 3
	PLIN 4
	PLIN 5
	PLIN 6
	PLIN 7
	PLIN 8
	PLIN 9
	PLIN 10
	PLIN 11
	PLIN 12
	PLIN 13
	PLIN 14
	PLIN 15
	PLIN 16
	PLIN 17
	PLIN 18
	PLIN 19
	PLIN 20
	PLIN 21
	PLIN 22
	PLIN 23
	PLIN 24
	RTS
ORROW	
LIN	MACRO LI
	MOVE.L (\1*160)(A3),D0
	OR.L (A4)+,D0
	MOVE.L D0,(\1*160)(A3)
	MOVE.W (\1*160)+4(A3),D0
	OR.W (A4)+,D0
	MOVE.W D0,(\1*160)+4(A3)
	ENDM
	LIN 0
	LIN 1
	LIN 2
	LIN 3
	LIN 4
	LIN 5
	LIN 6
	LIN 7
	LIN 8
	LIN 9
	LIN 10
	LIN 11
	LIN 12
	LIN 13
	LIN 14
	LIN 15
	LIN 16
	LIN 17
	LIN 18
	LIN 19
	LIN 20
	LIN 21
	LIN 22
	LIN 23
	LIN 24
	RTS
BARRE	ADDQ.L #8,A3
	REPT SLI
	MOVEM.L (A3)+,D0-D7/A0-A2/A4-A6
	MOVEM.L D0-D7/A0-A2/A4-A6,-8-(14*4)(A3)
	MOVEM.L (A3)+,D0-D7/A0-A2/A4-A6
	MOVEM.L D0-D7/A0-A2/A4-A6,-8-(14*4)(A3)
	MOVEM.L (A3)+,D0-D7/A0-A2/A4
	MOVEM.L D0-D7/A0-A2/A4,-8-(12*4)(A3)
	ENDR
	LEA (-160*SLI)+18*8(A3),A3
	RTS
PREPALL	MOVEQ #2,D0
	BSR MUSIX
	LEA MAXROW(PC),A0
	MOVE.W MTEXT(PC),(A0)
	MOVE.W #-1,-2(A0)
	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	RTS
REPALL	MOVEM.L OLDCOL(PC),D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	LEA $FFFF8800.W,A0
	MOVE.L #$8080000,(A0)
	MOVE.L #$9090000,(A0)
	MOVE.L #$A0A0000,(A0)
	RTS
SINT	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L $70.W,(A0)+
	MOVE.L $120.W,(A0)+
	MOVE.B 7(A1),(A0)+
	MOVE.B 9(A1),(A0)+
	MOVE.B $13(A1),(A0)+
	MOVE.L #VBL,$70.W
	MOVE.L #HBL,$120.W
	CLR.B 7(A1)
	CLR.B 9(A1)
	MOVE.B #1,7(A1)
	MOVE.B #1,$13(A1)
	MOVE.W #$2300,SR
	RTS
GINT	CLR.B SYNC
VVSYNC	TST.B SYNC
	BEQ.S VVSYNC
	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.B (A0)+,7(A1)
	MOVE.B (A0)+,9(A1)
	MOVE.B (A0)+,$13(A1)
	MOVE.W #$2300,SR
	RTS
VBL	ST SYNC
	CLR.B $FFFFFA1B.W
	MOVE.B #2,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #HBL,$120.W
	RTE
HBL	MOVE.W D0,-(A7)
IND1	MOVE.W C1+2,D0
	BMI.S CFUERA
	MOVE.W D0,$FFFF8240+(CV*2).W
	MOVE.W (A7)+,D0
	ADDQ.L #2,IND1+2
	BCLR #0,$FFFFFA0F.W
	RTE
CFUERA	MOVE.W (A7)+,D0
	CLR.B $FFFFFA1B.W
	MOVE.B #200-25-14,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #CHBL,$120.W
	BCLR #0,$FFFFFA0F.W
	RTE
CHBL	MOVEM.L A0-A1,-(A7)
	LEA FNT+2(PC),A0
	LEA $FFFF8240+2.W,A1 
	MOVE.W (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0),(A1)
	MOVEM.L (A7)+,A0-A1
	CLR.B $FFFFFA1B.W
	BCLR #0,$FFFFFA0F.W
	RTE
C1	DC.W $700,$710,$720,$730,$740,$750
	DC.W $770
	DC.W -1
INTS	DS.L 2
	DS.B 3
SYNC	DS.B 1
CHARCH	DC.B 'ABCDEFGHIJKLMNOPQRSTUVWXYZ!?:;0123456789"(),-.',"' ",-1
CHARNU	DC.B '222222222222222222222222222222222222222222222221',-1
TEXT	DC.B " HELLO AND WELCOME TO THE TSC SCROLL "
	DC.B -1
MTEXT	DS.L MTEXT-TEXT
CTEXT	DC.L MTEXT
INDSUM	DS.W 1
ROWNOW	DS.W 1
MAXROW	DS.W 1
ONETWO	DS.W 1
SCREEN	DC.L BASE
OLDCOL	DS.W 16
COLS1	DC.W $000,$110,$211,$322,$070,$110,$211,$322
	DC.W $433,$544,$655,$766,$433,$544,$655,$766
CWAVE	DC.L WAVETBL
WAVETBL	DC.L 0,0,1,1,1,1,2,2,2,2,2,2,3,3,3,3,3,3,3,3,2,2,2,2,2,2,1,1,1,1,0,0
	DC.L 0,0,-1,-1,-1,-1,-2,-2,-2,-2,-2,-2,-3,-3,-3,-3,-3,-3,-3,-3,-2,-2,-2,-2,-2,-2,-1,-1,-1,-1,0,0
	DC.L 55
MUSIX	INCBIN 'MUSIC.TEC'
TABLE	INCBIN 'AUGSIN.RED'
AUG	INCBIN 'AUG.BIN'
FNT	INCBIN 'FONT.BIN'
ENDP
LOGOR	EQU ENDP
BALL	EQU ENDP+(LLOGO*16)
FONT	EQU ENDP+(LLOGO*16)+LBAL*16
	