PI	EQU $2000
GRAD	EQU PI/4
MAX	EQU PI/2
MAY	EQU PI/4
LBAL	EQU 16*2*4*2
NBALLS	EQU 50
BASE 	EQU $F0000
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,A7
	BSR SETSIN
	BSR P_BALL
	BSR CLS
ME	MOVE.W #$25,-(A7)
	TRAP #14
	ADDQ.L #2,A7
	MOVEM.L BALLP,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	*MOVE.W #$700,$FFFF8240.W
	BSR INPUTK
	BSR CHGSC
	BSR BOSPR
	BSR PLAT
	BSR CALCTR
	BSR PSPRIT
	*MOVE.W #$777,$FFFF8240.W
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	CLR.L -(A7)
	TRAP #1
PLAT	LEA TRAY+2(PC),A0
	LEA BOR(PC),A1
	MOVEQ #NBALLS-1,D0
LOKJU	MOVE.W (A0),(A1)+
	ADDQ.L #4,A0
	DBF D0,LOKJU
	LEA BOR(PC),A0
	LEA BORR(PC),A1
	MOVEQ #NBALLS-1,D0
LOJU	MOVE.W (A0)+,(A1)+
	DBF D0,LOJU
	RTS
CHGSC	LEA SCREEN(PC),A0
	MOVE.L (A0),D0
	BCHG #15,D0
	MOVE.L D0,(A0)
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	RTS
BOSPR	MOVEA.L SCREEN,A1
	MOVEQ #0,D1
	LEA BORR(PC),A2
	MOVEQ #NBALLS-1,D7
CRLOP	MOVE.W (A2)+,D0
	LEA 0(A1,D0.W),A4
PBOR	MACRO LI
	MOVE.L D1,(\1*160)(A4)
	MOVE.L D1,8+(\1*160)(A4)
	ENDM
	PBOR 0
	PBOR 1
	PBOR 2
	PBOR 3
	PBOR 4
	PBOR 5
	PBOR 6
	PBOR 7
	PBOR 8
	PBOR 9
	PBOR 10
	PBOR 11
	PBOR 12
	PBOR 13
	PBOR 14
	*ADDQ.L #4,A2
	DBF D7,CRLOP
	RTS	
PSPRIT	MOVEA.L SCREEN,A1
	LEA BALL(PC),A0
	LEA TRAY(PC),A2
	MOVEQ #NBALLS-1,D7
BALLOP	MOVE.W (A2)+,D0
	LEA 0(A0,D0.W),A3
	MOVE.W (A2)+,D0
	LEA 0(A1,D0.W),A4
PSP	MACRO LI
	MOVE.L (160*\1)(A4),D0
	MOVE.L 8+(160*\1)(A4),D1
	AND.L (A3)+,D0
	AND.L (A3)+,D1
	OR.L (A3)+,D0
	OR.L (A3)+,D1
	MOVE.L D0,(160*\1)(A4)
	MOVE.L D1,8+(160*\1)(A4)
	ENDM
	PSP 0
	PSP 1
	PSP 2
	PSP 3
	PSP 4
	PSP 5
	PSP 6
	PSP 7
	PSP 8
	PSP 9
	PSP 10
	PSP 11
	PSP 12
	PSP 13
	PSP 14
	DBF D7,BALLOP
	RTS
INPUTK	MOVE.B $FFFFFC02.W,D0
	LEA FUNC(PC),A0
SEARCHX	MOVE.W (A0)+,D1
	BPL.S NVETE
	RTS
NVETE	ADDQ.L #4,A0
	CMP.B D0,D1
	BNE.S SEARCHX
	MOVE.L -4(A0),A0
	LEA SINUSVAR(PC),A1
	JMP (A0)
FUNC	
KEU	MACRO T,F
	DC.W \1
	DC.L \2
	ENDM
	KEU $3B,SUXP
	KEU $3C,REXP
	KEU $3D,SUXI
	KEU $3E,REXI
	KEU $3F,SUYP
	KEU $40,REYP
	KEU $41,SUYI
	KEU $42,REYI
	KEU $43,SUCP
	KEU $44,RECP
	KEU $62,SUCI
	KEU $61,RECI
	DC.W -1
SUXP	ADDQ.W #4,3*2(A1)
	RTS
REXP	SUBQ.W #4,3*2(A1)
	RTS
SUXI	ADDQ.W #4,8*2(A1)
	RTS
REXI	SUBQ.W #4,8*2(A1)
	RTS
SUYP	ADDQ.W #2,2*2(A1)
	RTS
REYP	SUBQ.W #2,2*2(A1)
	RTS
SUYI	ADDQ.W #2,7*2(A1)
	RTS
REYI	SUBQ.W #2,7*2(A1)
	RTS
SUCP	ADDQ.W #4,5*2(A1)
	RTS
RECP	SUBQ.W #4,5*2(A1)
	RTS
SUCI	ADDQ.W #4,6*2(A1)
	RTS
RECI	SUBQ.W #4,6*2(A1)
	RTS
SINUSVAR
POSX	DC.W 0
POSY	DC.W 0
INCX	DC.W 0*2
INCY	DC.W 0*4
POCC	DC.W 0
INKC	DC.W 0*4
INDC	DC.W 0*4
INDX	DC.W 0*2
INDY	DC.W 0*4
TRAY	REPT NBALLS
	DS.W 2
	ENDR
BOR	REPT NBALLS
	DC.W 0
	ENDR
BORR	REPT NBALLS
	DC.W 0
	ENDR
CALCTR	LEA TABLE(PC),A0
	LEA (MAX*2)(A0),A1
	LEA (MAY*4)(A1),A2
	LEA SINUSVAR(PC),A4
	LEA TRAY(PC),A5
	MOVE.L 2*2(A4),D1
	MOVE.L (A4),D0
	MOVE.W #(PI-1),D4
	SWAP D4
	MOVE.W #(PI-1),D4
	ADD.W D1,D0
	SWAP D0
	SWAP D1
	ADD.W D1,D0
	SWAP D0
	SWAP D1
	AND.L D4,D0
	MOVE.L D0,(A4)
	MOVE.W 5*2(A4),D3
	MOVE.W 4*2(A4),D1
	ADD.W D3,D1
	AND.W D4,D1
	MOVE.W D1,4*2(A4)
	MOVE.L 7*2(A4),D2
	MOVE.W 6*2(A4),D3
	MOVE.W #LBAL*16,D7
	REPT NBALLS
	BSR CLALC
	ENDR
	RTS
CLALC	ADD.W D2,D0
	AND.W D4,D0
	MOVE.W 0(A1,D0.W),D6
	MOVE.W 2(A1,D0.W),D5
	SWAP D0
	SWAP D2
	ADD.W D2,D0
	AND.W D4,D0
	ADD.W 0(A0,D0.W),D5 
	SWAP D2
	SWAP D0
	ADD.W D3,D1
	AND.W D4,D1
	CMP.W D1,D7
	BGT.S NSUC
	SUB.W D7,D1
NSUC	ADD.W 0(A2,D1.W),D6
	ADD.W 2(A2,D1.W),D5
	CMP.W D6,D7
	BGT.S NORES
	SUB.W D7,D6
	ADDQ.W #8,D5
NORES	MOVE.W D6,(A5)+
	MOVE.W D5,(A5)+
	RTS
SETSIN	LEA TABLE(PC),A6
	LEA (A6),A0
	MOVE.W #MAX-1,D0
	BSR SETYSIN
	LEA (MAX*2)(A6),A0
	MOVE.W #MAY-1,D0
	BSR SETXSIN
	LEA (MAX*2)+(MAY*4)(A6),A0
	MOVE.W #GRAD-1,D0
	BSR S_XYSIN
	RTS
SETYSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	MULU #$A0,D7
	MOVE.W D7,(A0)+
	DBF D0,SETYSIN
	RTS
SETXSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	DIVU #16,D7
	MOVE.W D7,D6
	CLR.W D7
	SWAP D7
	MULU #LBAL,D7
	MOVE.W D7,(A0)+
	LSL.W #3,D6
	MOVE.W D6,(A0)+
	DBF D0,SETXSIN
	RTS
S_XYSIN	MOVEQ #0,D7
	MOVE.W (A0),D7
	DIVU #16,D7
	MOVE.W D7,D6
	CLR.W D7
	SWAP D7
	MULU #LBAL,D7
	MOVE.W D7,(A0)+
	MULU #8,D6
	MOVEQ #0,D7
	MOVE.W (A0),D7
	MULU #160,D7
	ADD.W D7,D6
	MOVE.W D6,(A0)+
	DBF D0,S_XYSIN
	RTS
P_BALL	LEA BALLP+32(PC),A0
	LEA BASE,A1
	MOVEQ #0,D7
	MOVEQ #16-1,D0
DOKLP	MOVE.L (A0)+,(A1)
	MOVE.L D7,8(A1)
	LEA 160(A1),A1
	DBF D0,DOKLP
	LEA BALL(PC),A1
	MOVEQ #16-1,D0
PBALL	BSR SAVEB
	BSR ROTAB
	DBF D0,PBALL
	RTS
SAVEB	LEA BASE,A0
	MOVEQ #16-1,D7
SALVA	MOVE.L (A0),D1
	MOVE.L 8(A0),D2
	MOVE.W D1,D3
	SWAP D1
	OR.W D1,D3
	NOT.W D3
	MOVE.W D3,(A1)+
	MOVE.W D3,(A1)+
	MOVE.W D2,D3
	SWAP D2
	OR.W D2,D3
	NOT.W D3
	MOVE.W D3,(A1)+
	MOVE.W D3,(A1)+
	SWAP D1
	SWAP D2
	MOVE.L D1,(A1)+
	MOVE.L D2,(A1)+
	LEA 160(A0),A0
	DBF D7,SALVA
	RTS
ROTAB	LEA BASE,A0
	MOVEQ #16-1,D7
LINES	MOVEQ #2-1,D6
PLANES	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8(A0)
	ADDQ.L #2,A0
	DBF D6,PLANES
	LEA 160-(2*2)(A0),A0
	DBF D7,LINES
	RTS
CLS	LEA BASE,A0
	LEA BASE+($8000)+($7D00),A1
LKJ	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S LKJ
	RTS
SCREEN	DC.L BASE
TABLE	INCBIN 'MEGASIN.RED'
BALLP	INCBIN 'BALL.BIN'
BALL	
	