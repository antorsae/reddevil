BASE 	EQU $F8000
VELPAL	EQU 4
	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 6(A7),A7
	BSR CLS
	BSR STEXT
	BSR ROTY
	BSR ST_CONNEXION
	BSR PCOLS
	BSR PPICT		 
	MOVEM.L PICTURE+2,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	BSR SINT
ME	CLR.B SYNC
VSYNC	TST.B SYNC
	BEQ.S VSYNC
	CMP.B #$39,$FFFFFC02.W
	BNE.S ME
	BSR GINT
	MOVEM.L OLDCOL(PC),D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CLR.L -(A7)
	TRAP #1
CHNGPAL	LEA TIMEPAL(PC),A2
	SUBQ.W #1,(A2)
	BPL.S NOCHNG
	LEA DIRPAL(PC),A0
	ADD.L #(8*2)+2,(A0)
	MOVEA.L (A0),A1
	MOVE.W (A1)+,D0
	BPL.S NOENDP
	LEA PALS(PC),A1
	MOVE.L A1,(A0)
	MOVE.W (A1),D0
NOENDP	MOVE.W D0,(A2) 
	MOVEM.L (A1),D0-D3
	MOVEM.L D0-D3,COLNO
NOCHNG	RTS
PUTCOLS	LEA PICTURE,A0
	LEA $FFFF8240.W,A1
	MOVEM.L (A0),D0-D7
	MOVE.W D0,(A1)+
	MOVE.W D0,(A1)+
	MOVE.W D1,(A1)+
	MOVE.W D1,(A1)+
	MOVE.W D2,(A1)+
	MOVE.W D2,(A1)+
	MOVE.W D3,(A1)+
	MOVE.W D3,(A1)+
	MOVE.W D4,(A1)+
	MOVE.W D4,(A1)+
	MOVE.W D5,(A1)+
	MOVE.W D5,(A1)+
	MOVE.W D6,(A1)+
	MOVE.W D6,(A1)+
	MOVE.W D7,(A1)+
	MOVE.W D7,(A1)
	RTS
	
CTEXT	DC.L ZTEXT
VEL	EQU 2 ;(POSIBLES 1,2,4,8)
PAS	DC.W 7*4*(8-VEL)
SCLL	MOVEA.L CTEXT,A4
	MOVE.W PAS,D6
	MOVE.L (A4)+,A5
	LEA 0(A5,D6.W),A5
	MOVEA.L SCREEN,A6
	BSR PM1
	MOVE.L (A4)+,A5
	LEA 0(A5,D6.W),A5
	MOVEA.L SCREEN,A6
	BSR PO1
	MOVEQ #0,D0
	BSR LLEZ
	
	REPT 18
	ADDQ.W #8,D0
	BSR LLEZ
	ENDR
	
	ADDQ.W #8,D0
	MOVE.L (A4)+,A5
	LEA 0(A5,D6.W),A5
	MOVEA.L SCREEN,A6
	LEA 0(A6,D0.W),A6
	BSR PO19
	SUB.W #7*4*VEL,PAS
	BPL.S NO_VUL
	MOVE.W #7*4*(8-VEL),PAS
	ADDQ.L #4,CTEXT
	MOVE.L (A4),D6
	BNE.S NO_VUL
	MOVE.L #ZTEXT,CTEXT
NO_VUL	RTS 
LLEZ	MOVE.L (A4)+,A5
	LEA 0(A5,D6.W),A5
	MOVEA.L SCREEN,A6
	LEA 0(A6,D0.W),A6
	BSR PM
	MOVE.L (A4)+,A5
	LEA 0(A5,D6.W),A5
	ADDQ.W #8,A6
	BSR PO1
	RTS
PM	MOVEM.L (7*4*8)(A5),D1-D5/D7
	MOVE.W D1,8+(160*0)(A6)
	SWAP D1
	OR.W D1,(A6)
	MOVE.W D2,8+(160*1)(A6)
	SWAP D2
	OR.W D2,160*1(A6)
	MOVE.W D3,8+(160*2)(A6)
	SWAP D3
	OR.W D3,160*2(A6)
	MOVE.W D4,8+(160*3)(A6)
	SWAP D4
	OR.W D4,160*3(A6)
	MOVE.W D5,8+(160*4)(A6)
	SWAP D5
	OR.W D5,160*4(A6)
	MOVE.W D7,8+(160*5)(A6)
	SWAP D7
	OR.W D7,160*5(A6)
	MOVE.W (7*4*8)+(4*6)+2(A5),8+(160*6)(A6)
	MOVE.W (7*4*8)+(4*6)(A5),D7
	OR.W D7,160*6(A6)
	RTS	
PO1	MOVE.W (A5),D7
	OR.W D7,(A6)
	MOVE.W (4*1)(A5),D7
	OR.W D7,160*1(A6)
	MOVE.W (4*2)(A5),D7
	OR.W D7,160*2(A6)
	MOVE.W (4*3)(A5),D7
	OR.W D7,160*3(A6)
	MOVE.W (4*4)(A5),D7
	OR.W D7,160*4(A6)
	MOVE.W (4*5)(A5),D7
	OR.W D7,160*5(A6)
	MOVE.W (4*6)(A5),D7
	OR.W D7,160*6(A6)
	RTS
PO19	MOVE.W (2*0)+(7*4*8)(A5),D7
	OR.W D7,(A6)
	MOVE.W (2*2)+(7*4*8)(A5),D7
	OR.W D7,160*1(A6)
	MOVE.W (2*4)+(7*4*8)(A5),D7
	OR.W D7,160*2(A6)
	MOVE.W (2*6)+(7*4*8)(A5),D7
	OR.W D7,160*3(A6)
	MOVE.W (2*8)+(7*4*8)(A5),D7
	OR.W D7,160*4(A6)
	MOVE.W (2*10)+(7*4*8)(A5),D7
	OR.W D7,160*5(A6)
	MOVE.W (2*12)+(7*4*8)(A5),D7
	OR.W D7,160*6(A6)
	RTS
PM1	MOVE.W 2+(7*4*8)(A5),(A6)
	MOVE.W 2+(4*1)+(7*4*8)(A5),160*1(A6)
	MOVE.W 2+(4*2)+(7*4*8)(A5),160*2(A6)
	MOVE.W 2+(4*3)+(7*4*8)(A5),160*3(A6)
	MOVE.W 2+(4*4)+(7*4*8)(A5),160*4(A6)
	MOVE.W 2+(4*5)+(7*4*8)(A5),160*5(A6)
	MOVE.W 2+(4*6)+(7*4*8)(A5),160*6(A6)
	RTS	
CLS	LEA BASE,A0
	MOVE.W #(32000/8)-1,D0
KKLL	CLR.L (A0)+
	CLR.L (A0)+
	DBF D0,KKLL
	RTS
ROTY	LEA SFONT,A6
	MOVE.W #80-1,D7
GETTL	BSR CLD
	MOVE.W #80-1,D0
	SUB.W D7,D0
	LEA FONT,A0
	LEA BASE,A1
	BSR TODOL
	MOVE.W #16-1,D6
SALB	BSR SALVA
	BSR ROTA1
	DBF D6,SALB
	DBF D7,GETTL
	RTS
CLD	LEA BASE,A0
	CLR.W (A0)
	CLR.W 160*1(A0)
	CLR.W 160*2(A0)
	CLR.W 160*3(A0)
	CLR.W 160*4(A0)
	CLR.W 160*5(A0)
	CLR.W 160*6(A0)
	CLR.W 8(A0)
	CLR.W 8+(160*1)(A0)
	CLR.W 8+(160*2)(A0)
	CLR.W 8+(160*3)(A0)
	CLR.W 8+(160*4)(A0)
	CLR.W 8+(160*5)(A0)
	CLR.W 8+(160*6)(A0)
	RTS
SALVA	LEA BASE,A0
	MOVE.W (A0),(A6)+
	MOVE.W 8(A0),(A6)+
	MOVE.W 160*1(A0),(A6)+
	MOVE.W 8+(160*1)(A0),(A6)+
	MOVE.W 160*2(A0),(A6)+
	MOVE.W 8+(160*2)(A0),(A6)+
	MOVE.W 160*3(A0),(A6)+
	MOVE.W 8+(160*3)(A0),(A6)+
	MOVE.W 160*4(A0),(A6)+
	MOVE.W 8+(160*4)(A0),(A6)+
	MOVE.W 160*5(A0),(A6)+
	MOVE.W 8+(160*5)(A0),(A6)+
	MOVE.W 160*6(A0),(A6)+
	MOVE.W 8+(160*6)(A0),(A6)+
	RTS
ROTA1	LEA BASE,A0
	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8(A0)
	ROXR 160(A0)
	ROXR 8+160(A0)
	ROXR 160*2(A0)
	ROXR 8+(160*2)(A0)
	ROXR 160*3(A0)
	ROXR 8+(160*3)(A0)
	ROXR 160*4(A0)
	ROXR 8+(160*4)(A0)
	ROXR 160*5(A0)
	ROXR 8+(160*5)(A0)
	ROXR 160*6(A0)
	ROXR 8+(160*6)(A0)
	RTS
TODOL	MULU #8,D0
	LEA 0(A0,D0.W),A0
COPA	MOVE.B (A0)+,(A1)
	MOVE.B (A0)+,160*1(A1)
	MOVE.B (A0)+,160*2(A1)
	MOVE.B (A0)+,160*3(A1)
	MOVE.B (A0)+,160*4(A1)
	MOVE.B (A0)+,160*5(A1)
	MOVE.B (A0)+,160*6(A1)
	MOVE.B (A0)+,160*7(A1)
	RTS
STEXT	LEA TEXT,A0
	LEA ZTEXT,A1
TERR	MOVEQ #0,D0
	MOVE.B (A0)+,D0
	BEQ.S YAUNEG
	BSR SSER
	MULU #7*4*16,D0
	ADD.L #SFONT,D0
	MOVE.L D0,(A1)+
	BRA.S TERR
YAUNEG	CLR.L (A1)
	RTS
SSER	LEA CH,A2
	MOVEQ #-1,D1
SCLOP	MOVE.B (A2)+,D2
	BMI.S NFOUND
	ADDQ.W #1,D1
	CMP.B D0,D2
	BNE.S SCLOP
NFOUND	MOVEQ #0,D0
	MOVE.W D1,D0
	RTS
PPICT	LEA PICTURE+34,A0
	LEA BASE,A1
	MOVE.W #(32000/8)-1,D0
LOK	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D0,LOK
	RTS
PCOLS	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	LEA PALS(PC),A0
	MOVE.L A0,DIRPAL
	MOVE.W (A0)+,TIMEPAL
	MOVEM.L (A0),D0-D3
	MOVEM.L D0-D3,COLNO
	RTS
SINT 	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L $70.W,(A0)+
	MOVE.L $120.W,(A0)+
	MOVE.L $134.W,(A0)+
	MOVE.B 7(A1),(A0)+
	MOVE.B 9(A1),(A0)+
	MOVE.B $13(A1),(A0)+
	MOVE.B $17(A1),(A0)+
	MOVE.B $19(A1),(A0)+
	MOVE.B $1F(A1),(A0)+
	MOVE.B $FFFF8203.W,(A0)+
	MOVE.B $FFFF8201.W,(A0)+
	CLR.B 7(A1)
	CLR.B 9(A1)
	MOVE.L #SOUNDCHIP,$134.W
	MOVE.L #VBL,$70.W
	MOVE.L #HBL,$120.W
	MOVE.B #$12,$FFFFFC02.W
	BCLR.B #$3,$17(A1)
	CLR.B $19(A1)
	MOVE.B #FRQ_DATA,$1F(A1)
	MOVE.B #1,$19(A1)
	MOVEQ #33,D0
	MOVE.B D0,7(A1)
	MOVE.B D0,$13(A1)
	MOVE.W #$2300,SR
	RTS
GINT 	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$120.W
	MOVE.L (A0)+,$134.W
	MOVE.B (A0)+,7(A1)
	MOVE.B (A0)+,9(A1)
	MOVE.B (A0)+,$13(A1)
	MOVE.B (A0)+,$17(A1)
	MOVE.B (A0)+,$19(A1)
	MOVE.B (A0)+,$1F(A1)
	MOVE.B (A0)+,$FFFF8203.W
	MOVE.B (A0)+,$FFFF8201.W
	MOVE.B #8,$FFFFFC02.W
	MOVE.W #$2300,SR
	RTS
VBL	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVEM.L D0-D7/A0-A6,-(A7)
	BSR PUTCOLS
	MOVE.W COLNO,$FFFF8242.W
	MOVE.L #COLNO+2,HBL+2
	BSR CHNGPAL
	BSR ST_CONNEXION+8
	BSR SCLL
	MOVEM.L (A7)+,D0-D7/A0-A6
	ST SYNC
	RTE
HBL	MOVE.W COLNO,$FFFF8242.W
	ADDQ.L #2,HBL+2
	CMP.L #COLNO+(2*7),HBL+2
	BNE.S NEN
	CLR.B $FFFFFA1B.W
	MOVEM.L A0-A1,-(A7)
	LEA PICTURE+2,A0
	LEA $FFFF8240.W,A1
	MOVE.W (A0),(A1)
	MOVE.L 4*1(A0),4*1(A1)
	MOVE.L 4*2(A0),4*2(A1)
	MOVE.L 4*3(A0),4*3(A1)
	MOVE.L 4*4(A0),4*4(A1)
	MOVE.L 4*5(A0),4*5(A1)
	MOVE.L 4*6(A0),4*6(A1)
	MOVE.L 4*7(A0),4*7(A1)
	MOVE.L (A7)+,A0
	MOVE.L (A7)+,A1
NEN	BCLR #0,$FFFFFA0F.W
	RTE
ST_CONNEXION	BRA	INIT
		BRA	END
		BRA	PLAY
		DC.L	0
		DC.W	0
INIT	LEA ST_CONNEXION(PC),A0
	LEA MUSIC,A1
	MOVE.L A1,12(A0)
	LEA (A1),A0
	LEA	OFFSET(PC),A1
	MOVE.W	#$1D8,POS(A1)
	MOVE.W	#$258,PATT(A1)
	MOVE.W	#$F,NBR(A1)
	CMP.L	#'M.K.',$438(A0)
	BNE.S	NO_NEW_MODULES
	MOVE.W	#$3B8,POS(A1)
	MOVE.W	#$43C,PATT(A1)
	MOVE.W	#$1F,NBR(A1)
NO_NEW_MODULES
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	LEA	OFFSET(PC),A1
	ADD.W	POS(A1),A0
	MOVEQ	#$7F,D0
	MOVEQ	#0,D1
.LOOP1	MOVE.L	D1,D2
	SUBQ.W	#1,D0
.LOOP2	MOVE.B	(A0)+,D1
	CMP.B	D2,D1
	BGT.S	.LOOP1
	DBF	D0,.LOOP2
	ADDQ.B	#1,D2
	SWAP	D2
	LSR.L	#6,D2
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	ADD.L	A0,D2
	LEA	$14(A0),A0
	LEA	OFFSET(PC),A1
	MOVE.W	NBR(A1),D7
	ADD.W	PATT(A1),D2
	MOVE.L	D2,A2
	LEA	SAMPLESTARTS(PC),A1
	LEA	(A1),A3
	MOVEQ	#$1E,D6
.LOOP3	CLR.L	(A3)+
	DBRA	D6,.LOOP3
	SUBQ.W	#1,D7
.LOOP4	CLR.L	(A2)
	MOVE.L	A2,(A1)+
	MOVEQ	#0,D1
	MOVE.W	22(A0),D1
	LSL.L	#1,D1
	MOVE.L	A2,A5
	ADD.L	D1,A2
	MOVE.L	A2,(A1)
	LEA	(A2),A6
	MOVE.W	22(A0),D6
	BEQ.S	.NO_REVERSE
	SUBQ.W	#1,D6
.LOOP5	MOVE.B	(A5),D2
	MOVE.B	-1(A6),D3
	EOR.B	#$80,D2
	EOR.B	#$80,D3
	MOVE.B	D3,(A5)+
	MOVE.B	D2,-(A6)
	DBRA	D6,.LOOP5
.NO_REVERSE	
	CMP.W	#$0001,28(A0)
	BNE.S	.LOOP6
	CLR.W	28(A0)
.LOOP6	LEA	$1E(A0),A0
	DBRA	D7,.LOOP4
	LEA	CHIP(PC),A0
	LEA	NO_INS(PC),A1
	MOVEQ	#$7,D0
.LOOP7	MOVE.L	A1,(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D0,.LOOP7
	LEA	VOICES(PC),A0
	MOVEQ	#$1C-1,D0
.LOOP11	CLR.L	(A0)+
	DBRA	D0,.LOOP11
	LEA	OFFSET(PC),A0
	MOVE.B	#$6,SPEED(A0)
	CLR.B	SONGPOS(A0)
	CLR.B	COUNTER(A0)
	CLR.W	PATTPOS(A0)
	LEA	DIGIT_SWAP(PC),A0
	LEA	(DIGIT+1000)(PC),A1
	MOVE.L	A1,(A0)
	LEA	DIGIT(PC),A1
	MOVE.L	A1,4(A0)
	MOVE.W	#1000-1,D0
.LOOP8	MOVE.W	#$400,(A1)+
	DBRA	D0,.LOOP8
	LEA	YET(PC),A0
	CMP.L	#'CNX',(A0)
	BEQ.S	.NO_CHANGE
	LEA	SOUND(PC),A0
	MOVE.W	#255,D0
.LOOP9	MOVE.B	5(A0),6(A0)
	MOVE.B	5(A0),7(A0)
	MOVE.B	4(A0),5(A0)
	LEA	8(A0),A0
	DBRA	D0,.LOOP9
	LEA	VOLUME,A0
	MOVE.W	#$3FFF,D0
.LOOP10	MOVE.B	(A0),D1
	LSR.B	#2,D1
	MOVE.B	D1,(A0)+
	DBRA	D0,.LOOP10
	LEA	YET(PC),A0
	MOVE.L	#'CNX',(A0)
.NO_CHANGE
END	LEA $FFFF8800.W,A0
	MOVE.L	#$00000000,(A0)
	MOVE.L	#$01010000,(A0)
	MOVE.L	#$02020000,(A0)
	MOVE.L	#$03030000,(A0)
	MOVE.L	#$04040000,(A0)
	MOVE.L	#$05050000,(A0)
	MOVE.L	#$06060000,(A0)
	MOVE.L	#$0707FFFF,(A0)
	MOVE.L	#$08080000,(A0)
	MOVE.L	#$09090000,(A0)
	MOVE.L	#$0A0A0000,(A0)
	RTS
PLAY	MOVE.W #$2700,SR
	LEA	DIGIT_SWAP(PC),A0
	MOVE.L	4(A0),D0
	MOVE.L	(A0),4(A0)
	MOVE.L	D0,(A0)
	LEA	GRRR(PC),A0
	MOVE.L	D0,2(A0)
	MOVE.W #$2300,SR
	BSR	PLAYER
	BSR	PRECALC_DIGIT
	RTS
PLAYER	LEA	OFFSET(PC),A4
	ADDQ.B	#$1,COUNTER(A4)
	MOVE.B	COUNTER(A4),D0
	CMP.B	SPEED(A4),D0
	BLT.S	NONEW
	CLR.B	COUNTER(A4)
	BRA.S	READ_PATTERN
NONEW	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	BSR	CHECK_COMMAND
	BRA	END_OF_PLAYER
READ_PATTERN
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	LEA	$C(A0),A3
	LEA	(A0),A2
	ADD.W	POS(A4),A2
	ADD.W	PATT(A4),A0

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.B	SONGPOS(A4),D0
	MOVE.B	0(A2,D0.W),D1
	SWAP	D1
	LSR.L	#$6,D1
	ADD.W	PATTPOS(A4),D1
	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	BSR.S	PLAYVOICE
	BRA	SET_REPEAT
PLAYVOICE
	MOVE.L	(A0,D1.L),(A6)
	ADDQ.L	#4,D1
	MOVEQ	#0,D2
	MOVE.B	$2(A6),D2
	AND.B	#$F0,D2
	LSR.B	#4,D2
	MOVE.B	(A6),D0
	AND.B	#$F0,D0
	OR.B	D0,D2
	TST.B	D2
	BEQ.S	SET_REGS
	MOVEQ	#0,D3
	LEA	SAMPLESTARTS(PC),A1
	MOVE.L	D2,D4
	SUBQ.L	#$1,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MULU	#$1E,D4
	MOVE.L	(A1,D2.L),$4(A6)
	MOVE.W	(A3,D4.L),$8(A6)
	MOVE.W	$2(A3,D4.L),$12(A6)
	MOVE.W	$4(A3,D4.L),D3
	BRA.S	NO_LOOP
	MOVE.L	$4(A1,D2.L),$4(A6)
	ADD.W	$6(A3,D4.L),D3
	MOVE.W	D3,$8(A6)
	ADD.W	D3,D3
	SUB.L	D3,$4(A6)
	MOVE.L	$4(A6),$A(A6)
	MOVEQ	#0,D3
	MOVE.W	$6(A3,D4.L),D3
	MOVE.W	D3,$E(A6)
	MOVE.W	$12(A6),$8(A5)
	BRA.S	SET_REGS
NO_LOOP	MOVE.L	$4(A6),$A(A6)
	MOVE.W	$6(A3,D4.L),$E(A6)
	MOVE.W	$12(A6),$8(A5)
SET_REGS
	MOVE.W	(A6),D0
	AND.W	#$FFF,D0
	BEQ	CHECK_COMMAND_2
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	CMP.B	#$3,D0
	BNE.S	SETPERIOD
	BSR	SET_PORTAMENTO_TONE
	BRA	CHECK_COMMAND_2
SETPERIOD
	MOVE.W	(A6),$10(A6)
	AND.W	#$FFF,$10(A6)
	CLR.B	$1B(A6)
	MOVE.L	$4(A6),(A5)
	MOVEQ	#0,D0
	MOVE.W	$8(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$4(A5)
	MOVE.W	$10(A6),$A(A5)
	BRA	CHECK_COMMAND_2
SET_REPEAT
	MOVEQ	#0,D0
	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	ADD.W	#$10,PATTPOS(A4)
	CMP.W	#$400,PATTPOS(A4)
	BNE.S	END_OF_PLAYER
NEXT_PATT
	CLR.W	PATTPOS(A4)
	CLR.B	BREAK(A4)
	ADDQ.B	#1,SONGPOS(A4)
	AND.B	#$7F,SONGPOS(A4)
	MOVE.B	SONGPOS(A4),d1
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	ADD.W	POS(A4),A0
	CMP.B	-2(A0),d1
	BNE.S	END_OF_PLAYER
	CLR.B	SONGPOS(A4)
END_OF_PLAYER
	TST.B	BREAK(A4)
	BNE.S	NEXT_PATT
	RTS
CHECK_COMMAND
	MOVE.W	$2(A6),D0
	AND.W	#$FFF,D0
	BEQ.S	NO_COMMAND
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	BEQ.S	ARPEGGIO
	CMP.B	#$1,D0
	BEQ	PORTAMENTO_UP
	CMP.B	#$2,D0
	BEQ	PORTAMENTO_DOWN
	CMP.B	#$3,D0
	BEQ	PORTAMENTO_TONE
	CMP.B	#$4,D0
	BEQ	VIBRATO
	MOVE.W	$10(A6),$A(A5)
	CMP.B	#$A,D0
	BEQ	VOLUME_SLIDE
NO_COMMAND
	RTS
ARPEGGIO
	MOVEQ	#0,D0
	MOVE.B	COUNTER(A4),D0
	DIVS	#$3,D0
	SWAP	D0
	CMP.W	#$0,D0
	BEQ.S	ARPEGGIO2
	CMP.W	#$2,D0
	BEQ.S	ARPEGGIO1

	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	LSR.B	#$4,D0
	BRA.S	ARPEGGIO3
ARPEGGIO1
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	AND.B	#$F,D0
	BRA.S	ARPEGGIO3
ARPEGGIO2
	MOVE.W	$10(A6),D2
	BRA.S	ARPEGGIO4
ARPEGGIO3
	ADD.W	D0,D0
	MOVEQ	#0,D1
	MOVE.W	$10(A6),D1
	LEA	PERIODS(PC),A0
	MOVEQ	#$24,D7
ARPEGGIO_LOOP
	MOVE.W	(A0,D0.W),D2
	CMP.W	(A0),D1
	BGE.S	ARPEGGIO4
	LEA	2(A0),A0
	DBRA	D7,ARPEGGIO_LOOP
	RTS
ARPEGGIO4
	MOVE.W	D2,$A(A5)
	RTS
PORTAMENTO_UP
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	SUB.W	D0,$10(A6)
	MOVE.W	$10(A6),D0
	CMP.W	#$71,D0
	BPL.S	PORTUP
	MOVE.W	#$71,$10(A6)
PORTUP	MOVE.W	$10(A6),$A(A5)
	RTS
PORTAMENTO_DOWN
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	ADD.W	D0,$10(A6)
	MOVE.W	$10(A6),D0
	CMP.W	#$358,D0
	BMI.S	PORTDWN
	MOVE.W	#$358,$10(A6)
PORTDWN	MOVE.W	$10(A6),$A(A5)
	RTS
SET_PORTAMENTO_TONE
	MOVE.W	(A6),D2
	AND.W	#$FFF,D2
	MOVE.W	D2,$18(A6)
	MOVE.W	$10(A6),D0
	CLR.B	$16(A6)
	CMP.W	D0,D2
	BEQ.S	CLR_PORTAMENTO_TONE
	BGE.S	OK_RTS
	MOVE.B	#$1,$16(A6)
	RTS
CLR_PORTAMENTO_TONE
	CLR.W	$18(A6)
OK_RTS
	RTS
PORTAMENTO_TONE
	MOVE.B	$3(A6),D0
	BEQ.S	MY_SLIDE
	MOVE.B	D0,$17(A6)
	CLR.B	$3(A6)
MY_SLIDE
	TST.W	$18(A6)
	BEQ.S	OK_RTS
	MOVEQ	#0,D0
	MOVE.B	$17(A6),D0
	TST.B	$16(A6)
	BNE.S	MY_SUB
	ADD.W	D0,$10(A6)
	MOVE.W	$18(A6),D0
	CMP.W	$10(A6),D0
	BGT.S	MY_OK
	MOVE.W	$18(A6),$10(A6)
	CLR.W	$18(A6)
MY_OK	MOVE.W	$10(A6),$A(A5)
	RTS
MY_SUB
	SUB.W	D0,$10(A6)
	MOVE.W	$18(A6),D0
	CMP.W	$10(A6),D0
	BLT.S	MY_OK
	MOVE.W	$18(A6),$10(A6)
	CLR.W	$18(A6)
	MOVE.W	$10(A6),$A(A5)
	RTS
VIBRATO	MOVE.B	$3(A6),D0
	BEQ.S	VIB
	MOVE.B	D0,$1A(A6)
VIB	MOVE.B	$1B(A6),D0
	LEA	SINUS(PC),A0
	LSR.W	#$2,D0
	AND.W	#$1F,D0
	MOVEQ	#0,D2
	MOVE.B	(A0,D0.W),D2
	MOVE.B	$1A(A6),D0
	AND.W	#$F,D0
	MULU	D0,D2
	LSR.W	#$6,D2
	MOVE.W	$10(A6),D0
	TST.B	$1B(A6)
	BMI.S	VIB_MIN
	ADD.W	D2,D0
	BRA.S	VIB2
VIB_MIN	SUB.W	D2,D0
VIB2	MOVE.W	D0,$A(A5)
	MOVE.B	$1A(A6),D0
	LSR.W	#$2,D0
	AND.W	#$3C,D0
	ADD.B	D0,$1B(A6)
	RTS
VOLUME_SLIDE
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	LSR.B	#4,D0
	TST.B	D0
	BEQ.S	VOLUME_DOWN
	ADD.W	D0,$12(A6)
	CMP.W	#$40,$12(A6)
	BMI.S	VOLUP_OK
	MOVE.W	#$40,$12(A6)
VOLUP_OK
	MOVE.W	$12(A6),$8(A5)
	RTS
VOLUME_DOWN
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	AND.B	#$F,D0
	SUB.W	D0,$12(A6)
	BPL.S	VOLDWN_OK
	CLR.W	$12(A6)
VOLDWN_OK
	MOVE.W	$12(A6),$8(A5)
	RTS
CHECK_COMMAND_2
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	CMP.B	#$B,D0
	BEQ.S	POSITION_JUMP
	CMP.B	#$C,D0
	BEQ.S	SET_VOLUME
	CMP.B	#$D,D0
	BEQ.S	PATTERN_BREAK
	CMP.B	#$E,D0
	BEQ.S	SET_FILTER
	CMP.B	#$F,D0
	BEQ.S	SET_SPEED
	RTS
POSITION_JUMP
	MOVE.B	$3(A6),D0
	SUBQ.B	#$1,D0
	MOVE.B	D0,SONGPOS(A4)
	NOT.B	BREAK(A4)
	RTS
SET_VOLUME
	CMP.B	#$40,$3(A6)
	BLE.S	VOLUME_OK
	MOVE.B	#$40,$3(A6)
VOLUME_OK
	MOVE.B	$3(A6),$9(A5)
	RTS
PATTERN_BREAK
	NOT.B	BREAK(A4)
	RTS
SET_FILTER
	RTS
SET_SPEED
	MOVE.B	$3(A6),D0
	AND.W	#$1F,D0
	BEQ.S	NO_CHANGE_SPEED
	CLR.B	COUNTER(A4)
	MOVE.B	D0,SPEED(A4)
NO_CHANGE_SPEED
	RTS
PRECALC_DIGIT
	LEA	VOLUME,A3
	LEA	FRQ(PC),A4
	LEA	MEGABUFFER(PC),A5
	LEA	CHIP1(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+500(PC),A5
	LEA	CHIP2(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+1000(PC),A5
	LEA	CHIP3(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+1500(PC),A5
	LEA	CHIP4(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER(PC),A0
	LEA	500(A0),A1
	LEA	500(A1),A2
	LEA	500(A2),A3
	LEA	DIGIT_SWAP(PC),A4
	MOVE.L	4(A4),A4
	BSR	MIX_ALL_VOICES
	RTS
CALC_VOICE
	MOVE.L	$0(A6),A0
	MOVEQ	#0,D1
	MOVE.L	$4(A6),D3
	MOVE.W	$8(A6),D4
	BEQ.S	.VOLUME_NUL
	SUBQ.W	#1,D4
.VOLUME_NUL
	LSL.W	#$8,D4
	MOVE.L	$10(A6),D6
	MOVE.W	$A(A6),D7
	ADD.W	D7,D7
	ADD.W	D7,D7
	MOVE.L	(A4,D7.W),D7
	MOVE.W	D7,D0
	SWAP	D7
	MOVEQ	#0,D2
	MOVE.W	D7,D2
	SWAP	D7
	TST.L	D6
	BNE.S	.0
	CLR.L	D7
.0	MOVE.L	$C(A6),A2
	MOVEQ	#FRQ_LOOP,D5
.PAULA	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R1
.RR1	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R2
.RR2	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R3
.RR3	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	DBRA	D5,.PAULA
	BRA.S	END_OF_CALC_A_VOICE
.R1	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR1
.R2	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR2
.R3	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR3
END_OF_CALC_A_VOICE
	MOVE.L	A0,$0(A6)
	MOVE.L	D3,$4(A6)
	RTS
MIX_ALL_VOICES
	MOVEQ	#FRQ_LOOP,D1
.LOOP	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	DBRA	D1,.LOOP
	MOVE.W	D0,(A4)+
	MOVE.W	D0,(A4)+
	MOVE.W	D0,(A4)+
	RTS
SOUNDCHIP
	MOVEM.L	D0/A0,-(SP)
GRRR	LEA DIGIT,A0
	MOVE.W (A0),D0
	LEA (GRRR+4)(PC),A0
	ADDQ.W #2,(A0)
	LEA SOUND(PC,D0.W),A0
	MOVE.L (A0)+,D0
	MOVE.L (A0),$FFFF8800.W
	LEA $FFFF8800.W,A0
	MOVEP.L	D0,(A0)
	MOVE.L (SP)+,D0
	MOVE.L (SP)+,A0
	RTE
SOUND	DC.W	$80E,$90D,$A0C,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0A,0,$80E,$90D,$A0A,0
	DC.W	$80E,$90D,$A0A,0,$80E,$90D,$A0A,0
	DC.W	$80E,$90C,$A0C,0,$80E,$90D,$A00,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0A,0,$80E,$90C,$A0A,0
	DC.W	$80E,$90C,$A0A,0,$80E,$90C,$A0A,0
	DC.W	$80D,$90D,$A0C,0,$80D,$90D,$A0C,0
	DC.W	$80E,$90C,$A09,0,$80E,$90C,$A09,0
	DC.W	$80E,$90C,$A05,0,$80E,$90C,$A00,0
	DC.W	$80E,$90C,$A00,0,$80E,$90B,$A0B,0
	DC.W	$80E,$90B,$A0B,0,$80E,$90B,$A0B,0
	DC.W	$80E,$90B,$A0B,0,$80E,$90B,$A0A,0
	DC.W	$80E,$90B,$A0A,0,$80E,$90B,$A0A,0
	DC.W	$80D,$90D,$A0B,0,$80D,$90D,$A0B,0
	DC.W	$80D,$90D,$A0B,0,$80E,$90B,$A09,0
	DC.W	$80E,$90B,$A09,0,$80E,$90B,$A09,0
	DC.W	$80D,$90C,$A0C,0,$80D,$90D,$A0A,0
	DC.W	$80E,$90B,$A07,0,$80E,$90B,$A00,0
	DC.W	$80E,$90B,$A00,0,$80D,$90D,$A09,0
	DC.W	$80D,$90D,$A09,0,$80E,$90A,$A09,0
	DC.W	$80D,$90D,$A08,0,$80D,$90D,$A07,0
	DC.W	$80D,$90D,$A04,0,$80D,$90D,$A00,0
	DC.W	$80E,$90A,$A04,0,$80E,$909,$A09,0
	DC.W	$80E,$909,$A09,0,$80D,$90C,$A0B,0
	DC.W	$80E,$909,$A08,0,$80E,$909,$A08,0
	DC.W	$80E,$909,$A07,0,$80E,$908,$A08,0
	DC.W	$80E,$909,$A01,0,$80C,$90C,$A0C,0
	DC.W	$80D,$90C,$A0A,0,$80E,$908,$A06,0
	DC.W	$80E,$907,$A07,0,$80E,$908,$A00,0
	DC.W	$80E,$907,$A05,0,$80E,$906,$A06,0
	DC.W	$80D,$90C,$A09,0,$80E,$905,$A05,0
	DC.W	$80E,$904,$A04,0,$80D,$90C,$A08,0
	DC.W	$80D,$90B,$A0B,0,$80E,$900,$A00,0
	DC.W	$80D,$90C,$A06,0,$80D,$90C,$A05,0
	DC.W	$80D,$90C,$A02,0,$80C,$90C,$A0B,0
	DC.W	$80C,$90C,$A0B,0,$80D,$90B,$A0A,0
	DC.W	$80D,$90B,$A0A,0,$80D,$90B,$A0A,0
	DC.W	$80D,$90B,$A0A,0,$80C,$90C,$A0A,0
	DC.W	$80C,$90C,$A0A,0,$80C,$90C,$A0A,0
	DC.W	$80D,$90B,$A09,0,$80D,$90B,$A09,0
	DC.W	$80D,$90A,$A0A,0,$80D,$90A,$A0A,0
	DC.W	$80D,$90A,$A0A,0,$80C,$90C,$A09,0
	DC.W	$80C,$90C,$A09,0,$80C,$90C,$A09,0
	DC.W	$80D,$90B,$A06,0,$80C,$90B,$A0B,0
	DC.W	$80C,$90C,$A08,0,$80D,$90B,$A00,0
	DC.W	$80D,$90B,$A00,0,$80C,$90C,$A07,0
	DC.W	$80C,$90C,$A06,0,$80C,$90C,$A05,0
	DC.W	$80C,$90C,$A03,0,$80C,$90C,$A01,0
	DC.W	$80C,$90B,$A0A,0,$80D,$90A,$A05,0
	DC.W	$80D,$90A,$A04,0,$80D,$90A,$A02,0
	DC.W	$80D,$909,$A08,0,$80D,$909,$A08,0
	DC.W	$80C,$90B,$A09,0,$80C,$90B,$A09,0
	DC.W	$80D,$908,$A08,0,$80B,$90B,$A0B,0
	DC.W	$80D,$909,$A05,0,$80C,$90B,$A08,0
	DC.W	$80D,$909,$A02,0,$80D,$908,$A06,0
	DC.W	$80C,$90B,$A07,0,$80D,$907,$A07,0
	DC.W	$80C,$90B,$A06,0,$80C,$90A,$A09,0
	DC.W	$80B,$90B,$A0A,0,$80C,$90B,$A02,0
	DC.W	$80C,$90B,$A00,0,$80C,$90A,$A08,0
	DC.W	$80D,$906,$A04,0,$80D,$905,$A05,0
	DC.W	$80D,$905,$A04,0,$80C,$909,$A09,0
	DC.W	$80D,$904,$A03,0,$80B,$90B,$A09,0
	DC.W	$80C,$90A,$A05,0,$80B,$90A,$A0A,0
	DC.W	$80C,$909,$A08,0,$80B,$90B,$A08,0
	DC.W	$80C,$90A,$A00,0,$80C,$90A,$A00,0
	DC.W	$80C,$909,$A07,0,$80B,$90B,$A07,0
	DC.W	$80C,$909,$A06,0,$80B,$90B,$A06,0
	DC.W	$80B,$90A,$A09,0,$80B,$90B,$A05,0
	DC.W	$80A,$90A,$A0A,0,$80B,$90B,$A02,0
	DC.W	$80B,$90A,$A08,0,$80C,$907,$A07,0
	DC.W	$80C,$908,$A04,0,$80C,$907,$A06,0
	DC.W	$80B,$909,$A09,0,$80C,$906,$A06,0
	DC.W	$80A,$90A,$A09,0,$80C,$907,$A03,0
	DC.W	$80B,$90A,$A05,0,$80B,$909,$A08,0
	DC.W	$80B,$90A,$A03,0,$80A,$90A,$A08,0
	DC.W	$80B,$90A,$A00,0,$80B,$909,$A07,0
	DC.W	$80B,$908,$A08,0,$80A,$90A,$A07,0
	DC.W	$80A,$909,$A09,0,$80C,$901,$A01,0
	DC.W	$80A,$90A,$A06,0,$80B,$908,$A07,0
	DC.W	$80A,$90A,$A05,0,$80A,$909,$A08,0
	DC.W	$80A,$90A,$A02,0,$80A,$90A,$A01,0
	DC.W	$80A,$90A,$A00,0,$809,$909,$A09,0
	DC.W	$80A,$908,$A08,0,$80B,$908,$A01,0
	DC.W	$80A,$909,$A06,0,$80B,$907,$A04,0
	DC.W	$80A,$909,$A05,0,$809,$909,$A08,0
	DC.W	$80A,$909,$A03,0,$80A,$908,$A06,0
	DC.W	$80A,$909,$A00,0,$809,$909,$A07,0
	DC.W	$809,$908,$A08,0,$80A,$908,$A04,0
	DC.W	$809,$909,$A06,0,$80A,$908,$A01,0
	DC.W	$809,$909,$A05,0,$809,$908,$A07,0
	DC.W	$808,$908,$A08,0,$809,$909,$A02,0
	DC.W	$809,$908,$A06,0,$809,$909,$A00,0
	DC.W	$809,$907,$A07,0,$808,$908,$A07,0
	DC.W	$809,$907,$A06,0,$809,$908,$A02,0
	DC.W	$808,$908,$A06,0,$809,$906,$A06,0
	DC.W	$808,$907,$A07,0,$808,$908,$A04,0
	DC.W	$808,$907,$A06,0,$808,$908,$A02,0
	DC.W	$807,$907,$A07,0,$808,$906,$A06,0
	DC.W	$808,$907,$A04,0,$807,$907,$A06,0
	DC.W	$808,$906,$A05,0,$808,$906,$A04,0
	DC.W	$807,$906,$A06,0,$807,$907,$A04,0
	DC.W	$808,$905,$A04,0,$806,$906,$A06,0
	DC.W	$807,$906,$A04,0,$807,$905,$A05,0
	DC.W	$806,$906,$A05,0,$806,$906,$A04,0
	DC.W	$806,$905,$A05,0,$806,$906,$A02,0
	DC.W	$806,$905,$A04,0,$805,$905,$A05,0
	DC.W	$806,$905,$A02,0,$805,$905,$A04,0
	DC.W	$805,$904,$A04,0,$805,$905,$A02,0
	DC.W	$804,$904,$A04,0,$804,$904,$A03,0
	DC.W	$804,$904,$A02,0,$804,$903,$A03,0
	DC.W	$803,$903,$A03,0,$803,$903,$A02,0
	DC.W	$803,$902,$A02,0,$802,$902,$A02,0
	DC.W	$802,$902,$A01,0,$801,$901,$A01,0
	DC.W	$802,$901,$A00,0,$801,$901,$A00,0
	DC.W	$801,$900,$A00,0,$800,$900,$A00,0
COLNO	DS.W 8
LONG1	EQU 100
LM	EQU 4
PALS	DC.W LONG1
	DC.W $070,$170,$270,$370,$470,$570,$670,$770
	DC.W LM
	DC.W $170,$260,$360,$460,$560,$660,$760,$760
	DC.W LM
	DC.W $270,$360,$450,$550,$650,$750,$740,$750
	DC.W LM
	DC.W $370,$460,$550,$640,$740,$740,$730,$740
	DC.W LM
	DC.W $470,$560,$650,$740,$730,$730,$720,$730
	DC.W LM
	DC.W $570,$660,$750,$740,$730,$720,$710,$720
	DC.W LM
	DC.W $670,$760,$750,$740,$730,$720,$710,$710
	DC.W LONG1
	DC.W $770,$760,$750,$740,$730,$720,$710,$700
	DC.W LM
	DC.W $771,$761,$751,$741,$731,$721,$711,$701
	DC.W LM
	DC.W $772,$762,$752,$742,$732,$722,$712,$702
	DC.W LM
	DC.W $773,$763,$753,$743,$733,$723,$713,$703
	DC.W LM
	DC.W $774,$764,$754,$744,$734,$724,$714,$704
	DC.W LM
	DC.W $775,$765,$755,$745,$735,$725,$715,$705
	DC.W LM
	DC.W $776,$766,$756,$746,$736,$726,$716,$706
	DC.W LONG1
	DC.W $777,$767,$757,$747,$737,$727,$717,$707
	DC.W LM
	DC.W $677,$667,$657,$647,$637,$627,$617,$607
	DC.W LM
	DC.W $577,$567,$557,$547,$537,$527,$517,$507
	DC.W LM
	DC.W $477,$467,$457,$447,$437,$427,$417,$407
	DC.W LM
	DC.W $377,$367,$357,$347,$337,$327,$317,$307
	DC.W LM
	DC.W $277,$267,$257,$247,$237,$227,$217,$207
	DC.W LM
	DC.W $177,$167,$157,$147,$137,$127,$117,$107
	DC.W LONG1
	DC.W $077,$067,$057,$047,$037,$027,$017,$007
	DC.W LM
	DC.W $067,$067,$057,$047,$037,$027,$016,$006
	DC.W LM
	DC.W $057,$057,$057,$047,$037,$026,$015,$005
	DC.W LM
	DC.W $047,$047,$047,$047,$036,$025,$014,$004
	DC.W LM
	DC.W $037,$037,$037,$036,$035,$024,$013,$003
	DC.W LM
	DC.W $027,$027,$026,$024,$024,$023,$012,$002
	DC.W LM
	DC.W $017,$016,$015,$014,$013,$012,$011,$001
	DC.W LONG1
	DC.W $007,$006,$005,$004,$003,$002,$001,$000
	DC.W LM
	DC.W $016,$115,$114,$113,$112,$111,$110,$110
	DC.W LM
	DC.W $025,$124,$223,$222,$221,$220,$220,$220
	DC.W LM
	DC.W $034,$133,$232,$331,$330,$330,$330,$330
	DC.W LM
	DC.W $043,$142,$241,$340,$440,$440,$440,$440
	DC.W LM
	DC.W $052,$151,$250,$350,$450,$550,$550,$550
	DC.W LM
	DC.W $061,$160,$260,$360,$460,$560,$660,$660
	DC.W -1
DIRPAL	DC.L 0
TIMEPAL	DC.W 0
SYNC	DS.W 1
INTS	DS.L 6	
OLDCOL	DS.W 16
CH	DC.B "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789:!. \_+-=?(),'",'"',-1  
SCREE	DS.L 1
TEXT	DC.B '                                           '
	DC.B "ALMOST EVERYONE THOUGHT WE COULDN'T MAKE IT, BUT OF COURSE THEY WERE WRONG! HELLO AND WELCOME TO THE COOL DEMO, THIS IS THE DEMO MODE, PRESS SPACE IF YOU DON'T WANT TO LOOK AT THE DEMOS, JUST READ THIS SCROLL."
	DC.B "OK, NOW THE CREDITS FOR THE BIGEST SPANISH DEMO EVER: MOST CODING SOME ARROGANT CODERS: RED DEVIL, CANAL 23 AND REALIDAD BINARIA, ARTWORK BY RED DEVIL, SIGFRIDO (THIS PICTURE WAS DIGITISED BY HIM), KNIGHTHAWKS (AMIGA FONT), MUSIX BY "
	DC.B "MAD MAX, SCRATCHIN AND SOME AMIGA CREWS (THIS MUSIK FOR EXAMPLE). WELL, THIS IS MY FIRST DEMO SO I THINK I WILL WRITE A LOT OF TEXT. FIRSTABLE I WANT TO TELL YOU HOW WE DECIDED TO MAKE THIS PIECE OF WORK: A FEW MONTHS AGO, "
	DC.B "WE WERE WORKING FOR THE PRETTY GOOD DEMO, AFTER HAVING FINISHED 2 SCREENS WE PHONED XTC AND THEY TOLD US THAT THE DEMO WILL NOT BE PRODUCED BECAUSE OF THE LACK OF SCREENS AND INTEREST, WE WERE QUITE IMPRESED SO WE DECIDED TO CONTINUE "
	DC.B "MAKING DEMOS, WE MADE (WELL, FROM NOW, I MADE), I MADE SEVERAL DEMOS WICH YOU WON'T SEE, THOSE WERE MY FIRST DEMOS, SO AFTER HAVING CODED A FEW DEMOS AND HAVING A LITTLE EXPERIENCE IN 68000 CODE MACHINE I STARTED CODING THIS DEMO."
	DC.B "OK, I THINK I SHOULD TELL YOU WHO WE ARE OR WHAT ARE WE DOING NOW, WE ARE A CREW CALLED THE DELIVERANCE OR TDL, MEMBERS OF THIS CREW ARE: ALEATUM (HALF MEMBER), SCRATCHIN (MEGA MUSICIAN OF THE GROUP) AND RED DEVIL (CODER AND ARTIST), "
	DC.B "THIS IS THE ACTUAL STATUS OF THE GROUP, I HAVE TO POINT OUT THAT ALEATUM IS NOT AN ACTIVE MEMBER AS HE IS NOT DOING WHAT HE WAS SUPPOSED TO DO. "
	DC.B "ONTO OTHER MATTER, IT WOULDN'T BE FAIR THAT WE CONSIDER CANAL 23 OR REALIDAD BINARIA AS GUEST APPERANCES, "
	DC.B "THEY HAVE CONTRIBUTED IN THIS DEMO. WELL, NOW I'M GOING TO TELL YOU WHAT I USED FOR THIS DEMO: FIRST OF ALL AN ST1040, SHARP TV, A FEW JOYSTICKS, LOTSA DISKS, CASIO FX-451M CALCULATOR, DEVPACK 2.20, GFA BASIC 3.5, 1 LITRE OF ZUMOSOL\DAY"
	DC.B "(ORANGE JUICE), ATARI ST INTERNALS, ATARI ST REFERENCE GUIDE, SOME MATHS BOOKS, IN SEARCH OF FOREVER BY RODNEY MATTEWS, KICK OFF 2, AND THE MOST IMPORTANT: THE HELP GIVEN BY MOST FRIENDS. OK, TALKING OF THAT, HERE ARE THE GREETINGS, FIRST "
	DC.B "TO PEOPLE WE NOW: POLIYO SOFT (WHAT IS(A0)+?), JJ (NICE DEMO, GUYS!), ST CONNEXION (THANX FOR ALL YOUR HELP!), GHOST-SLIMMER-OR WHATEVER (STILL RG!), BLACK CORSAIR, FALCON, RIPPER SOFT, ELECTRO SOFT (WHAT DO YOU THINK OF MY SYNC SCROLLER?), "
	DC.B "CARLITOS AND CARLITOS (SORRY BUT I HAD NO TIME TO WRITE YOU), AND NOW TO PEOPLE I DON'T KNOW: THE CAREBEARS (STILL THE BEST), THE LOST BOYS (2ND), AUTOMATION, MEDWAY BOYS, TDA, AENIGMATICA, TEX, THE EMPIRE,  I'M AFRAID I HAVE TO START "
	DC.B "THE FUCKING GREETINGS: THE BEAST CREW (AH! AH! FUCK YOU! I THINK YOU ARE NOT THE BEST SPAINSH ST CREW, BIG BIG LAMERS!), SWEET REVENGE (YOUR REVENGE IS NOT THAT SWEET!) AND ALEATUM (WHERE ARE YOUR COMPACTS, MY DISKS AND EVERYTHING?). "
	DC.B "OK, HERE IS MY ADDRESS, SO TAKE A PIECE OF PAPER AND START COPYING: R E D  D E V I L           P. O.  B. O. X.  5 6 6              0 3 0 8 0 - A L I C A N T E  -S P A I N , NOW AGAIN: "
	DC.B "                R E D  D E V I L           P. O.  B. O. X.  5 6 6              0 3 0 8 0 - A L I C A N T E  -S P A I N , OK I THINK THAT'S ALL, SO THE SCROLL IS OVER, BYE! "
	DC.B '                                           '
	DC.B 0
ZTEXT	DS.L (ZTEXT-TEXT)
SCREEN	DC.L BASE
SINUS
 DC.B $00,$18,$31,$4A,$61,$78,$8D,$A1,$B4,$C5,$D4,$E0,$EB,$F4,$FA,$FD
 DC.B $FF,$FD,$FA,$F4,$EB,$E0,$D4,$C5,$B4,$A1,$8D,$78,$61,$4A,$31,$18
PERIODS
 dc.w $0358,$0328,$02FA,$02D0,$02A6,$0280,$025C,$023A,$021A,$01FC,$01E0
 dc.w $01C5,$01AC,$0194,$017D,$0168,$0153,$0140,$012E,$011D,$010D,$00FE
 dc.w $00F0,$00E2,$00D6,$00CA,$00BE,$00B4,$00AA,$00A0,$0097,$008F,$0087
 dc.w $007F,$0078,$0071,$0000,$0000
OFFSET	DS.W	$6
SPEED	EQU	$0
SONGPOS	EQU	$1
PATTPOS	EQU	$2
COUNTER	EQU	$4
BREAK	EQU	$5
POS	EQU	$6
PATT	EQU	$8
NBR	EQU	$A
SAMPLESTARTS	DS.L	$20
VOICES	DS.L	$1C
VOICE1	EQU	VOICES
VOICE2	EQU	VOICES+$1C
VOICE3	EQU	VOICES+$38
VOICE4	EQU	VOICES+$54
CHIP	DS.L	$18
CHIP1	EQU	CHIP
CHIP2	EQU	CHIP+$18
CHIP3	EQU	CHIP+$30
CHIP4	EQU	CHIP+$48
NO_INS	DC.W	$8080
MEGABUFFER	DS.L	500
DIGIT	DS.W	500*2
DIGIT_SWAP	DS.L	2
FRQ_LOOP	EQU	81
FRQ_DATA	EQU	50
;Voici les valeurs pour changer de frequence
;Frequences 14Khz  12.3Khz  10.1Khz  8.5Khz  8Khz   7.5Khz
;Frq_loop   44     50       61       72      76       82
;Frq_data   92     81       66       56      53       49
FRQ	DC.L	0
	INCBIN	'12,3_KHZ.FRQ'
YET	DC.L	0
SFONT	DS.L 80*16*8
VOLUME	INCBIN 'VOLUME.BIN'
FONT	INCBIN 'FONT8P3.RED'
PICTURE	INCBIN 'DMENU.PI1'
MUSIC	INCBIN 'CLUTCH.MOD'
