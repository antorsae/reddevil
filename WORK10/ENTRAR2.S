** INTRO-COOL DEMO, SECUENCIA
** QUE SE UTILIZARA PARA LA 
** ENTRADA DE LA DEMO; CON MUSICA
** DIGITALIZADA (DFORCE2.MOD), Y 
** UN LOGO DE LOS AUTORES DE LA 
** COOL DEMO: RED DEVIL, CANAL 23,
** Y POR ULTIMO UN LOGO CON COOL
** DEMO... QUIZA LA REALICE A 
** FULLSCREEN, ES DE NOTAR QUE LA
** RUTINA LLEVA HARDWARE SCROLLER!

** (C) CODING+DIBUJOS-MUSICA: RED DEVIL '90

ANCH	EQU $A0
BASE	EQU $F0000	
	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,SP
	BSR BSC
	LEA PIC1+(16*2)(PC),A0
	LEA BASE+(200*ANCH)+8,A1
	MOVEQ #18-1,D0
	MOVE.W #131-1,D1
	BSR PREPSC
	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	BSR SINT
VSYNC1	CLR.B SYNC
WAIT1	TST.B SYNC
	BEQ.S WAIT1
	MOVEM.L COLS,D0-D7
	MOVEM.L D0-D7,CNOW
SSC	MACRO 
	MOVEA.L CHSC,A0
	MOVE.L (A0)+,CONTLR
	MOVE.L SCREEN,D0
	MOVE.L D0,SCREEN
	MOVE.L (A0),D1
	SUB.L D1,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	ENDM
	SSC
	ADD.L #8,CHSC
	MOVE.W 4(A0),D0
	BPL.S KWAIT1
	ADD.L #$500,SCREEN
	MOVE.L #SCDATA,CHSC
	SUBQ.W #1,VECD
	BEQ.S VSYNC2
KWAIT1	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC1
SPDUP	EQU 4
VSYNC2	CLR.B SYNC
WAIT2	TST.B SYNC
	BEQ.S WAIT2
	SSC
	LEA COLANI(PC),A0
	LEA CNOW+(12*2)(PC),A1
	ADD.W STEPNOW,A0
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0),(A1)
	SUBQ.W #1,SPEED
	BNE.S KWAIT2  
	MOVE.W #SPDUP,SPEED
	ADDQ.W #8,STEPNOW
	MOVE.W 4(A0),D0
	BMI.S VSYNC3
KWAIT2	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC2
VSYNC3	CLR.B SYNC
WAIT3	TST.B SYNC
	BEQ.S WAIT3
	SSC
	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC3
	BSR RINT
	MOVEM.L OLDCOL,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CLR.L -(A7)
	TRAP #1
OLDCOL	DS.W 16
CDOL	DS.W 1
VECD	DC.W (200-35)/8
SCREEN 	DC.L BASE
SINT	MOVE.W #$2700,SR
	LEA INTS,A0
	MOVE.L $70.W,(A0)+
	MOVE.B $FFFFFA07.W,(A0)+
	MOVE.B $FFFFFA09.W,(A0)+
	MOVE.B $FFFF8260.W,(A0)+
	MOVE.B $FFFF820A.W,(A0)+
	MOVE.W $FFFF8240.W,(A0)+
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	MOVE.L #FULLVBL,$70.W
	MOVE.W #$2300,SR
	RTS
RINT	MOVE.W #$2700,SR
	LEA INTS,A0
	MOVE.L (A0)+,$70.W
	MOVE.B (A0)+,$FFFFFA07.W
	MOVE.B (A0)+,$FFFFFA09.W
	MOVE.B (A0)+,$FFFF8260.W
	MOVE.B (A0)+,$FFFF820A.W
	MOVE.W (A0)+,$FFFF8240.W
	MOVE.W #$2300,SR
	RTS
INTS	DS.L 3
FULLVBL	MOVEM.L D0-D7/A0-A6,-(A7)
	LEA $FFFF8240.W,A0
	MOVEQ	#0,D1
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)
	LEA $FFFF8209.W,A0
	LEA $FFFF8260.W,A1
	MOVEQ	#$10,D2
	MOVEQ	#2,D3
	MOVEQ	#0,D4
	MOVEQ #28-1,D5
	MOVE.W CONTLR,D6
	MOVE.W CONTLR+2,D7
	SUB.W D6,D5
	SUB.W D7,D5
	MOVE.W	#$2700,SR
	MOVEQ	#10-1,D0
SWAIT1	DBF	D0,SWAIT1
	CLR.B	$FFFF820A.W
	MOVEQ	#3,D0
SWAIT2	NOP
	DBF	D0,SWAIT2
	MOVE.W	#2,$FFFF820A.W
COUNT1	MOVE.B	(A0),D0
	BEQ.S	COUNT1
	SUB.W	D0,D2
	LSL.W	D2,D1
	LEA $FFFF820A.W,A0
	REPT 85
	NOP
	ENDR
MAINLO1	REPT 10
	NOP
	ENDR
	MOVE.B	D3,(A1)
	MOVE.B	D4,(A1)
	REPT 89
	NOP
	ENDR
	MOVE.B	D4,(A0)
	MOVE.B	D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B	D3,(A1)
	NOP
	MOVE.B	D4,(A1)
	DBF	D6,MAINLO1
	BRA.S COLOCA1
MAINLO2	REPT 4
	NOP
	ENDR
COLOCA1	REPT 99
	NOP
	ENDR
	MOVE.B	D4,(A0)
	MOVE.B	D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B	D3,(A1)
	NOP
	MOVE.B	D4,(A1)
	DBF	D7,MAINLO2
	BRA.S COLOCA2
MAINLO3	REPT 4
	NOP
	ENDR
COLOCA2	REPT 121
	NOP
	ENDR
	DBF	D5,MAINLO3
	MOVEM.L CNOW,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
EVBL	MOVEM.L (A7)+,D0-D7/A0-A6
	ST SYNC
	RTE
BSC	LEA BASE,A0
	LEA BASE+(200*ANCH*2),A1
SS	CLR.L (A0)+
	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S SS
	RTS
PREPSC	MOVEQ #(ANCH/8)-1,D3
	SUB.W D0,D3
	LSL.W #3,D3
COPV	MOVE.W D0,D2
COPH	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D2,COPH
	LEA 0(A1,D3.W),A1
	DBF D1,COPV
	RTS	
STEPNOW	DC.W 0
SPEED	DC.W SPDUP
SYNC	DS.W 1
CONTLR	DS.W 1
	DS.W 1
CHSC	DC.L SCDATA
SCDATA	INCBIN 'VERTI.DAT'
	DC.W -1
COLS	DC.W $000,$747,$737,$727,$717
	DC.W $707,$606,$505,$404,$304
	DC.W $203,$101,$304,$404,$505
	DC.W $606
COLANI	DC.W $304,$404,$505,$606
	DC.W $414,$515,$616,$717
	DC.W $424,$525,$626,$727
	DC.W $434,$535,$636,$737
	DC.W $444,$545,$646,$747
	DC.W $555,$555,$656,$757
	DC.W $666,$666,$666,$767
	DC.W $646,$767,$777,$777
	DC.W $535,$656,$767,$777
	DC.W $424,$545,$656,$767
	DC.W $313,$434,$545,$656
	DC.W $202,$323,$434,$545
	DC.W -1
CNOW	DS.W 16
PIC1	INCBIN 'REDLOGO.BIN'
