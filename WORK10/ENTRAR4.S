** INTRO COOL DEMO: SECUENCIA DE LOGOS 
** EN LOS QUE SE ENCUNTRAN LOS CREADORES
** DE LA 'COOL DEMO', LA MUSICA ES DFORCE_1.MOD
** Y LAS RUTINAS DE PLAYBACK SON DE ST_CNX

** (C) CODING+DIBUJOS-MUSICA: RED DEVIL '90

ANCH	EQU $A0
BASE	EQU $E0000	
SPDUP	EQU 4

SSC	MACRO 
	MOVEA.L CHSC,A0
	MOVE.L (A0)+,CONTLR
	MOVE.L SCREEN,D0
	MOVE.L D0,SCREEN
	MOVE.L (A0),D1
	SUB.L D1,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	ENDM

OTRAHO	MACRO C1,C2,C3,C4,C5,C6,C7,C8
	MOVE.W #\7,D0
	BSR WAITS
	MOVEM.L \1,D0-D7
	MOVEM.L D0-D7,CNOW
	MOVE.L #\2,CANIM
	MOVE.W #\3,VECD1
	MOVE.W #\4,VECD2
	MOVE.L #\5,SCREEN
	MOVE.W #\6,DELAY
	MOVE.W #\8,DELAYZ
	MOVE.L #SCDATA,CHSC
	MOVE.W #SPDUP,SPED
	CLR.W STEPNOW
	BSR PLOGO
	ENDM

COLOCA	MACRO C1,C2,R1,R2
	MOVEQ #0,D0
	LEA CNOW(PC),A0
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)+
	MOVE.L D0,(A0)
	BSR BSC
	LEA \1+(16*2),A0
	LEA BASE+(\2*ANCH)+\3,A1
	MOVEQ #\4-1,D0
	MOVE.W #\5-1,D1
	BSR PREPSC
	ENDM

	
	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA $2000,SP
	COLOCA PIC1,200,2+(-8*5),18,131
	MOVEM.L $FFFF8240.W,D0-D7
	MOVEM.L D0-D7,OLDCOL
	BSR ST_CONNEXION
	BSR SINT
	OTRAHO COLS1,COLANI1,(200-35)/8,176/8,BASE,100,520,30
	COLOCA PIC2,200,(-8*6)+2,20,59
	OTRAHO COLS2,COLANI2,(200-70)/8,136/8,BASE,100,1-1,30
	BSR RINT
	MOVEM.L OLDCOL,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CLR.L -(A7)
	TRAP #1
PLOGO
VSYNC1	CLR.B SYNC
WAIT1	TST.B SYNC
	BEQ.S WAIT1
	SSC
	ADD.L #8,CHSC
	MOVE.W 4(A0),D0
	BPL.S KWAIT1
	ADD.L #$500,SCREEN
	MOVE.L #SCDATA,CHSC
	SUBQ.W #1,VECD1
	BEQ.S VSYNC2
KWAIT1	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC1
VSYNC2	CLR.B SYNC
WAIT2	TST.B SYNC
	BEQ.S WAIT2
	SSC
	LEA DELAYZ(PC),A0
	SUBQ.W #1,(A0)
	TST.W (A0)
	BNE.S VSYNC2
	MOVE.W #1,(A0)
	MOVEA.L CANIM,A0
	LEA CNOW+(12*2)(PC),A1
	ADDA.W STEPNOW,A0
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0),(A1)
	SUBQ.W #1,SPED
	BNE.S KWAIT2  
	MOVE.W #SPDUP,SPED
	ADDQ.W #8,STEPNOW
	MOVE.W 4(A0),D0
	BMI.S VSYNC3
KWAIT2	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC2
VSYNC3	CLR.B SYNC
WAIT3	TST.B SYNC
	BEQ.S WAIT3
	SSC
	SUBQ.W #1,DELAY
	BEQ.S VSYNC4
	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC3
VSYNC4	CLR.B SYNC
WAIT4	TST.B SYNC
	BEQ.S WAIT4
	SSC
	ADD.L #8,CHSC
	MOVE.W 4(A0),D0
	BPL.S KWAIT4
	ADD.L #$500,SCREEN
	MOVE.L #SCDATA,CHSC
	SUBQ.W #1,VECD2
	BEQ.S ESYNC
KWAIT4	CMP.B #$39,$FFFFFC02.W
	BNE.S VSYNC4
ESYNC	RTS
OLDCOL	DS.W 16
DELAY	DS.W 1
DELAYZ	DS.W 1
VECD1	DS.W 1
VECD2	DS.W 1
SCREEN 	DS.L 1
SINT	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L $70.W,(A0)+
	MOVE.L $134.W,(A0)+
	MOVE.B 7(A1),(A0)+
	MOVE.B 9(A1),(A0)+
	MOVE.B $13(A1),(A0)+
	MOVE.B $17(A1),(A0)+
	MOVE.B $19(A1),(A0)+
	MOVE.B $1F(A1),(A0)+
	MOVE.B $FFFF8260.W,(A0)+
	MOVE.B $FFFF820A.W,(A0)+
	CLR.B 7(A1)
	CLR.B 9(A1)
	MOVE.L #SOUNDCHIP,$134.W
	MOVE.L #FULLVBL,$70.W
	MOVE.B #$12,$FFFFFC02.W
	BCLR.B #$3,$17(A1)
	CLR.B $19(A1)
	MOVE.B #FRQ_DATA,$1F(A1)
	MOVE.B #1,$19(A1)
	MOVEQ #32,D0
	MOVE.B D0,7(A1)
	MOVE.B D0,$13(A1)
	MOVE.W #$2300,SR
	RTS
RINT	MOVE.W #$2700,SR
	LEA INTS(PC),A0
	LEA $FFFFFA00.W,A1
	MOVE.L (A0)+,$70.W
	MOVE.L (A0)+,$134.W
	MOVE.B (A0)+,7(A1)
	MOVE.B (A0)+,9(A1)
	MOVE.B (A0)+,$13(A1)
	MOVE.B (A0)+,$17(A1)
	MOVE.B (A0)+,$19(A1)
	MOVE.B (A0)+,$1F(A1)
	MOVE.B (A0)+,$FFFF8260.W
	MOVE.B (A0)+,$FFFF820A.W
	MOVE.B #8,$FFFFFC02.W
	MOVE.W #$2300,SR
	RTS
INTS	DS.L 4
FULLVBL	MOVEM.L D0-D7/A0-A6,-(A7)
	LEA $FFFF8240.W,A0
	MOVEQ #0,D1
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)+
	MOVE.L D1,(A0)
	MOVE.L D1,VL1
	LEA $FFFF8209.W,A0
	LEA $FFFF8260.W,A1
	MOVEQ #16,D2
	MOVEQ #2,D3
	MOVEQ #0,D4
	MOVEQ #28-4,D5
	MOVE.W CONTLR,D6
	MOVE.W CONTLR+2,D7
	SUB.W D6,D5
	SUB.W D7,D5
	BTST #0,D6
	BEQ.S NP1
	ST VL1
NP1	BTST #0,D7
	BEQ.S NP2
	ST VL2
NP2	BTST #0,D5
	BEQ.S NP3
	ST VL3
NP3	LSR.W #1,D5
	LSR.W #1,D6
	LSR.W #1,D7
	MOVE.W #$2700,SR
	MOVEQ #10-1,D0
SWAIT1	DBF D0,SWAIT1
	CLR.B $FFFF820A.W
	MOVEQ #3,D0
SWAIT2	NOP
	DBF D0,SWAIT2
	MOVE.W #2,$FFFF820A.W
COUNT1	MOVE.B (A0),D0
	BEQ.S COUNT1
	SUB.W D0,D2
	LSL.W D2,D1
	LEA $FFFF820A.W,A0
RPLAY	MACRO 
	MOVEA.L GRRR+2,A6 ;5
	MOVE.W (A6),D0; 2
	LEA (GRRR+4)(PC),A6; 2
	ADDQ.W #2,(A6);3 
	LEA SOUND(PC),A6;2
	LEA 0(A6,D0.W),A6;4
	MOVE.L (A6)+,D0;3
	MOVE.L (A6),$FFFF8800.W;7
	LEA $FFFF8800.W,A6;2 
	MOVEP.L	D0,(A6);7
	ENDM

LRE	EQU 37
	RPLAY
	REPT 85-LRE
	NOP
	ENDR
MAINLO1	REPT 10
	NOP
	ENDR
	MOVE.B D3,(A1)
	MOVE.B D4,(A1)
	REPT 89
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	
	REPT 10+3
	NOP
	ENDR
	MOVE.B D3,(A1)
	MOVE.B D4,(A1)
	
	RPLAY
	 
	REPT 89-LRE
	NOP
	ENDR
	
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	DBF D6,MAINLO1
	
	TST.B VL1
	BEQ COLOCB1
LT	EQU 8
LNT	EQU 8	
	REPT 10-LT
	NOP
	ENDR
	MOVE.B D3,(A1)
	MOVE.B D4,(A1)
	
	RPLAY

	REPT 89-LRE
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	REPT 3
	NOP
	ENDR
	
MAINLO2	REPT LNT
	NOP
	ENDR
COLOCB1	REPT 103-LNT
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	
	RPLAY
	REPT 103+3-LRE
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	
	DBF D7,MAINLO2
	
	TST.B VL2
	BEQ COLOCB2
	
	RPLAY
	
	REPT 103-LT-LRE
	NOP
	ENDR
	MOVE.B D4,(A0)
	MOVE.B D3,(A0)
	REPT 13
	NOP
	ENDR
	MOVE.B D3,(A1)
	NOP
	MOVE.B D4,(A1)
	REPT 3
	NOP
	ENDR
	
MAINLO3	REPT LNT
	NOP
	ENDR
COLOCB2	REPT 125-LNT
	NOP
	ENDR
	
	RPLAY

	REPT 125+3-LRE
	NOP
	ENDR
	DBF D5,MAINLO3
	
	TST.B VL3
	BEQ EVBL

	RPLAY
	REPT 125-LT-LRE
	NOP
	ENDR
	
EVBL	MOVEM.L CNOW,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	MOVE.W #$2300,SR
	BSR ST_CONNEXION+$8
	MOVEM.L (A7)+,D0-A6
	ST SYNC
	RTE
	
WAITS	CLR.B SYNC
VWAIT	TST.B SYNC
	BEQ.S VWAIT
	DBF D0,WAITS
	RTS

BSC	LEA BASE,A0
	LEA BASE+(200*ANCH*3),A1
SS	CLR.L (A0)+
	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S SS
	RTS
PREPSC	MOVEQ #(ANCH/8)-1,D3
	SUB.W D0,D3
	LSL.W #3,D3
COPV	MOVE.W D0,D2
COPH	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D2,COPH
	LEA 0(A1,D3.W),A1
	DBF D1,COPV
	RTS	
ST_CONNEXION	BRA	INIT
		BRA	END
		BRA	PLAY
		DC.L	0
		DC.W	0
INIT	LEA ST_CONNEXION(PC),A0
	LEA MUSIC,A1
	MOVE.L A1,12(A0)
	LEA (A1),A0
	LEA	OFFSET(PC),A1
	MOVE.W	#$1D8,POS(A1)
	MOVE.W	#$258,PATT(A1)
	MOVE.W	#$F,NBR(A1)
	CMP.L	#'M.K.',$438(A0)
	BNE.S	NO_NEW_MODULES
	MOVE.W	#$3B8,POS(A1)
	MOVE.W	#$43C,PATT(A1)
	MOVE.W	#$1F,NBR(A1)
NO_NEW_MODULES
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	LEA	OFFSET(PC),A1
	ADD.W	POS(A1),A0
	MOVEQ	#$7F,D0
	MOVEQ	#0,D1
.LOOP1	MOVE.L	D1,D2
	SUBQ.W	#1,D0
.LOOP2	MOVE.B	(A0)+,D1
	CMP.B	D2,D1
	BGT.S	.LOOP1
	DBF	D0,.LOOP2
	ADDQ.B	#1,D2
	SWAP	D2
	LSR.L	#6,D2
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	ADD.L	A0,D2
	LEA	$14(A0),A0
	LEA	OFFSET(PC),A1
	MOVE.W	NBR(A1),D7
	ADD.W	PATT(A1),D2
	MOVE.L	D2,A2
	LEA	SAMPLESTARTS(PC),A1
	LEA	(A1),A3
	MOVEQ	#$1E,D6
.LOOP3	CLR.L	(A3)+
	DBRA	D6,.LOOP3
	SUBQ.W	#1,D7
.LOOP4	CLR.L	(A2)
	MOVE.L	A2,(A1)+
	MOVEQ	#0,D1
	MOVE.W	22(A0),D1
	LSL.L	#1,D1
	MOVE.L	A2,A5
	ADD.L	D1,A2
	MOVE.L	A2,(A1)
	LEA	(A2),A6
	MOVE.W	22(A0),D6
	BEQ.S	.NO_REVERSE
	SUBQ.W	#1,D6
.LOOP5	MOVE.B	(A5),D2
	MOVE.B	-1(A6),D3
	EOR.B	#$80,D2
	EOR.B	#$80,D3
	MOVE.B	D3,(A5)+
	MOVE.B	D2,-(A6)
	DBRA	D6,.LOOP5
.NO_REVERSE	
	CMP.W	#$0001,28(A0)
	BNE.S	.LOOP6
	CLR.W	28(A0)
.LOOP6	LEA	$1E(A0),A0
	DBRA	D7,.LOOP4
	LEA	CHIP(PC),A0
	LEA	NO_INS(PC),A1
	MOVEQ	#$7,D0
.LOOP7	MOVE.L	A1,(A0)+
	CLR.L	(A0)+
	CLR.L	(A0)+
	DBRA	D0,.LOOP7
	LEA	VOICES(PC),A0
	MOVEQ	#$1C-1,D0
.LOOP11	CLR.L	(A0)+
	DBRA	D0,.LOOP11
	LEA	OFFSET(PC),A0
	MOVE.B	#$6,SPEED(A0)
	CLR.B	SONGPOS(A0)
	CLR.B	COUNTER(A0)
	CLR.W	PATTPOS(A0)
	LEA	DIGIT_SWAP(PC),A0
	LEA	(DIGIT+1000)(PC),A1
	MOVE.L	A1,(A0)
	LEA	DIGIT(PC),A1
	MOVE.L	A1,4(A0)
	MOVE.W	#1000-1,D0
.LOOP8	MOVE.W	#$400,(A1)+
	DBRA	D0,.LOOP8
	LEA	YET(PC),A0
	CMP.L	#'CNX',(A0)
	BEQ.S	.NO_CHANGE
	LEA	SOUND(PC),A0
	MOVE.W	#255,D0
.LOOP9	MOVE.B	5(A0),6(A0)
	MOVE.B	5(A0),7(A0)
	MOVE.B	4(A0),5(A0)
	LEA	8(A0),A0
	DBRA	D0,.LOOP9
	LEA	VOLUME,A0
	MOVE.W	#$3FFF,D0
.LOOP10	MOVE.B	(A0),D1
	LSR.B	#2,D1
	MOVE.B	D1,(A0)+
	DBRA	D0,.LOOP10
	LEA	YET(PC),A0
	MOVE.L	#'CNX',(A0)
.NO_CHANGE
END	LEA $FFFF8800.W,A0
	MOVE.L	#$00000000,(A0)
	MOVE.L	#$01010000,(A0)
	MOVE.L	#$02020000,(A0)
	MOVE.L	#$03030000,(A0)
	MOVE.L	#$04040000,(A0)
	MOVE.L	#$05050000,(A0)
	MOVE.L	#$06060000,(A0)
	MOVE.L	#$0707FFFF,(A0)
	MOVE.L	#$08080000,(A0)
	MOVE.L	#$09090000,(A0)
	MOVE.L	#$0A0A0000,(A0)
	RTS
PLAY	MOVE.W #$2700,SR
	LEA	DIGIT_SWAP(PC),A0
	MOVE.L	4(A0),D0
	MOVE.L	(A0),4(A0)
	MOVE.L	D0,(A0)
	LEA	GRRR(PC),A0
	MOVE.L	D0,2(A0)
	MOVE.W #$2300,SR
	BSR	PLAYER
	BSR	PRECALC_DIGIT
	RTS
PLAYER	LEA	OFFSET(PC),A4
	ADDQ.B	#$1,COUNTER(A4)
	MOVE.B	COUNTER(A4),D0
	CMP.B	SPEED(A4),D0
	BLT.S	NONEW
	CLR.B	COUNTER(A4)
	BRA.S	READ_PATTERN
NONEW	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	BSR	CHECK_COMMAND
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	BSR	CHECK_COMMAND
	BRA	END_OF_PLAYER
READ_PATTERN
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	LEA	$C(A0),A3
	LEA	(A0),A2
	ADD.W	POS(A4),A2
	ADD.W	PATT(A4),A0

	MOVEQ	#0,D0
	MOVEQ	#0,D1
	MOVE.B	SONGPOS(A4),D0
	MOVE.B	0(A2,D0.W),D1
	SWAP	D1
	LSR.L	#$6,D1
	ADD.W	PATTPOS(A4),D1
	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	BSR.S	PLAYVOICE
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	BSR.S	PLAYVOICE
	BRA	SET_REPEAT
PLAYVOICE
	MOVE.L	(A0,D1.L),(A6)
	ADDQ.L	#4,D1
	MOVEQ	#0,D2
	MOVE.B	$2(A6),D2
	AND.B	#$F0,D2
	LSR.B	#4,D2
	MOVE.B	(A6),D0
	AND.B	#$F0,D0
	OR.B	D0,D2
	TST.B	D2
	BEQ.S	SET_REGS
	MOVEQ	#0,D3
	LEA	SAMPLESTARTS(PC),A1
	MOVE.L	D2,D4
	SUBQ.L	#$1,D2
	ADD.W	D2,D2
	ADD.W	D2,D2
	MULU	#$1E,D4
	MOVE.L	(A1,D2.L),$4(A6)
	MOVE.W	(A3,D4.L),$8(A6)
	MOVE.W	$2(A3,D4.L),$12(A6)
	MOVE.W	$4(A3,D4.L),D3
	BRA.S	NO_LOOP
	MOVE.L	$4(A1,D2.L),$4(A6)
	ADD.W	$6(A3,D4.L),D3
	MOVE.W	D3,$8(A6)
	ADD.W	D3,D3
	SUB.L	D3,$4(A6)
	MOVE.L	$4(A6),$A(A6)
	MOVEQ	#0,D3
	MOVE.W	$6(A3,D4.L),D3
	MOVE.W	D3,$E(A6)
	MOVE.W	$12(A6),$8(A5)
	BRA.S	SET_REGS
NO_LOOP	MOVE.L	$4(A6),$A(A6)
	MOVE.W	$6(A3,D4.L),$E(A6)
	MOVE.W	$12(A6),$8(A5)
SET_REGS
	MOVE.W	(A6),D0
	AND.W	#$FFF,D0
	BEQ	CHECK_COMMAND_2
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	CMP.B	#$3,D0
	BNE.S	SETPERIOD
	BSR	SET_PORTAMENTO_TONE
	BRA	CHECK_COMMAND_2
SETPERIOD
	MOVE.W	(A6),$10(A6)
	AND.W	#$FFF,$10(A6)
	CLR.B	$1B(A6)
	MOVE.L	$4(A6),(A5)
	MOVEQ	#0,D0
	MOVE.W	$8(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$4(A5)
	MOVE.W	$10(A6),$A(A5)
	BRA	CHECK_COMMAND_2
SET_REPEAT
	MOVEQ	#0,D0
	LEA	CHIP1(PC),A5
	LEA	VOICE1(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP2(PC),A5
	LEA	VOICE2(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP3(PC),A5
	LEA	VOICE3(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	LEA	CHIP4(PC),A5
	LEA	VOICE4(PC),A6
	MOVE.L	$A(A6),$C(A5)
	MOVE.W	$E(A6),D0
	ADD.W	D0,D0
	MOVE.L	D0,$10(A5)
	ADD.W	#$10,PATTPOS(A4)
	CMP.W	#$400,PATTPOS(A4)
	BNE.S	END_OF_PLAYER
NEXT_PATT
	CLR.W	PATTPOS(A4)
	CLR.B	BREAK(A4)
	ADDQ.B	#1,SONGPOS(A4)
	AND.B	#$7F,SONGPOS(A4)
	MOVE.B	SONGPOS(A4),d1
	LEA	ST_CONNEXION(PC),A0
	MOVE.L	12(A0),A0
	ADD.W	POS(A4),A0
	CMP.B	-2(A0),d1
	BNE.S	END_OF_PLAYER
	CLR.B	SONGPOS(A4)
END_OF_PLAYER
	TST.B	BREAK(A4)
	BNE.S	NEXT_PATT
	RTS
CHECK_COMMAND
	MOVE.W	$2(A6),D0
	AND.W	#$FFF,D0
	BEQ.S	NO_COMMAND
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	BEQ.S	ARPEGGIO
	CMP.B	#$1,D0
	BEQ	PORTAMENTO_UP
	CMP.B	#$2,D0
	BEQ	PORTAMENTO_DOWN
	CMP.B	#$3,D0
	BEQ	PORTAMENTO_TONE
	CMP.B	#$4,D0
	BEQ	VIBRATO
	MOVE.W	$10(A6),$A(A5)
	CMP.B	#$A,D0
	BEQ	VOLUME_SLIDE
NO_COMMAND
	RTS
ARPEGGIO
	MOVEQ	#0,D0
	MOVE.B	COUNTER(A4),D0
	DIVS	#$3,D0
	SWAP	D0
	CMP.W	#$0,D0
	BEQ.S	ARPEGGIO2
	CMP.W	#$2,D0
	BEQ.S	ARPEGGIO1

	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	LSR.B	#$4,D0
	BRA.S	ARPEGGIO3
ARPEGGIO1
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	AND.B	#$F,D0
	BRA.S	ARPEGGIO3
ARPEGGIO2
	MOVE.W	$10(A6),D2
	BRA.S	ARPEGGIO4
ARPEGGIO3
	ADD.W	D0,D0
	MOVEQ	#0,D1
	MOVE.W	$10(A6),D1
	LEA	PERIODS(PC),A0
	MOVEQ	#$24,D7
ARPEGGIO_LOOP
	MOVE.W	(A0,D0.W),D2
	CMP.W	(A0),D1
	BGE.S	ARPEGGIO4
	LEA	2(A0),A0
	DBRA	D7,ARPEGGIO_LOOP
	RTS
ARPEGGIO4
	MOVE.W	D2,$A(A5)
	RTS
PORTAMENTO_UP
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	SUB.W	D0,$10(A6)
	MOVE.W	$10(A6),D0
	CMP.W	#$71,D0
	BPL.S	PORTUP
	MOVE.W	#$71,$10(A6)
PORTUP	MOVE.W	$10(A6),$A(A5)
	RTS
PORTAMENTO_DOWN
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	ADD.W	D0,$10(A6)
	MOVE.W	$10(A6),D0
	CMP.W	#$358,D0
	BMI.S	PORTDWN
	MOVE.W	#$358,$10(A6)
PORTDWN	MOVE.W	$10(A6),$A(A5)
	RTS
SET_PORTAMENTO_TONE
	MOVE.W	(A6),D2
	AND.W	#$FFF,D2
	MOVE.W	D2,$18(A6)
	MOVE.W	$10(A6),D0
	CLR.B	$16(A6)
	CMP.W	D0,D2
	BEQ.S	CLR_PORTAMENTO_TONE
	BGE.S	OK_RTS
	MOVE.B	#$1,$16(A6)
	RTS
CLR_PORTAMENTO_TONE
	CLR.W	$18(A6)
OK_RTS
	RTS
PORTAMENTO_TONE
	MOVE.B	$3(A6),D0
	BEQ.S	MY_SLIDE
	MOVE.B	D0,$17(A6)
	CLR.B	$3(A6)
MY_SLIDE
	TST.W	$18(A6)
	BEQ.S	OK_RTS
	MOVEQ	#0,D0
	MOVE.B	$17(A6),D0
	TST.B	$16(A6)
	BNE.S	MY_SUB
	ADD.W	D0,$10(A6)
	MOVE.W	$18(A6),D0
	CMP.W	$10(A6),D0
	BGT.S	MY_OK
	MOVE.W	$18(A6),$10(A6)
	CLR.W	$18(A6)
MY_OK	MOVE.W	$10(A6),$A(A5)
	RTS
MY_SUB
	SUB.W	D0,$10(A6)
	MOVE.W	$18(A6),D0
	CMP.W	$10(A6),D0
	BLT.S	MY_OK
	MOVE.W	$18(A6),$10(A6)
	CLR.W	$18(A6)
	MOVE.W	$10(A6),$A(A5)
	RTS
VIBRATO	MOVE.B	$3(A6),D0
	BEQ.S	VIB
	MOVE.B	D0,$1A(A6)
VIB	MOVE.B	$1B(A6),D0
	LEA	SINUS(PC),A0
	LSR.W	#$2,D0
	AND.W	#$1F,D0
	MOVEQ	#0,D2
	MOVE.B	(A0,D0.W),D2
	MOVE.B	$1A(A6),D0
	AND.W	#$F,D0
	MULU	D0,D2
	LSR.W	#$6,D2
	MOVE.W	$10(A6),D0
	TST.B	$1B(A6)
	BMI.S	VIB_MIN
	ADD.W	D2,D0
	BRA.S	VIB2
VIB_MIN	SUB.W	D2,D0
VIB2	MOVE.W	D0,$A(A5)
	MOVE.B	$1A(A6),D0
	LSR.W	#$2,D0
	AND.W	#$3C,D0
	ADD.B	D0,$1B(A6)
	RTS
VOLUME_SLIDE
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	LSR.B	#4,D0
	TST.B	D0
	BEQ.S	VOLUME_DOWN
	ADD.W	D0,$12(A6)
	CMP.W	#$40,$12(A6)
	BMI.S	VOLUP_OK
	MOVE.W	#$40,$12(A6)
VOLUP_OK
	MOVE.W	$12(A6),$8(A5)
	RTS
VOLUME_DOWN
	MOVEQ	#0,D0
	MOVE.B	$3(A6),D0
	AND.B	#$F,D0
	SUB.W	D0,$12(A6)
	BPL.S	VOLDWN_OK
	CLR.W	$12(A6)
VOLDWN_OK
	MOVE.W	$12(A6),$8(A5)
	RTS
CHECK_COMMAND_2
	MOVE.B	$2(A6),D0
	AND.B	#$F,D0
	CMP.B	#$B,D0
	BEQ.S	POSITION_JUMP
	CMP.B	#$C,D0
	BEQ.S	SET_VOLUME
	CMP.B	#$D,D0
	BEQ.S	PATTERN_BREAK
	CMP.B	#$E,D0
	BEQ.S	SET_FILTER
	CMP.B	#$F,D0
	BEQ.S	SET_SPEED
	RTS
POSITION_JUMP
	MOVE.B	$3(A6),D0
	SUBQ.B	#$1,D0
	MOVE.B	D0,SONGPOS(A4)
	NOT.B	BREAK(A4)
	RTS
SET_VOLUME
	CMP.B	#$40,$3(A6)
	BLE.S	VOLUME_OK
	MOVE.B	#$40,$3(A6)
VOLUME_OK
	MOVE.B	$3(A6),$9(A5)
	RTS
PATTERN_BREAK
	NOT.B	BREAK(A4)
	RTS
SET_FILTER
	RTS
SET_SPEED
	MOVE.B	$3(A6),D0
	AND.W	#$1F,D0
	BEQ.S	NO_CHANGE_SPEED
	CLR.B	COUNTER(A4)
	MOVE.B	D0,SPEED(A4)
NO_CHANGE_SPEED
	RTS
PRECALC_DIGIT
	LEA	VOLUME,A3
	LEA	FRQ(PC),A4
	LEA	MEGABUFFER(PC),A5
	LEA	CHIP1(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+500(PC),A5
	LEA	CHIP2(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+1000(PC),A5
	LEA	CHIP3(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER+1500(PC),A5
	LEA	CHIP4(PC),A6
	BSR.S	CALC_VOICE
	LEA	MEGABUFFER(PC),A0
	LEA	500(A0),A1
	LEA	500(A1),A2
	LEA	500(A2),A3
	LEA	DIGIT_SWAP(PC),A4
	MOVE.L	4(A4),A4
	BSR	MIX_ALL_VOICES
	RTS
CALC_VOICE
	MOVE.L	$0(A6),A0
	MOVEQ	#0,D1
	MOVE.L	$4(A6),D3
	MOVE.W	$8(A6),D4
	BEQ.S	.VOLUME_NUL
	SUBQ.W	#1,D4
.VOLUME_NUL
	LSL.W	#$8,D4
	MOVE.L	$10(A6),D6
	MOVE.W	$A(A6),D7
	ADD.W	D7,D7
	ADD.W	D7,D7
	MOVE.L	(A4,D7.W),D7
	MOVE.W	D7,D0
	SWAP	D7
	MOVEQ	#0,D2
	MOVE.W	D7,D2
	SWAP	D7
	TST.L	D6
	BNE.S	.0
	CLR.L	D7
.0	MOVE.L	$C(A6),A2
	MOVEQ	#FRQ_LOOP,D5
.PAULA	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R1
.RR1	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R2
.RR2	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	SUB.W	D0,D1
	SUBX.W	D2,D3
	BCS.S	.R3
.RR3	MOVE.B	0(A0,D3.L),D4
	MOVE.B	0(A3,D4.W),(A5)+
	DBRA	D5,.PAULA
	BRA.S	END_OF_CALC_A_VOICE
.R1	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR1
.R2	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR2
.R3	MOVE.L	D6,D3
	MOVE.W	D7,D0
	SWAP	D7
	MOVE.W	D7,D2
	SWAP	D7
	MOVE.L	A2,A0
	BRA.S	.RR3
END_OF_CALC_A_VOICE
	MOVE.L	A0,$0(A6)
	MOVE.L	D3,$4(A6)
	RTS
MIX_ALL_VOICES
	MOVEQ	#FRQ_LOOP,D1
.LOOP	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	MOVEQ	#0,D0
	MOVE.B	(A0)+,D0
	ADD.B	(A1)+,D0
	ADD.B	(A2)+,D0
	ADD.B	(A3)+,D0
	LSL.W	#3,D0
	MOVE.W	D0,(A4)+
	DBRA	D1,.LOOP
	MOVE.W	D0,(A4)+
	MOVE.W	D0,(A4)+
	MOVE.W	D0,(A4)+
	RTS
SOUNDCHIP
	MOVEM.L	D0/A0,-(SP)
GRRR	LEA DIGIT,A0
	MOVE.W (A0),D0
	LEA (GRRR+4)(PC),A0
	ADDQ.W #2,(A0)
	LEA SOUND(PC,D0.W),A0
	MOVE.L (A0)+,D0
	MOVE.L (A0),$FFFF8800.W
	LEA $FFFF8800.W,A0
	MOVEP.L	D0,(A0)
	MOVE.L (SP)+,D0
	MOVE.L (SP)+,A0
	RTE

SOUND	DC.W	$80E,$90D,$A0C,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80F,$903,$A00,0
	DC.W	$80F,$903,$A00,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0B,0,$80E,$90D,$A0B,0
	DC.W	$80E,$90D,$A0A,0,$80E,$90D,$A0A,0
	DC.W	$80E,$90D,$A0A,0,$80E,$90D,$A0A,0
	DC.W	$80E,$90C,$A0C,0,$80E,$90D,$A00,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80D,$90D,$A0D,0,$80D,$90D,$A0D,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0B,0,$80E,$90C,$A0B,0
	DC.W	$80E,$90C,$A0A,0,$80E,$90C,$A0A,0
	DC.W	$80E,$90C,$A0A,0,$80E,$90C,$A0A,0
	DC.W	$80D,$90D,$A0C,0,$80D,$90D,$A0C,0
	DC.W	$80E,$90C,$A09,0,$80E,$90C,$A09,0
	DC.W	$80E,$90C,$A05,0,$80E,$90C,$A00,0
	DC.W	$80E,$90C,$A00,0,$80E,$90B,$A0B,0
	DC.W	$80E,$90B,$A0B,0,$80E,$90B,$A0B,0
	DC.W	$80E,$90B,$A0B,0,$80E,$90B,$A0A,0
	DC.W	$80E,$90B,$A0A,0,$80E,$90B,$A0A,0
	DC.W	$80D,$90D,$A0B,0,$80D,$90D,$A0B,0
	DC.W	$80D,$90D,$A0B,0,$80E,$90B,$A09,0
	DC.W	$80E,$90B,$A09,0,$80E,$90B,$A09,0
	DC.W	$80D,$90C,$A0C,0,$80D,$90D,$A0A,0
	DC.W	$80E,$90B,$A07,0,$80E,$90B,$A00,0
	DC.W	$80E,$90B,$A00,0,$80D,$90D,$A09,0
	DC.W	$80D,$90D,$A09,0,$80E,$90A,$A09,0
	DC.W	$80D,$90D,$A08,0,$80D,$90D,$A07,0
	DC.W	$80D,$90D,$A04,0,$80D,$90D,$A00,0
	DC.W	$80E,$90A,$A04,0,$80E,$909,$A09,0
	DC.W	$80E,$909,$A09,0,$80D,$90C,$A0B,0
	DC.W	$80E,$909,$A08,0,$80E,$909,$A08,0
	DC.W	$80E,$909,$A07,0,$80E,$908,$A08,0
	DC.W	$80E,$909,$A01,0,$80C,$90C,$A0C,0
	DC.W	$80D,$90C,$A0A,0,$80E,$908,$A06,0
	DC.W	$80E,$907,$A07,0,$80E,$908,$A00,0
	DC.W	$80E,$907,$A05,0,$80E,$906,$A06,0
	DC.W	$80D,$90C,$A09,0,$80E,$905,$A05,0
	DC.W	$80E,$904,$A04,0,$80D,$90C,$A08,0
	DC.W	$80D,$90B,$A0B,0,$80E,$900,$A00,0
	DC.W	$80D,$90C,$A06,0,$80D,$90C,$A05,0
	DC.W	$80D,$90C,$A02,0,$80C,$90C,$A0B,0
	DC.W	$80C,$90C,$A0B,0,$80D,$90B,$A0A,0
	DC.W	$80D,$90B,$A0A,0,$80D,$90B,$A0A,0
	DC.W	$80D,$90B,$A0A,0,$80C,$90C,$A0A,0
	DC.W	$80C,$90C,$A0A,0,$80C,$90C,$A0A,0
	DC.W	$80D,$90B,$A09,0,$80D,$90B,$A09,0
	DC.W	$80D,$90A,$A0A,0,$80D,$90A,$A0A,0
	DC.W	$80D,$90A,$A0A,0,$80C,$90C,$A09,0
	DC.W	$80C,$90C,$A09,0,$80C,$90C,$A09,0
	DC.W	$80D,$90B,$A06,0,$80C,$90B,$A0B,0
	DC.W	$80C,$90C,$A08,0,$80D,$90B,$A00,0
	DC.W	$80D,$90B,$A00,0,$80C,$90C,$A07,0
	DC.W	$80C,$90C,$A06,0,$80C,$90C,$A05,0
	DC.W	$80C,$90C,$A03,0,$80C,$90C,$A01,0
	DC.W	$80C,$90B,$A0A,0,$80D,$90A,$A05,0
	DC.W	$80D,$90A,$A04,0,$80D,$90A,$A02,0
	DC.W	$80D,$909,$A08,0,$80D,$909,$A08,0
	DC.W	$80C,$90B,$A09,0,$80C,$90B,$A09,0
	DC.W	$80D,$908,$A08,0,$80B,$90B,$A0B,0
	DC.W	$80D,$909,$A05,0,$80C,$90B,$A08,0
	DC.W	$80D,$909,$A02,0,$80D,$908,$A06,0
	DC.W	$80C,$90B,$A07,0,$80D,$907,$A07,0
	DC.W	$80C,$90B,$A06,0,$80C,$90A,$A09,0
	DC.W	$80B,$90B,$A0A,0,$80C,$90B,$A02,0
	DC.W	$80C,$90B,$A00,0,$80C,$90A,$A08,0
	DC.W	$80D,$906,$A04,0,$80D,$905,$A05,0
	DC.W	$80D,$905,$A04,0,$80C,$909,$A09,0
	DC.W	$80D,$904,$A03,0,$80B,$90B,$A09,0
	DC.W	$80C,$90A,$A05,0,$80B,$90A,$A0A,0
	DC.W	$80C,$909,$A08,0,$80B,$90B,$A08,0
	DC.W	$80C,$90A,$A00,0,$80C,$90A,$A00,0
	DC.W	$80C,$909,$A07,0,$80B,$90B,$A07,0
	DC.W	$80C,$909,$A06,0,$80B,$90B,$A06,0
	DC.W	$80B,$90A,$A09,0,$80B,$90B,$A05,0
	DC.W	$80A,$90A,$A0A,0,$80B,$90B,$A02,0
	DC.W	$80B,$90A,$A08,0,$80C,$907,$A07,0
	DC.W	$80C,$908,$A04,0,$80C,$907,$A06,0
	DC.W	$80B,$909,$A09,0,$80C,$906,$A06,0
	DC.W	$80A,$90A,$A09,0,$80C,$907,$A03,0
	DC.W	$80B,$90A,$A05,0,$80B,$909,$A08,0
	DC.W	$80B,$90A,$A03,0,$80A,$90A,$A08,0
	DC.W	$80B,$90A,$A00,0,$80B,$909,$A07,0
	DC.W	$80B,$908,$A08,0,$80A,$90A,$A07,0
	DC.W	$80A,$909,$A09,0,$80C,$901,$A01,0
	DC.W	$80A,$90A,$A06,0,$80B,$908,$A07,0
	DC.W	$80A,$90A,$A05,0,$80A,$909,$A08,0
	DC.W	$80A,$90A,$A02,0,$80A,$90A,$A01,0
	DC.W	$80A,$90A,$A00,0,$809,$909,$A09,0
	DC.W	$80A,$908,$A08,0,$80B,$908,$A01,0
	DC.W	$80A,$909,$A06,0,$80B,$907,$A04,0
	DC.W	$80A,$909,$A05,0,$809,$909,$A08,0
	DC.W	$80A,$909,$A03,0,$80A,$908,$A06,0
	DC.W	$80A,$909,$A00,0,$809,$909,$A07,0
	DC.W	$809,$908,$A08,0,$80A,$908,$A04,0
	DC.W	$809,$909,$A06,0,$80A,$908,$A01,0
	DC.W	$809,$909,$A05,0,$809,$908,$A07,0
	DC.W	$808,$908,$A08,0,$809,$909,$A02,0
	DC.W	$809,$908,$A06,0,$809,$909,$A00,0
	DC.W	$809,$907,$A07,0,$808,$908,$A07,0
	DC.W	$809,$907,$A06,0,$809,$908,$A02,0
	DC.W	$808,$908,$A06,0,$809,$906,$A06,0
	DC.W	$808,$907,$A07,0,$808,$908,$A04,0
	DC.W	$808,$907,$A06,0,$808,$908,$A02,0
	DC.W	$807,$907,$A07,0,$808,$906,$A06,0
	DC.W	$808,$907,$A04,0,$807,$907,$A06,0
	DC.W	$808,$906,$A05,0,$808,$906,$A04,0
	DC.W	$807,$906,$A06,0,$807,$907,$A04,0
	DC.W	$808,$905,$A04,0,$806,$906,$A06,0
	DC.W	$807,$906,$A04,0,$807,$905,$A05,0
	DC.W	$806,$906,$A05,0,$806,$906,$A04,0
	DC.W	$806,$905,$A05,0,$806,$906,$A02,0
	DC.W	$806,$905,$A04,0,$805,$905,$A05,0
	DC.W	$806,$905,$A02,0,$805,$905,$A04,0
	DC.W	$805,$904,$A04,0,$805,$905,$A02,0
	DC.W	$804,$904,$A04,0,$804,$904,$A03,0
	DC.W	$804,$904,$A02,0,$804,$903,$A03,0
	DC.W	$803,$903,$A03,0,$803,$903,$A02,0
	DC.W	$803,$902,$A02,0,$802,$902,$A02,0
	DC.W	$802,$902,$A01,0,$801,$901,$A01,0
	DC.W	$802,$901,$A00,0,$801,$901,$A00,0
	DC.W	$801,$900,$A00,0,$800,$900,$A00,0
STEPNOW	DS.W 1
SPED	DS.W 1
VL1	DS.B 1
VL2	DS.B 1
VL3	DS.B 1
SYNC	DS.B 1
CONTLR	DS.W 1
	DS.W 1
CHSC	DS.L 1
SCDATA	INCBIN 'VERTI.DAT'
	DC.W -1
CANIM	DS.L 1
COLORES	DS.L 1
COLS1	DC.W $000,$745,$735,$725,$715
	DC.W $705,$605,$504,$403,$302
	DC.W $201,$100,$302,$403,$504
	DC.W $606
COLANI1	DC.W $302,$403,$504,$605
	DC.W $413,$514,$615,$716
	DC.W $524,$625,$726,$727
	DC.W $635,$736,$737,$737
	DC.W $746,$747,$747,$747
	DC.W $757,$767,$767,$766
	DC.W $677,$767,$776,$775
	DC.W $567,$676,$775,$774
	DC.W $456,$565,$674,$773
	DC.W $345,$454,$563,$672
	DC.W $234,$343,$452,$561
	DC.W $123,$232,$341,$450
	DC.W -1
COLS2	DC.W $000,$657,$546,$435,$324
	DC.W $213,$102,$002,$037,$027
	DC.W $017,$107,$107,$017,$027
	DC.W $037
COLANI2	DC.W $107,$017,$027,$037
	DC.W $217,$127,$137,$147
	DC.W $327,$237,$247,$257
	DC.W $437,$347,$357,$367
	DC.W $547,$457,$467,$477
	DC.W $657,$567,$577,$577
	DC.W $767,$677,$677,$677
	DC.W $777,$777,$777,$777
	DC.W $767,$777,$777,$777
	DC.W $757,$767,$777,$777
	DC.W $747,$757,$777,$777
	DC.W $737,$747,$767,$777
	DC.W $626,$737,$757,$777
	DC.W $516,$626,$646,$666
	DC.W $405,$515,$535,$555
	DC.W -1
CNOW	DS.W 16
SINUS
 DC.B $00,$18,$31,$4A,$61,$78,$8D,$A1,$B4,$C5,$D4,$E0,$EB,$F4,$FA,$FD
 DC.B $FF,$FD,$FA,$F4,$EB,$E0,$D4,$C5,$B4,$A1,$8D,$78,$61,$4A,$31,$18
PERIODS
 dc.w $0358,$0328,$02FA,$02D0,$02A6,$0280,$025C,$023A,$021A,$01FC,$01E0
 dc.w $01C5,$01AC,$0194,$017D,$0168,$0153,$0140,$012E,$011D,$010D,$00FE
 dc.w $00F0,$00E2,$00D6,$00CA,$00BE,$00B4,$00AA,$00A0,$0097,$008F,$0087
 dc.w $007F,$0078,$0071,$0000,$0000
OFFSET	DS.W	$6
SPEED	EQU	$0
SONGPOS	EQU	$1
PATTPOS	EQU	$2
COUNTER	EQU	$4
BREAK	EQU	$5
POS	EQU	$6
PATT	EQU	$8
NBR	EQU	$A
SAMPLESTARTS	DS.L	$20
VOICES	DS.L	$1C
VOICE1	EQU	VOICES
VOICE2	EQU	VOICES+$1C
VOICE3	EQU	VOICES+$38
VOICE4	EQU	VOICES+$54
CHIP	DS.L	$18
CHIP1	EQU	CHIP
CHIP2	EQU	CHIP+$18
CHIP3	EQU	CHIP+$30
CHIP4	EQU	CHIP+$48
NO_INS	DC.W	$8080
MEGABUFFER	DS.L	500
DIGIT	DS.W	500*2
DIGIT_SWAP	DS.L	2
FRQ_LOOP	EQU	66
FRQ_DATA	EQU	61
;Voici les valeurs pour changer de frequence
;Frequences 14Khz  12.3Khz  10.1Khz  8.5Khz  8Khz   7.5Khz
;Frq_loop   44     50       61       72      76       82
;Frq_data   92     81       66       56      53       49
FRQ	DC.L	0
	INCBIN	'10_KHZ.FRQ'
YET	DC.L	0
VOLUME	INCBIN 'VOLUME.BIN'
PIC1	INCBIN 'REDLOGO.BIN'
PIC2	INCBIN 'C23.BIN'
MUSIC	INCBIN 'DFORCE_1.MOD'
