** M_BALL DEMO. OTRA DE BOLAS!!
** FALTA EL SCROLL, LA MUSICA
** EL MARCADOR, OTRA TABLA
** Y CAMBIAR EL TAMA¥O DE LA TABLA
** V.-0.7

** (C) 1990 RED DEVIL

MAXX	EQU (320-16-1)*8
MAXY	EQU (200-16)*8
BOLAS	EQU 41
LONG	EQU (8*35*160)
BASE	EQU $F6000-LONG
SCREE1	EQU BASE
SCREE2	EQU BASE+LONG
DES	EQU 40*160
LETRE	EQU (40*160)
	BRA START
COPDEL	LEA PRO+34+(37*160),A0
	LEA SCREE1+LETRE,A1
	BSR XCOP
	LEA PRO+34+(37*160),A0
	LEA SCREE2+LETRE,A1
XCOP	MOVE.W #(80*160/8)-1,D0
COPLA	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D0,COPLA
	RTS
MAKESPR	LEA PRO+34+2,A0
	LEA SCREE2,A1
	BSR C_SP
	LEA PRO+34+(16*160)+2,A0
	LEA SCREE2+(17*160),A1
	BSR C_SP
	MOVEQ #16-1,D7
	LEA SPR_OR,A1
ROT_OR	BSR SAVE_SP
	LEA SCREE2,A0
	BSR ROX_OR
	LEA SCREE2+(17*160),A0
	BSR ROX_AND
	DBF D7,ROT_OR
	RTS
C_SP	MOVEQ #16-1,D7
CPSLOOP	MOVE.L (A0),(A1)
	MOVE.L 4(A0),4(A1)
	MOVE.L 8(A0),8(A1)
	MOVE.L 8+4(A0),8+4(A1)
	LEA 160(A0),A0
	LEA 160(A1),A1
	DBF D7,CPSLOOP
	RTS
SAVE_SP	LEA SCREE2,A0
	MOVEQ #16-1,D6
SVLOOP	MOVE.L 160*17(A0),(A1)+
	MOVE.L (A0),(A1)+
	MOVE.L (160*17)+8(A0),(A1)+
	MOVE.L 8(A0),(A1)+
	LEA 160(A0),A0
	DBF D6,SVLOOP
	RTS
ROX_OR	MOVEQ #16-1,D6
ROXLIN	MOVEQ #4-1,D5
ROXPLA	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8(A0)
	ADDQ.L #2,A0
	DBF D5,ROXPLA
	LEA 160-(4*2)(A0),A0
	DBF D6,ROXLIN
	RTS
ROX_AND	MOVEQ #16-1,D6
ROXLINA	MOVEQ #4-1,D5
ROXPLAA	MOVE.W #-1,CCR
	ROXR (A0)
	ROXR 8(A0)
	ADDQ.L #2,A0
	DBF D5,ROXPLAA
	LEA 160-(4*2)(A0),A0
	DBF D6,ROXLINA
	RTS
G_SINUS	LEA SINUS1,A0
	LEA SINUS2,A1
	LEA ESINUS1,A2
C_SINU	MOVE.L (A0)+,(A1)+
	CMPA.L A0,A2
	BGT.S C_SINU
	LEA SINUS1,A0
	MOVE.L #SCREE2+2,LOG_SC
	BSR GENEY
	LEA SINUS2,A0
	MOVE.L #SCREE1+2,LOG_SC
	BSR GENEY
	RTS
GENEY	MOVE.W #MAXX-1,D7
LOOPX	MOVEQ #0,D0
	MOVE.W (A0),D0
	MULU #160,D0
	MOVE.L D0,(A0)+
	MOVE.L #0,(A0)+
	DBF D7,LOOPX
	MOVE.W #MAXY-1,D7
GENEX	MOVEQ #0,D0
	MOVEQ #0,D1
	MOVE.W (A0),D0
	DIVU #16,D0
	MOVE.W D0,D1
	MULU #8,D1
	CLR.W D0
	SWAP D0
	MULU #2*4*16*2,D0
	ADD.L #SPR_OR,D0
	ADD.L LOG_SC,D1
	MOVE.L D0,(A0)+
	MOVE.L D1,(A0)+
	DBF D7,GENEX
	RTS
START	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 6(A7),A7
	LEA $2000,A7
	BSR MAKESPR
	BSR G_SINUS
	JSR GETFONT
	JSR ROTAX
	JSR SETTEXT
	MOVEQ #2,D0
	JSR MUZAX
	BSR CLS
	BSR COPDEL
	JSR HBLON
	MOVEM.L PRO+2,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
UJ	CLR.B SYNC
SYNX1	TST.B SYNC
	BEQ.S SYNX1
	MOVE.W COLOR2,$FFFF8242.W
	MOVE.L #SCREE2-DES,D0
	MOVE.L #LONG-DES,SCREEN
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	BSR FLASH
	BSR INPUT_K
	JSR MUZAX+4+4
	BSR BORRAB1
	BSR PUT_BS1
	JSR SC_BIG
	CLR.B SYNC
SYNX2	TST.B SYNC
	BEQ.S SYNX2
	MOVE.W COLOR2,$FFFF8242.W
	MOVE.L #SCREE1-DES,D0
	MOVE.L #-DES,SCREEN
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	BSR FLASH
	JSR MUZAX+4+4
	BSR INPUT_K
	BSR BORRAB2
	BSR PUT_BS2
	JSR SC_BIG
	CMP.B #$39,$FFFFFC02.W
	BNE UJ
	JSR HBLOFF
	CLR.L -(A7)
	TRAP #1
FLASK	EQU 100
DELAY	EQU 4
FLASH	TST.W FLC
	BNE.S NOFLASH
	MOVE.W #DELAY,FLC
	ADDQ.L #2,FLNO
	MOVE.L FLNO,A0
	MOVE.W (A0),D1
	BPL.S PONO
	MOVE.W #FLASK,FLC
	MOVE.L #LFLIST,FLNO
	BRA.S NOFLASH			
PONO	MOVE.W D1,COLOR2
NOFLASH	SUBQ.W #1,FLC
GOOUT	RTS
COLOR2	DS.W 1
FLNO	DC.L LFLIST
FLC	DC.W FLASK
LFLIST	DC.W 0,$1,$2,$3,$4,$5,$6,$7
	DC.W $17,$27,$37,$47,$57,$67,$77
	DC.W $177,$277,$377,$477,$577,$677,$777
	REPT 10
	DC.W $777
	ENDR
	DC.W $777,$677,$577,$477,$377,$277,$177
	DC.W $77,$67,$57,$47,$37,$27,$17
	DC.W $7,$6,$5,$4,$3,$2,$1,$0
	DC.W -1
WAITK	EQU 2
DELK	DC.W WAITK
INPUT_K	SUBQ.W #1,DELK
	BNE DEDD
	MOVE.W #WAITK,DELK
	MOVE.B $FFFFFC02.W,D0
	SUBQ.B #2,D0
	BNE.S INCXX
	ADDQ.W #8,INCX
INCXX	CMP.B #1,D0
	BNE.S DECXX
	SUBQ.W #8,INCX
	BPL.S DECXX
	MOVE.W #0,INCX
DECXX	CMP.B #2,D0
	BNE.S INCYY
	ADDQ.W #8,INCY
INCYY	CMP.B #3,D0
	BNE.S DECYY
	SUBQ.W #8,INCY
	BPL.S DECYY
	MOVE.W #0,INCY
DECYY	CMP.B #4,D0
	BNE.S DEXX
	ADDQ.W #8,DOX
DEXX	CMP.B #5,D0
	BNE.S INDY
	SUBQ.W #8,DOX
	BPL.S INDY
	MOVE.W #0,DOX
INDY	CMP.B #6,D0
	BNE.S DEDY
	ADDQ.W #8,DOY
DEDY	CMP.B #7,D0
	BNE.S DEDD
	SUBQ.W #8,DOY
	BPL.S DEDD
	MOVE.W #0,DOY
DEDD	RTS
PUT_BS1	MOVE.L INCX,D0
	ADD.L D0,SUX
	MOVE.L SUX,DUX
	BSR GET_N1
	MOVE.L A1,DAT_BO1
	BSR P_SPR
	LEA DAT_BO1+4,A3
	MOVE.L DOX,D2
	REPT BOLAS
	BSR GET_NA1
	MOVE.L A1,(A3)+
	BSR P_SPR
	ADD.L D2,DUX
	ENDR
	RTS
PUT_BS2	MOVE.L INCX,D0
	ADD.L D0,SUX
	MOVE.L SUX,DUX
	BSR GET_N2
	MOVE.L A1,DAT_BO2
	BSR P_SPR
	LEA DAT_BO2+4,A3
	MOVE.L DOX,D2
	REPT BOLAS
	BSR GET_NA2
	MOVE.L A1,(A3)+
	BSR P_SPR
	ADD.L D2,DUX
	ENDR
	RTS
BORRAB1	LEA DAT_BO1,A0
	MOVE.W #BOLAS,D0
	MOVEQ #0,D7
CLR_BA	MOVEA.L (A0)+,A1
	MOVE.L D7,(A1)
	MOVE.L D7,8(A1)
	MOVE.L D7,(160*1)(A1)
	MOVE.L D7,(160*1)+8(A1)
	MOVE.L D7,(160*2)(A1)
	MOVE.L D7,(160*2)+8(A1)
	MOVE.L D7,(160*3)(A1)
	MOVE.L D7,(160*3)+8(A1)
	MOVE.L D7,(160*4)(A1)
	MOVE.L D7,(160*4)+8(A1)
	MOVE.L D7,(160*5)(A1)
	MOVE.L D7,(160*5)+8(A1)
	MOVE.L D7,(160*6)(A1)
	MOVE.L D7,(160*6)+8(A1)
	MOVE.L D7,(160*7)(A1)
	MOVE.L D7,(160*7)+8(A1)
	MOVE.L D7,(160*8)(A1)
	MOVE.L D7,(160*8)+8(A1)
	MOVE.L D7,(160*9)(A1)
	MOVE.L D7,(160*9)+8(A1)
	MOVE.L D7,(160*10)(A1)
	MOVE.L D7,(160*10)+8(A1)
	MOVE.L D7,(160*11)(A1)
	MOVE.L D7,(160*11)+8(A1)
	MOVE.L D7,(160*12)(A1)
	MOVE.L D7,(160*12)+8(A1)
	MOVE.L D7,(160*13)(A1)
	MOVE.L D7,(160*13)+8(A1)
	MOVE.L D7,(160*14)(A1)
	MOVE.L D7,(160*14)+8(A1)
	MOVE.L D7,(160*15)(A1)
	MOVE.L D7,(160*15)+8(A1)
	DBF D0,CLR_BA
	RTS
BORRAB2	LEA DAT_BO2,A0
	MOVE.W #BOLAS,D0
	MOVEQ #0,D7
CLR_BB	MOVEA.L (A0)+,A1
	MOVE.L D7,(A1)
	MOVE.L D7,8(A1)
	MOVE.L D7,(160*1)(A1)
	MOVE.L D7,(160*1)+8(A1)
	MOVE.L D7,(160*2)(A1)
	MOVE.L D7,(160*2)+8(A1)
	MOVE.L D7,(160*3)(A1)
	MOVE.L D7,(160*3)+8(A1)
	MOVE.L D7,(160*4)(A1)
	MOVE.L D7,(160*4)+8(A1)
	MOVE.L D7,(160*5)(A1)
	MOVE.L D7,(160*5)+8(A1)
	MOVE.L D7,(160*6)(A1)
	MOVE.L D7,(160*6)+8(A1)
	MOVE.L D7,(160*7)(A1)
	MOVE.L D7,(160*7)+8(A1)
	MOVE.L D7,(160*8)(A1)
	MOVE.L D7,(160*8)+8(A1)
	MOVE.L D7,(160*9)(A1)
	MOVE.L D7,(160*9)+8(A1)
	MOVE.L D7,(160*10)(A1)
	MOVE.L D7,(160*10)+8(A1)
	MOVE.L D7,(160*11)(A1)
	MOVE.L D7,(160*11)+8(A1)
	MOVE.L D7,(160*12)(A1)
	MOVE.L D7,(160*12)+8(A1)
	MOVE.L D7,(160*13)(A1)
	MOVE.L D7,(160*13)+8(A1)
	MOVE.L D7,(160*14)(A1)
	MOVE.L D7,(160*14)+8(A1)
	MOVE.L D7,(160*15)(A1)
	MOVE.L D7,(160*15)+8(A1)
	DBF D0,CLR_BB
	RTS
CLS	LEA SCREE1,A0
	LEA SCREE2+LONG,A1
CLR_L	CLR.L (A0)+
	CLR.L (A0)+
	CMPA.L A0,A1
	BGT.S CLR_L
	RTS
PARTE	MACRO RED
	MOVE.L (160*\1)(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(160*\1)(A1)
	MOVE.L (160*\1)+8(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(160*\1)+8(A1)
	ENDM
PARTE0	MACRO RED
	MOVE.L (A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(A1)
	MOVE.L 8(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,8(A1)
	ENDM
P_SPR	PARTE0
	PARTE 1
	PARTE 2
	PARTE 3
	PARTE 4
	PARTE 5
	PARTE 6
	PARTE 7
	PARTE 8
	PARTE 9
	PARTE 10
	PARTE 11
	PARTE 12
	PARTE 13
	PARTE 14
	PARTE 15
	RTS
GET_N1	MOVE.W SUX,D7
	CMP.W #MAXY*8,D7
	BLT.S NOOVXA
	SUB.W #MAXY*8,D7
	MOVE.W D7,SUX
NOOVXA	LEA SINUS1+(MAXX*8),A2
	MOVE.L 0(A2,D7.W),A0
	MOVE.L 4(A2,D7.W),A1
	MOVE.W SUY,D7
	CMP.W #MAXX*8,D7
	BLT.S NOOVYA
	SUB.W #MAXX*8,D7
	MOVE.W D7,SUY
NOOVYA	LEA SINUS1(PC,D7.W),A2
	ADDA.L (A2),A1
	RTS
GET_NA1	MOVE.W DUX,D7
	CMP.W #MAXY*8,D7
	BLT.S NOINXA  
	SUB.W #MAXY*8,D7
	MOVE.W D7,DUX
NOINXA	LEA SINUS1+(MAXX*8),A2
	MOVE.L 0(A2,D7.W),A0
	MOVE.L 4(A2,D7.W),A1
	MOVE.W DUY,D7
	CMP.W #MAXX*8,D7
	BLT.S NOINYA  
	SUB.W #MAXX*8,D7
	MOVE.W D7,DUY
NOINYA	LEA SINUS1(PC,D7.W),A2
	ADDA.L (A2),A1
	RTS
SINUS1	INCBIN 'SENOS.RED'
ESINUS1
GET_N2	MOVE.W SUX,D7
	CMP.W #MAXY*8,D7
	BLT.S NOOVXB
	SUB.W #MAXY*8,D7
	MOVE.W D7,SUX
NOOVXB	LEA SINUS2+(MAXX*8),A2
	MOVE.L 0(A2,D7.W),A0
	MOVE.L 4(A2,D7.W),A1
	MOVE.W SUY,D7
	CMP.W #MAXX*8,D7
	BLT.S NOOVYB
	SUB.W #MAXX*8,D7
	MOVE.W D7,SUY
NOOVYB	LEA SINUS2(PC,D7.W),A2
	ADDA.L (A2),A1
	RTS
GET_NA2	MOVE.W DUX,D7
	CMP.W #MAXY*8,D7
	BLT.S NOINXB  
	SUB.W #MAXY*8,D7
	MOVE.W D7,DUX
NOINXB	LEA SINUS2+(MAXX*8),A2
	MOVE.L 0(A2,D7.W),A0
	MOVE.L 4(A2,D7.W),A1
	MOVE.W DUY,D7
	CMP.W #MAXX*8,D7
	BLT.S NOINYB  
	SUB.W #MAXX*8,D7
	MOVE.W D7,DUY
NOINYB	LEA SINUS2(PC,D7.W),A2
	ADDA.L (A2),A1
	RTS
SINUS2	DS.B ESINUS1-SINUS1
HBLON	MOVE.W #$2700,SR
	MOVE.L $70.W,OLD70
	MOVE.L $120.W,OLD120
	MOVE.B $FFFFFA07.W,OLD7
	MOVE.B $FFFFFA09.W,OLD9
	CLR.B $FFFFFA07.W
	CLR.B $FFFFFA09.W
	MOVE.L #VBL,$70
	MOVE.W #$2300,SR
	RTS
HBLOFF	MOVE.W #$2700,SR
	MOVE.B OLD7,$FFFFFA07.W
	MOVE.B OLD9,$FFFFFA09.W
	MOVE.L OLD120,$120.W
	MOVE.L OLD70,$70.W
	MOVE.W #$2300,SR
	RTS
VBL	MOVE.L #COLOR,COLORZ
	ST SYNC
	MOVE.B #0,$FFFF820A.W
	MOVE.L #HBL1,$120.W
	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	OR.B #1,$FFFFFA07.W
	OR.B #1,$FFFFFA13.W
	RTE
HBL1	MOVE.B #2,$FFFF820A.W
	CLR.B $FFFFFA1B.W
	MOVE.B #210,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #HBL2A,$120.W
	BCLR #0,$FFFFFA0F.W
	RTE
HBL2A	MOVE.L A0,-(A7)
	ADDQ.L #2,COLORZ
	MOVEA.L COLORZ,A0
	MOVE.W (A0),$FFFF8242.W
	MOVE.L (A7)+,A0
	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #HBL2B,$120.W
	BCLR #0,$FFFFFA0F.W
	RTE
HBL2B	MOVE.L A0,-(A7)
	ADDQ.L #2,COLORZ
	MOVEA.L COLORZ,A0
	MOVE.W (A0),$FFFF8242.W
	MOVE.L (A7)+,A0
	CMP.L #COLOR+(2*18),COLORZ
	BNE.S SABT
	MOVE.L #HBL2,$120.W
SABT	BCLR #0,$FFFFFA0F.W
	RTE
HBL2	MOVE.B #0,$FFFF820A.W
	REPT 6
	NOP
	ENDR
	MOVE.B #2,$FFFF820A.W
	MOVE.L A0,-(A7)
	ADDQ.L #2,COLORZ
	MOVEA.L COLORZ,A0
	MOVE.W (A0),$FFFF8242.W
	MOVE.L (A7)+,A0
	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #HBL3,$120.W
	BCLR #0,$FFFFFA0F.W
	RTE
HBL3	MOVE.B #2,$FFFF820A.W
	MOVE.L A0,-(A7)
	ADDQ.L #2,COLORZ
	MOVEA.L COLORZ,A0
	MOVE.W (A0),$FFFF8242.W
	MOVE.L (A7)+,A0
	CLR.B $FFFFFA1B.W
	MOVE.B #1,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	MOVE.L #HBL3A,$120.W
	BCLR #0,$FFFFFA0F.W
	RTE
HBL3A	MOVE.L A0,-(A7)
	ADDQ.L #2,COLORZ
	MOVEA.L COLORZ,A0
	MOVE.W (A0),$FFFF8242.W
	MOVE.L (A7)+,A0
	BCLR #0,$FFFFFA0F.W
	RTE
DESL	EQU 212*160
LLE1	EQU BASE+DESL
LLE2	EQU BASE+DESL+(8*4)
LLE3	EQU BASE+DESL+(8*4*2)
LLE4	EQU BASE+DESL+(8*4*3)
LLE5	EQU BASE+DESL+(8*4*4)
LLE6	EQU BASE+DESL+(8*4*5)

PSTEP	MACRO numero	
	MOVE.W (A0)+,D0
	MOVE.W D0,8*\1(A1)
	MOVE.W D0,(160*1)+(8*\1)(A1)
	MOVE.W D0,(160*2)+(8*\1)(A1)
	MOVE.W D0,(160*3)+(8*\1)(A1)
	MOVE.W D0,(160*4)+(8*\1)(A1)
	MOVE.W D0,(160*5)+(8*\1)(A1)
	MOVE.W D0,(160*6)+(8*\1)(A1)
	ENDM

ZOOMCH	EQU 7

SC_BIG	ADDQ.W #1,COFF
	CMP.W #1,COFF
	BEQ COKK1
	CMP.W #2,COFF
	BEQ COKK2
	CMP.W #3,COFF
	BEQ COKK3
	CMP.W #4,COFF
	BEQ COKK4
	CMP.W #5,COFF
	BEQ COKK5
	CMP.W #6,COFF
	BEQ COKK6
	CMP.W #7,COFF
	BEQ COKK7
COKK8	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL3
	MOVEA.L (A2)+,A0
	LEA LLE2-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE3-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE4-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE5-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE6-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINT33
	ADDQ.L #4,C_LEE
	MOVE.L C_LEE,A0
	MOVE.L 5*4(A0),D0
	BPL.S NOFIN
	MOVE.L #STEXT,C_LEE
NOFIN	CLR.W COFF
	RTS
COKK1	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE2,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE3,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE4,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE5,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	RTS
COKK2	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE2,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE3,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE4,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE5,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	RTS
COKK3	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL1
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE2-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE3-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE4-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE5-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE6-8,A1
	ADD.L SCREEN,A1
	BSR PRINT11
	RTS
COKK4	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL1
	MOVEA.L (A2)+,A0
	LEA LLE2-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE3-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE4-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE5-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE6-8,A1
	ADD.L SCREEN,A1
	BSR PRINT11
	RTS
COKK5	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL2
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE2-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE3-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE4-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE5-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE6-16,A1
	ADD.L SCREEN,A1
	BSR PRINT22
	RTS
COKK6	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL2
	MOVEA.L (A2)+,A0
	LEA LLE2-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE3-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE4-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE5-16,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	LEA LLE6-16,A1
	ADD.L SCREEN,A1
	BSR PRINT22
	RTS
COKK7	MOVEA.L C_LEE,A2
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE1,A1
	ADD.L SCREEN,A1
	BSR PRINTL3
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE2-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE3-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE4-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE5-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINTLE
	MOVEA.L (A2)+,A0
	ADD.L #4*2*8*80,A0
	LEA LLE6-16-8,A1
	ADD.L SCREEN,A1
	BSR PRINT33
	RTS
ROTAX	LEA FONT,A0
	LEA RFONT,A1
	MOVE.W #(8*80)-1,D0
COPIAL2	MOVE.B #0,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	MOVE.B (A0)+,(A1)+
	ADDQ.L #1,A0
	DBF D0,COPIAL2
	RTS	
PRINTLE	MOVEQ #7-1,D1
PLE	PSTEP 0
	PSTEP 1
	PSTEP 2
	PSTEP 3
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE
	RTS
PRINTL1	MOVEQ #7-1,D1
PLE1	ADDQ.L #2,A0
	PSTEP 0
	PSTEP 1
	PSTEP 2
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE1
	RTS
PRINTL2	MOVEQ #7-1,D1
PLE2	ADDQ.L #2*2,A0
	PSTEP 0
	PSTEP 1
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE2
	RTS
PRINTL3	MOVEQ #7-1,D1
PLE3	ADDQ.L #2*3,A0
	PSTEP 0
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE3
	RTS
PRINT11	MOVEQ #7-1,D1
PLE11	PSTEP 0
	ADDQ.W #3*2,A0
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE11
	RTS
PRINT22	MOVEQ #7-1,D1
PLE22	PSTEP 0
	PSTEP 1
	ADDQ.W #2*2,A0
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE22
	RTS
PRINT33	MOVEQ #7-1,D1
PLE33	PSTEP 0
	PSTEP 1
	PSTEP 2
	ADDQ.W #1*2,A0
	LEA 160*ZOOMCH(A1),A1
	DBF D1,PLE33
	RTS
SETTEXT	LEA TEXT,A0
	LEA STEXT,A1
LOOOPP	MOVEQ #0,D0
	MOVE.B (A0)+,D0
	BMI.S YAHHJ
	BSR.S SEARCH
	MULU #2*4*8,D0
	ADD.L #FONT,D0
	MOVE.L D0,(A1)+
	BRA.S LOOOPP
YAHHJ	MOVE.L #-1,(A1)+
	RTS
SEARCH	MOVEQ #-1,D1
	LEA CHAR,A2
SERT	MOVE.B (A2)+,D2
	BMI.S ENDS
	ADDQ.W #1,D1
	CMP.B D2,D0
	BNE.S SERT
ENDS	MOVEQ #0,D0
	MOVE.B D1,D0
	RTS
CHAR	DC.B "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789:!. \_+-=?(),'",'"',-1
GETFONT	LEA FONT,A0
	LEA RFONT,A2
QLLOO	MOVE.W (A0),D0
	CMP.W #0,D0
	BEQ.S CERO
	CMP.W #$0100,D0
	BEQ.S UNIZ
	CMP.W #$0001,D0
	BEQ.S UNDE
UNUN	MOVE.W #%1111111111111111,(A0)+
COMPAR	CMPA.L A0,A2
	BGT.S QLLOO
	RTS
CERO	MOVE.W #0,(A0)+
	BRA.S COMPAR
UNIZ	MOVE.W #%1111111100000000,(A0)+
	BRA.S COMPAR
UNDE	MOVE.W #%0000000011111111,(A0)+
	BRA.S COMPAR
C_LEE	DC.L STEXT
COFF	DS.W 1
TEXT	DC.B "     HI GUYS! THIS IS ANOTHER FUCKING BALL DEMO, HOW MANY BALL-SCREENS HAVE YOU EVER SEEN? OK, I KNOW IT'S NOT ORIGINAL, BUT I HAD TO MAKE IT COZ I HAVEN'T DONE ANY BALL-SCREEN BEFORE. LET'S TELL YOU HOW "
	DC.B " TO OPERATE THIS STUFF. KEYS 1-2 X INC + X SUB, 3-4 Y INC + Y SUB, 5-6 X TABLE INC + X TABLE SUB, 7-8 Y TABLE INC + Y TABLE SUB. F1-F10 PREDEFINED WAVES. MEGA THANX TO -REALIDAD BINARIA- FROM ELECTRO SOFT "
	DC.B "FOR EXPLAINING ME HOW TO MAKE SOMETHING LIKE THIS. "
	DC.B "MAYBE YOU ARE WONDERING ABOUT THE GREAT IDEA OF PUTTING THIS SCROLL DOWNSIDE THE SCREEN. OK I HAD SUCH STUPID IDEA!!! NO SCREENS FINISHED, NO SAMPLES READY, NO GRAPHIX DRAWN, NO INTELLIGENCE, NO ELECTRICITY... THAT'S ME (THE 0 MAN) "
	DC.B "LETTING NOSENSES DOWN I THINK THIS SCREEN IS NOT LOOKING PRETTY GOOD, BUT YOU HAVE TO CONSIDER THAT IT'S MY SECOND SCREEN SO FAR. DEEP SCROLLER IS NOT FINISHED YET, AND I THINK THIS ONE WILL BE FINISHED SOON... NOW THE CREDITS:"
	DC.B "GRAPHIX + RIPPING MUZAK + CODING ... RED DEVIL FROM TDL OR THE DELIVERANCE. THIS SCROLL WAS MADE ONE OR TWO MONTHS AGO, AND "
	DC.B "THE MOMENT I'M WRITING THIS AND, AT THE MOMENT I HAVE MUCH BETTER SCROLL ROUTS (4 PLANE, 25 LINES, VARIBLE WIDE AND NO SPACE "
	DC.B "BETWEEN LETTERS: THAT'S ABOUT HALF OF THE UPPER BORDER IN VBL TIME, IT'S CALLED TSC SCROLL TECNIX). WELL I HAVE GEOGRAPHY + LITERATURE FINAL EXAMINATIONS TOMOROW, AND HAVE TO WAKE UP  AT 5:00 AM SO GOTTA GO TO BED NOW GUYS, BYE      OK, "
	DC.B "I HAVE DONE BOTH GEOGRAPHY AND LITERATURE EXAMS AND DID IT VERY WELL (I THINK SO). ONTO OTHER MATTER... I HAVE FINISHED SCHOOL FOR THIS YEAR (WOW). NOW SUMMER... I'LL GO TO ENGLAND ON 10TH JULY AND COME BACK ON 20TH AUGUST, BUT DON'T CARE "
	DC.B "COZ I THINK I WILL WRAZ NOW... (FINAL NOTE: TODAY I HAVE LINKED THE SCROLL, DRAWN THE LOGO AND FLASH IT...NICE WORK, A PITY I'M GOING TO BED....) BYE FRIENDS, DOGS, CATS, COMPUTERS AND OTHER ANIMALS AROUND "
	DC.B "(!!).... (NOW IT'S ABOUT 11:30 PM AND THE "
	DC.B "STREET IS BLEAK)..... DEFINITIVELY BYE  NOW....       "
	DC.B -1
STEXT	DS.L (STEXT-TEXT)	
SCREEN	DS.L 1
OLD70	DS.L 1
OLD120	DS.L 1
SYNC	DS.W 1
OLD7	DS.B 1
OLD9	DS.B 1
COLORZ	DS.L 1
INCX	DS.W 1
INCY	DS.W 1
DOX	DS.W 1
DOY	DS.W 1
DUX	DS.W 1
DUY	DS.W 1
SUX	DS.W 1
SUY	DS.W 1
BALLX1	DC.W 1
LOG_SC	DS.L 1
DAT_BO1	REPT BOLAS+1
	DC.L SCREE2
	ENDR
DAT_BO2	REPT BOLAS+1
	DC.L SCREE1
	ENDR
SPR_OR	DS.W (2*4)*16*16
PRO	INCBIN 'M_BALL.PI1'	
COLOR	DC.W $000,$200,$400,$600,$700
	DC.W $710,$720,$730,$740,$750
	DC.W $760,$770,$771,$772,$773
	DC.W $774,$775,$776,$777
	REPT 18
	DC.W $777
	ENDR
	DC.W $777,$776,$775,$774
	DC.W $773,$772,$771,$770,$760
	DC.W $750,$740,$730,$720,$710
	DC.W $700,$600,$400,$200,$000
MUZAX	INCBIN 'JAMBALA.MUS'
FONT	INCBIN 'FONTB8P.RED'
RFONT
	

	
	
	
	
	
		
	
	