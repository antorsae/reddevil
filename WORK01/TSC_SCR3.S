** TSC SCROLLER. PRIMERO PILLA LAS LETRAS
** CON SU LONGITUD, Y LUEGO GENERA LAS TABLAS
** DE DIRECCIONES CON EL TEXTO.
** LA TECNICA CONSISTE EN TENER 2 PANTALLAS
** UNA CON EL TEXTO ROTADO 8 PIX Y OTRA NORMAL
** IR ALTERNANDOLAS Y MIENTRAS HACER UN BARRIDO
** CON 8 BYTES (A ROWS). ADEMAS MIDE LO QUE DURA
** CADA LETRA.

** (C)  V.-1.02 1990 RED DEVIL


LIN	EQU 25
	
	PEA 0
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 6(A7),A7
	LEA $2000,SP
	BSR SUBERL
	BSR CLETRAS
	BSR SETTEXT
	
ME	MOVE.W #$25,-(A7)
	TRAP #14
	LEA 2(A7),A7
	MOVE.W #$666,$FFFF8240.W
	BSR TSC
	MOVE.W #$777,$FFFF8240.W
	BRA.S ME
	CLR.L -(A7)
	TRAP #1
CHAR	DS.W 1
CLETRAS	LEA FONT,A1
OTRA_L	BSR C_CLR
	LEA PIX_F+34,A0
	LEA $F8000,A3
	BSR C_LET
	BSR SAVE_L
	BSR S_ROT
	BSR SAVE_L
	ADDQ.W #1,CHAR
	MOVE.W CHAR,D0
	LEA CHARSET,A2
	MOVE.B 0(A2,D0.W),D0
	BPL.S OTRA_L
	RTS
SAVE_L	LEA $F8000,A0
	MOVEQ.L #0,D2
	MOVE.W #LIN-1,D1
	MOVE.W LCH,D2
S_VER	MOVE.W D2,D0
S_HOR	MOVE.L (A0),(A1)+
	MOVE.L 4(A0),(A1)+
	ADDQ.L #8,A0
	DBF D0,S_HOR
	ADD.L #160,A0
	MOVE.L D2,D0
	ADDQ.B #1,D0
	MULU #8,D0
	SUBA.W D0,A0
	DBF D1,S_VER
	RTS
C_CLR	LEA $F8000,A2
	MOVE.W #LIN-1,D7
	MOVEM.L BASURA,D0-D6/A3-A5
CL_L	MOVEM.L D0-D6/A3-A5,(A2)
	LEA 160(A2),A2
	DBF D7,CL_L
	RTS	
S_ROT	MOVE.W #8-1,D2
ROTA_T	LEA $F8000,A2
	MOVE.W #LIN-1,D1
UNA_L	MOVE.W #4-1,D0
UN_PL	MOVE.W #0,CCR
	ROXR (A2)
	ROXR 8(A2)
	ROXR 8*2(A2)
	ROXR 8*3(A2)
	ROXR 8*4(A2)
	ADDQ.L #2,A2
	DBF D0,UN_PL
	LEA 160-(2*4)(A2),A2
	DBF D1,UNA_L 
	DBF D2,ROTA_T
	RTS
SUBERL	LEA LONGC,A2
	LEA ELONGC,A3
SUB_L	MOVE.B (A2),D0
	SUB.B #'0',D0
	MOVE.B D0,(A2)+
	CMPA.L A2,A3
	BGT.S SUB_L
	RTS
C_LET	LEA LONGC,A2
	MOVE.W CHAR,D7
	MOVE.W D7,D6
	MOVEQ.L #0,D0
	MOVEQ.L #0,D1
SUMAS_S	ADD.B (A2)+,D0
	DBF D6,SUMAS_S
	SUBQ.L #1,A2
	DIVU #20,D0
	MOVE.W D0,D1
	CLR.W D0
	SWAP D0
	MULU #8,D0
	MULU #160*LIN,D1
	ADD.L D0,D1
	LEA 0(A0,D1.W),A0
	BSR COP_LE
	RTS
COP_LE	MOVEQ.L #0,D2
	MOVE.W #LIN-1,D1
	MOVE.B (A2),D2
	MOVE.W D2,LCH
	SUBQ.B #1,D2
C_VER	MOVE.W D2,D0
C_HOR	MOVE.L (A0),(A3)
	MOVE.L 4(A0),4(A3)
	ADDQ.L #8,A0
	ADDQ.L #8,A3
	DBF D0,C_HOR
	ADD.L #160,A0
	ADD.L #160,A3
	MOVE.L D2,D0
	ADDQ.B #1,D0
	MULU #8,D0
	SUBA.W D0,A0
	SUBA.W D0,A3
	DBF D1,C_VER
	RTS
SCREE1	EQU $F8000
SCREE2	EQU $F8000-$7D00
POINT	EQU 19*8
CD	DS.W 1
TSC	EOR.B #-1,CD
	BNE.S PARTE2
PARTE1	MOVE.L #SCREE2,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	BSR BARRE1
	MOVE.L C_TEXT,A2
	LEA SCREE1+POINT,A1
	CMP.W #8,PAS1
	BNE.S NO_01
	BSR OREA1
	RTS
PARTE2	MOVE.L #SCREE1,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	BSR BARRE2
	MOVE.L C_TEXT,A2
	LEA SCREE2+POINT,A1
	CMP.W #8,PAS2
	BNE.S NO_02
	BSR OREA2
	RTS
MEZCL	MOVE.W #LIN-1,D7
OR_L	MOVE.L (A0),D1
	OR.L D1,(A1)
	MOVE.L 4(A0),D1
	OR.L D1,4(A1)
	ADDA.W D0,A0
	LEA 160(A1),A1
	DBF D7,OR_L
	RTS
NO_01	SUBQ.W #8,PAS1
	MOVE.W PAS1,D1
	MOVE.W PXS1,D0
	SUB.W D1,D0
	MOVE.L 2(A2),A0
	LEA 0(A0,D0.W),A0
	ADD.W D1,D0
	BSR METE_L
	CMP.W #8,PAS1
	BNE.S GO_OUT1
OREA1	MOVE.W (A2),D0
	MOVE.W D0,PXS1
	MOVE.W D0,PAS1
	MOVE.L 2(A2),A0
	LEA SCREE1+POINT,A1
	BSR MEZCL
GO_OUT1	RTS
NO_02	SUBQ.W #8,PAS2
	MOVE.W PAS2,D1
	MOVE.W PXS2,D0
	SUB.W D1,D0
	MOVE.L 2(A2),A0
	ADDA.W 6(A2),A0 
	LEA 0(A0,D0.W),A0
	ADD.W D1,D0
	BSR METE_L
	CMP.W #8,PAS2
	BNE.S GO_OUT2
OREA2	MOVEQ #8,D1
	ADDA.L D1,A2
	ADD.L D1,C_TEXT
	MOVE.W (A2),D0
	BNE.S NO_FINA
	MOVE.L #STEXT,D1
	MOVE.L D1,C_TEXT
	MOVEA.L D1,A2
	MOVE.W (A2),D0
NO_FINA	MOVE.W D0,PXS2
	MOVE.W D0,PAS2
	MOVE.L 2(A2),A0
	ADD.W 6(A2),A0	
	LEA SCREE2+POINT,A1
	BSR MEZCL
GO_OUT2	RTS
PAS1	DC.W 8
PXS1	DC.W 8
PAS2	DC.W 8
PXS2	DC.W 8
METE_L	MOVE.W #LIN-1,D7
PURT_L	MOVE.L (A0),(A1)
	MOVE.L 4(A0),4(A1)
	ADDA.W D0,A0
	LEA 160(A1),A1
	DBF D7,PURT_L
	RTS
BARRE1	LEA SCREE1,A0
	MOVE.W #LIN-1,D7
BAR_L1	MOVEM.L 8(A0),D0-D6/A1-A6
	MOVEM.L D0-D6/A1-A6,(A0)
	MOVEM.L 8+(13*4*1)(A0),D0-D6/A1-A6
	MOVEM.L D0-D6/A1-A6,(13*4*1)(A0)
	MOVEM.L 8+(13*4*2)(A0),D0-D6/A1-A5
	MOVEM.L D0-D6/A1-A5,(13*4*2)(A0)
	LEA 160(A0),A0
	DBF D7,BAR_L1
	RTS
BARRE2	LEA SCREE2,A0
	MOVE.W #LIN-1,D7
BAR_L2	MOVEM.L 8(A0),D0-D6/A1-A6
	MOVEM.L D0-D6/A1-A6,(A0)
	MOVEM.L 8+(13*4*1)(A0),D0-D6/A1-A6
	MOVEM.L D0-D6/A1-A6,(13*4*1)(A0)
	MOVEM.L 8+(13*4*2)(A0),D0-D6/A1-A5
	MOVEM.L D0-D6/A1-A5,(13*4*2)(A0)
	LEA 160(A0),A0
	DBF D7,BAR_L2
	RTS
LCH	DS.W 1
BASURA	DS.L 16
SETTEXT	LEA TEXT,A0
	LEA STEXT,A1
S_TEX	MOVEQ #0,D0
	MOVE.B (A0)+,D0
	BSR C_CODE
	LSL.W #3,D3
	MOVE.W D3,(A1)+
	MOVE.L D1,(A1)+
	MULU #LIN,D3
	MOVE.W D3,(A1)+
	TST.B (A0)
	BPL.S S_TEX
	MOVE.L #0,(A1)+
	MOVE.L #0,(A1)+
	RTS
C_CODE	MOVEQ #-1,D1
	LEA CHARSET,A2
SEARCH	MOVE.B (A2)+,D2
	ADDQ.W #1,D1
	CMP.B D0,D2
	BNE.S SEARCH
	LEA LONGC,A3
	LEA LONGC(PC,D1),A2
	MOVEQ #1,D3
	ADD.B (A2),D3
	MOVEQ #0,D1
	MOVEQ #0,D0
SUM_L	MOVE.B (A3)+,D0
	ADDQ.B #1,D0
	ADD.W D0,D1
	CMPA.L A3,A2
	BGT.S SUM_L
	MULU #LIN*8*2,D1
	ADD.L #FONT,D1
	RTS
I_SX	DS.W 1
C_TEXT	DC.L STEXT
C_LETRA	DS.W 1
CHARSET	DC.B 'ABCDEFGHIJKLMNOPQRSTUVWXYZ ',-1
LONGC
	DC.B '222222222222222222222222222',-1
ELONGC
PIX_F	INCBIN 'FONTER4P.PI1'
TEXT	DC.B 'YEAJHH  NOW LETS ASASSSSSSSSSSSSSSSSSSSSSSSSSSSSS',-1 
STEXT	DS.B (STEXT-TEXT)*8
	
W_AREA
FONT	EQU W_AREA