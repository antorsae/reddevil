** RUTINA DE SPRITES V 1.03

** (C) RED DEVIL

	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 6(A7),A7
	BSR MAKESPR
	LEA SPR_OR,A0
	LEA $F8000,A1
	BSR P_SPR
	CLR.L -(A7)
	TRAP #1
PSPR	MACRO LINERX
	MOVE.L (160*\1)(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(160*\1)(A1)
	MOVE.L (160*\1)+8(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(160*\1)+8(A1)
	ENDM
PSPR0	MACRO LINERX
	MOVE.L (A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,(A1)
	MOVE.L 8(A1),D0
	AND.L (A0)+,D0
	OR.L (A0)+,D0
	MOVE.L D0,8(A1)
	ENDM
P_SPR	PSPR0
	PSPR 1
	PSPR 2
	PSPR 3
	PSPR 4
	PSPR 5
	PSPR 6
	PSPR 7
	PSPR 8
	PSPR 9
	PSPR 10
	PSPR 11
	PSPR 12
	PSPR 13
	PSPR 14
	PSPR 15
	RTS
MAKESPR	LEA PRO+34+(88*160),A0
	LEA $F8000,A1
	BSR C_SP
	LEA PRO+34+((88+16)*160),A0
	LEA $F8000+(160*17),A1
	BSR C_SP
	MOVEQ #16-1,D7
	LEA SPR_OR,A1
ROT_OR	BSR SAVE_SP
	LEA $F8000,A0
	BSR ROX_OR
	LEA $F8000+(160*17),A0
	BSR ROX_AND
	DBF D7,ROT_OR
	RTS
C_SP	MOVEQ #16-1,D7
CPSLOOP	MOVE.L (A0),(A1)
	MOVE.L 4(A0),4(A1)
	MOVE.L 8(A0),8(A1)
	MOVE.L 8+4(A0),8+4(A1)
	LEA 160(A0),A0
	LEA 160(A1),A1
	DBF D7,CPSLOOP
	RTS
SAVE_SP	LEA $F8000,A0
	MOVEQ #16-1,D6
SVLOOP	MOVE.L (160*17)(A0),(A1)+
	MOVE.L (A0),(A1)+
	MOVE.L (160*17)+8(A0),(A1)+
	MOVE.L 8(A0),(A1)+
	LEA 160(A0),A0
	DBF D6,SVLOOP
	RTS
ROX_OR	MOVEQ #16-1,D6
ROXLIN	MOVEQ #4-1,D5
ROXPLA	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8(A0)
	ADDQ.L #2,A0
	DBF D5,ROXPLA
	LEA 160-(4*2)(A0),A0
	DBF D6,ROXLIN
	RTS
ROX_AND	MOVEQ #16-1,D6
ROXLINA	MOVEQ #4-1,D5
ROXPLAA	MOVE.W #-1,CCR
	ROXR (A0)
	ROXR 8(A0)
	ADDQ.L #2,A0
	DBF D5,ROXPLAA
	LEA 160-(4*2)(A0),A0
	DBF D6,ROXLINA
	RTS
SPR_OR	DS.B (2*4)*16*16
	DS.B (2*4)*16*16
PRO	INCBIN 'DEMO2_LG.PI1'	

	
	
	
	
	
		
	
	