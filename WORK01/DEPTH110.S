** (C) 1990 RED DEVIL + SCRATCHIN.
** FALTA TODAVIA UNA NUEVA FUENTE EN
** EL SCROLL DE LA CARRETERA, ADEMAS
** FALTA CAMBIAR LA MUSICA DIGITALIZADA

** RUTINAS: RED DEVIL 
** MUSICA SAMPLEADA POR: SCRACHIN 
** DIBUJOS: RED DEVIL (FONT) Y RIPPEDADOS DE POR AHI.

** V 1.101 PRESCINDE DE D6-D7/A5-A6 DURANTE
** EL VBL (C) 1990 RED DEVIL

LE1	EQU $F6000+(210*160)
LE2	EQU $F6000+(3*8)+(210*160)
LE3	EQU $F6000+((3*8)*2)+(210*160)
LE4	EQU $F6000+((3*8)*3)+(210*160)
LE5	EQU $F6000+((3*8)*4)+(210*160)
LE6	EQU $F6000+((3*8)*5)+(210*160)
LE7	EQU $F6000+((3*8)*6)+(210*160)
LE8	EQU $F6000+((3*8)*7)+(210*160)
LINES	EQU 2
	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 2(A7),A7
	LEA $2000,A7
	BSR SET_EVE
	BSR HBL_ON
KEY_W	CLR.B SYNC_70
SINJ	TST.B SYNC_70
	BEQ.S SINJ
	
	BSR SCINT 
	BSR MONTSC
	BSR SCROLL
	
	CMP.B #$39,$FFFFFC02.W
	BNE.S KEY_W
	BSR HBL_OFF
	MOVEM.L PIC+2,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	CLR.L -(A7)
	TRAP #1
SET_EVE	BSR TEXTSS
	BSR CLCHIP
	BSR DSAMPLE
	BSR HAZ_RO
	BSR SET_TEX
	LEA PIC+34,A0
	LEA $F6000,A1
	MOVE.W #(32000/8)-1,D0
CPNAT	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D0,CPNAT
	MOVEM.L PIC+2,D0-D7
	MOVEM.L D0-D7,$FFFF8240.W
	MOVE.L #$F6000,D0
	LSR.L #8,D0
	MOVE.B D0,$FFFF8203.W
	LSR.W #8,D0
	MOVE.B D0,$FFFF8201.W
	RTS
SCINT	BSR.S INBUF
	BSR OUTBUF
	RTS
INBUF	LEA COLOR_B+((100/LINES)*16*2),A0
	REPT 50
	MOVEM.L 16*2(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,(A0)
	LEA 16*2(A0),A0
	ENDR
FIN	ADD.W #16*2,CPARTE
	CMP.W #16*2*12,CPARTE
	BLE.S NADATIO
	CLR.W CPARTE
	ADDQ.L #4,CLETRA
NADATIO	MOVEA.L CLETRA,A0
	MOVEA.L (A0),A0
	MOVE.L A0,D0
	BPL.S NOESF
	MOVE.L #TEXTCL,CLETRA
	MOVEA.L TEXTCL,A0
NOESF	ADDA.W CPARTE,A0
	MOVEM.L (A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,COLOR_B+((200/LINES)-1)*16*2)	
	RTS
OUTBUF	LEA COLOR_B+((100/LINES)*16*2),A0
	LEA COLORAB+(((120-60)/LINES)*16*2),A1
	MOVEM.L (A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,(A1)
	MOVEM.L 16*2*3(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,16*2(A1)
	MOVEM.L 16*2*6(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,16*2*2(A1)
	MOVEM.L 16*2*9(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,16*2*3(A1)
	MOVEM.L 16*2*11(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,16*2*4(A1)
	MOVEM.L 16*2*13(A0),D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,16*2*5(A1)
	LEA 16*2*14(A0),A0
	LEA 16*2*6(A1),A1
	REPT 40-6
	MOVEM.L (A0)+,D0-D5/A3-A4
	MOVEM.L D0-D5/A3-A4,(A1)
	LEA 16*2(A1),A1
	ENDR
	RTS
HBL_ON	LEA $FFFF8800.W,A5
	LEA SAMPLE,A6
	MOVE.W #$2700,SR
	MOVE.L $70.W,OLDVBL
	MOVE.L $68.W,OLDFRE
	MOVE.L $120.W,OLDHBL
	MOVE.B $FFFFFA09.W,OLDR9
	MOVE.B $FFFFFA0F.W,OLDRF
	MOVE.B $FFFFFA07.W,OLDR7
	MOVE.B $FFFFFA13.W,OLDR13
	MOVE.B #0,$FFFFFA09.W
	MOVE.B #1,$FFFFFA07.W
	MOVE.B #1,$FFFFFA13.W
	MOVE.L #N_HBL,$120.W
	MOVE.L #N_VBL,$70.W
	MOVE.L #SAM,$68.W
	MOVE.W #$2100,SR
	RTS
HBL_OFF	CLR.B SYNC_70
SYNK	TST.B SYNC_70
	BEQ.S SYNK
	MOVE.W #$2700,SR
	MOVE.L OLDVBL,$70.W
	MOVE.L OLDHBL,$120.W
	MOVE.L OLDFRE,$68.W
	MOVE.B OLDR9,$FFFFFA09.W
	MOVE.B OLDRF,$FFFFFA0F.W
	MOVE.B OLDR7,$FFFFFA07.W
	MOVE.B OLDR13,$FFFFFA13.W
	MOVE.W #$2300,SR
	RTS
N_VBL	ST SYNC_70
	MOVE.L #COLORAB,COLOR_N
	MOVE.L #COLOR0,BTS
	MOVE.L #N_HBL,$120.W
	CLR.B $FFFFFA1B.W
	MOVE.B #30*2,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	CMPA.L #ESAMPLE,A6
	BLT.S NOFIN
	LEA SAMPLE,A6 
NOFIN	MOVEM.L D0-D5/A0-A1,-(A7)
	MOVEM.L PIC+2,D0-D5/A0-A1
	MOVEM.L D0-D5/A0-A1,$FFFF8240.W
	MOVEM.L (A7)+,D0-D5/A0-A1
	RTE
N_HBL	MOVE.L A0,-(A7)
	ADDQ.L #2,BTS
	ADD.L #2*16,COLOR_N
	MOVEA.L BTS,A0
	MOVE.W (A0),$FFFF8240.W
	MOVE.L #HHBL,$120.W
	CLR.B $FFFFFA1B.W
	MOVE.B #LINES,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
	BCLR #0,$FFFFFA0F.W
	MOVE.L (A7)+,A0
	RTE
HHBL	MOVEM.L A0-A1,-(A7)
	ADDQ.L #2,BTS
	ADD.L #2*16,COLOR_N
	CMP.L #((120*2)/LINES)-58+COLOR0,BTS
	BLT.S ANADAY
	MOVEA.L COLOR_N,A0
	ADDQ.L #2,A0
	LEA $FFFF8242.W,A1
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.W (A0)+,(A1)+
	CMP.L #(70*2)+COLOR0,BTS
	BNE.S ANADAY
	MOVE.L #BORDER,$120.W
	CLR.B $FFFFFA1B.W
	MOVE.B #2,$FFFFFA21.W
	MOVE.B #8,$FFFFFA1B.W
ANADAY	MOVEA.L BTS,A0
	MOVE.W (A0),$FFFF8240.W
	MOVEM.L (A7)+,A0-A1
	BCLR #0,$FFFFFA0F.W
	RTE
BORDER	BCLR #1,$FFFF820A.W
	NOP
	NOP
	NOP
	BSET #1,$FFFF820A.W
	MOVEM.L A0-A1,-(A7)
	LEA PICA+2,A0
	LEA $FFFF8240.W,A1
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	MOVEM.L (A7)+,A0-A1 
	BCLR #0,$FFFFFA0F.W
	MOVE.B #0,$FFFFFA1B.W
	RTE
TEXTSS	LEA TEXTC,A0
	LEA TEXTCL,A1
LOOPE	MOVEQ.L #0,D0
	MOVE.B (A0)+,D0
	BMI.S NEGH
	BSR.S WATTCH
	MULU #16*2*13,D0
	ADD.L #FONT,D0
	MOVE.L D0,(A1)+
	MOVE.L D0,D2
	BRA.S LOOPE
NEGH	MOVE.L D2,(A1)+
	MOVE.L #-1,(A1)
	RTS
WATTCH	LEA CHARS,A6
	MOVEQ #0,D6
	MOVEQ #0,D7
FIND_L	MOVE.B (A6)+,D7
	CMP.B D7,D0
	BEQ.S FOUND
	ADDQ.B #1,D6
	CMPA.L #ECHARS,A6
	BLT.S FIND_L
	RTS
FOUND	MOVE.L D6,D0
	RTS
CHARS	DC.B 'ABCDEFGHIJKLMNOPQRSTUVWXYZ-+.,()0123456789"',"' "
ECHARS
DSAMPLE	LEA SAMPLE,A0
	LEA W_AREA,A1
	LEA ES1,A2
SREGS	MOVEQ #0,D0
	MOVE.B (A0)+,D0
	LSL.W #3,D0
	MOVE.W D0,(A1)+
	CMPA.L A0,A2
	BGT.S SREGS
	SUBA.L #2,A1
	MOVEA.L A1,A2
	LEA SAMPLE,A1
	LEA W_AREA,A0
C_REDA	MOVE.W (A0)+,(A1)+
	CMPA.L A0,A2
	BGT.S C_REDA
	RTS
CLCHIP	MOVEQ #$A,D0
CHI	MOVE.B D0,$FFFF8800.W
	CLR.B $FFFF8802.W
	DBF D0,CHI
	CLR.B $FFFF8800.W
	CLR.B $FFFF8802.W
	MOVE.B #7,$FFFF8800
	MOVE.B #-1,$FFFF8802
	RTS
MONTSC	SUB.L #15*4*8,PONM
	CMP.L #POSI_SC-(15*4*8),PONM
	BNE.S NOFPL
	MOVE.L #POSI_SC+(15*4*8*15),PONM
NOFPL	MOVEA.L PONM,A0
	LEA LE1+(15*160),A1
	REPT 15
	MOVEM.L (A0)+,D0-D5/A2-A3
	MOVEM.L D0-D5/A2-A3,-(A1)
	MOVEM.L D0-D5/A2-A3,-(A1)
	MOVEM.L D0-D5/A2-A3,-(A1)
	MOVEM.L D0-D5/A2-A3,-(A1)
	MOVEM.L D0-D5/A2-A3,-(A1)
	ENDR
	RTS
SCROLL	MOVEA.L C_SCR,A0
	MOVE.L 12*4(A0),D0
	BPL.S NADA
	MOVE.L #TEXT_P,C_SCR
	LEA TEXT_P,A0
NADA	ADDQ.L #4,CER
	MOVEA.L CER,A1
	MOVEA.L (A1),A1
	JMP (A1)
PUTY1	LEA LE1,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE2,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE3,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE4,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE5,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE6,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE7,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTNE1
	RTS
PUTY2	LEA LE1,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE2,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE3,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE4,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE5,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE6,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE7,A2
	MOVE.L (A0)+,A1
	BSR PUTNE1
	RTS
PUTY3	LEA LE1,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLE1
	LEA LE2-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE3-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE4-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE5-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE6-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE7-8,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	RTS
PUTY4	LEA LE1,A2
	MOVE.L (A0)+,A1
	BSR PUTLE1
	LEA LE2-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE3-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE4-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE5-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE6-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE7-8,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	RTS
PUTY5	LEA LE1,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLE2
	LEA LE2-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE3-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE4-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE5-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE6-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE7-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTLLE
	LEA LE8-16,A2
	MOVE.L (A0)+,A1
	ADD.L #15*3*2,A1
	BSR PUTNE2
	RTS
PUTY6	LEA LE1,A2
	MOVE.L (A0)+,A1
	BSR PUTLE2
	LEA LE2-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE3-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE4-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE5-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE6-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE7-16,A2
	MOVE.L (A0)+,A1
	BSR PUTLLE
	LEA LE8-16,A2
	MOVE.L (A0)+,A1
	BSR PUTNE2
	MOVE.L #LISTSC-4,CER
	ADDQ.L #4,C_SCR
	RTS
PUTLLE	MOVE.W (A1)+,(A2)
	MOVE.W (A1)+,8(A2)
	MOVE.W (A1)+,8*2(A2)
	MOVE.W (A1)+,(160*1)(A2)
	MOVE.W (A1)+,(160*1)+8(A2)
	MOVE.W (A1)+,(160*1)+8*2(A2)
	MOVE.W (A1)+,(160*2)(A2)
	MOVE.W (A1)+,(160*2)+8(A2)
	MOVE.W (A1)+,(160*2)+8*2(A2)
	MOVE.W (A1)+,(160*3)(A2)
	MOVE.W (A1)+,(160*3)+8(A2)
	MOVE.W (A1)+,(160*3)+8*2(A2)
	MOVE.W (A1)+,(160*4)(A2)
	MOVE.W (A1)+,(160*4)+8(A2)
	MOVE.W (A1)+,(160*4)+8*2(A2)
	MOVE.W (A1)+,(160*5)(A2)
	MOVE.W (A1)+,(160*5)+8(A2)
	MOVE.W (A1)+,(160*5)+8*2(A2)
	MOVE.W (A1)+,(160*6)(A2)
	MOVE.W (A1)+,(160*6)+8(A2)
	MOVE.W (A1)+,(160*6)+8*2(A2)
	MOVE.W (A1)+,(160*7)(A2)
	MOVE.W (A1)+,(160*7)+8(A2)
	MOVE.W (A1)+,(160*7)+8*2(A2)
	MOVE.W (A1)+,(160*8)(A2)
	MOVE.W (A1)+,(160*8)+8(A2)
	MOVE.W (A1)+,(160*8)+8*2(A2)
	MOVE.W (A1)+,(160*9)(A2)
	MOVE.W (A1)+,(160*9)+8(A2)
	MOVE.W (A1)+,(160*9)+8*2(A2)
	MOVE.W (A1)+,(160*10)(A2)
	MOVE.W (A1)+,(160*10)+8(A2)
	MOVE.W (A1)+,(160*10)+8*2(A2)
	MOVE.W (A1)+,(160*11)(A2)
	MOVE.W (A1)+,(160*11)+8(A2)
	MOVE.W (A1)+,(160*11)+8*2(A2)
	MOVE.W (A1)+,(160*12)(A2)
	MOVE.W (A1)+,(160*12)+8(A2)
	MOVE.W (A1)+,(160*12)+8*2(A2)
	MOVE.W (A1)+,(160*13)(A2)
	MOVE.W (A1)+,(160*13)+8(A2)
	MOVE.W (A1)+,(160*13)+8*2(A2)
	MOVE.W (A1)+,(160*14)(A2)
	MOVE.W (A1)+,(160*14)+8(A2)
	MOVE.W (A1)+,(160*14)+8*2(A2)
	RTS
PUTLE1	ADDQ.L #2,A1
	MOVE.W (A1)+,(A2)
	MOVE.W (A1)+,8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*1)(A2)
	MOVE.W (A1)+,(160*1)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*2)(A2)
	MOVE.W (A1)+,(160*2)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*3)(A2)
	MOVE.W (A1)+,(160*3)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*4)(A2)
	MOVE.W (A1)+,(160*4)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*5)(A2)
	MOVE.W (A1)+,(160*5)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*6)(A2)
	MOVE.W (A1)+,(160*6)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*7)(A2)
	MOVE.W (A1)+,(160*7)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*8)(A2)
	MOVE.W (A1)+,(160*8)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*9)(A2)
	MOVE.W (A1)+,(160*9)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*10)(A2)
	MOVE.W (A1)+,(160*10)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*11)(A2)
	MOVE.W (A1)+,(160*11)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*12)(A2)
	MOVE.W (A1)+,(160*12)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*13)(A2)
	MOVE.W (A1)+,(160*13)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*14)(A2)
	MOVE.W (A1)+,(160*14)+8(A2)
	RTS
PUTLE2	ADDQ.L #4,A1
	MOVE.W (A1)+,(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*1)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*2)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*3)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*4)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*5)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*6)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*7)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*8)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*9)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*10)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*11)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*12)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*13)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*14)(A2)
	RTS
PUTNE2	MOVE.W (A1)+,(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*1)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*2)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*3)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*4)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*5)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*6)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*7)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*8)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*9)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*10)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*11)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*12)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*13)(A2)
	ADDQ.L #4,A1
	MOVE.W (A1)+,(160*14)(A2)
	RTS
PUTNE1	MOVE.W (A1)+,(A2)
	MOVE.W (A1)+,8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*1)(A2)
	MOVE.W (A1)+,(160*1)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*2)(A2)
	MOVE.W (A1)+,(160*2)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*3)(A2)
	MOVE.W (A1)+,(160*3)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*4)(A2)
	MOVE.W (A1)+,(160*4)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*5)(A2)
	MOVE.W (A1)+,(160*5)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*6)(A2)
	MOVE.W (A1)+,(160*6)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*7)(A2)
	MOVE.W (A1)+,(160*7)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*8)(A2)
	MOVE.W (A1)+,(160*8)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*9)(A2)
	MOVE.W (A1)+,(160*9)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*10)(A2)
	MOVE.W (A1)+,(160*10)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*11)(A2)
	MOVE.W (A1)+,(160*11)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*12)(A2)
	MOVE.W (A1)+,(160*12)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*13)(A2)
	MOVE.W (A1)+,(160*13)+8(A2)
	ADDQ.L #2,A1
	MOVE.W (A1)+,(160*14)(A2)
	MOVE.W (A1)+,(160*14)+8(A2)
	ADDQ.L #2,A1
	RTS
FIL	EQU 8
LEF	EQU 6 
HAZ_RO	LEA PICA+34,A0
	LEA $F8000,A1
	MOVE.W #FIL-1,D7 ;3 LINEAS
MASA	MOVE.W #LEF-1,D6 ;10 POR LINEA
TA	BSR CLS_TR
	BSR COP_LE
	BSR SAVE_L
	LEA $F8000,A1
	MOVE.W #15-1,D0
	MOVE.W #4-1,D1
	MOVE.W #8-1,D2
	BSR ASRI
	BSR SAVE_L
	DBF D6,TA
	DBF D7,MASA
	LEA $F8000,A1
	LEA PICA+34+(160*(151)),A0
	MOVE.W #(25*160/8)-1,D0
CLOPS	MOVE.L (A0)+,(A1)+
	MOVE.L (A0)+,(A1)+
	DBF D0,CLOPS 
	LEA POSI_SC,A1
	MOVEQ #16-1,D0
GETSC	BSR S_SC
	BSR S_ROT
	BSR S_ROT
	BSR S_ROT
	BSR S_ROT
	DBF D0,GETSC
	RTS
S_SC	LEA $F8000+(8*4)+(15*160),A0
	MOVE.W #15-1,D7
CSCTR	MOVEM.L (A0),D1-D6/A2-A3
	MOVEM.L D1-D6/A2-A3,(A1)
	LEA -160(A0),A0
	LEA 4*8(A1),A1
	DBF D7,CSCTR
	RTS
S_ROT	LEA $F8000,A0
	MOVE.W #25-1,D6
SCLIN	MOVE.W #4-1,D7
CPLAN	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8*1(A0)
	ROXR 8*2(A0)
	ROXR 8*3(A0)
	ROXR 8*4(A0)
	ROXR 8*5(A0)
	ROXR 8*6(A0)
	ROXR 8*7(A0)
	ADDQ.L #2,A0
	DBF D7,CPLAN
	LEA 160-(2*4)(A0),A0
	DBF D6,SCLIN
	RTS
NEXT_L	DC.L POSI_L
SAVE_L	MOVEA.L NEXT_L,A1
	LEA $F8000,A0
	REPT 15
	MOVEM.L (A0),D0-D5
	SWAP D0
	SWAP D2
	SWAP D4
	MOVE.W D0,(A1)+  
	MOVE.W D2,(A1)+  
	MOVE.W D4,(A1)+  
	LEA 160(A0),A0
	ENDR
	MOVE.L A1,NEXT_L
	RTS
COP_LE	LEA PICA+34,A0
	LEA $F8000,A1
	MOVEQ.L #FIL-1,D0
	SUB.W D7,D0
	MOVEQ.L #LEF-1,D1
	SUB.W D6,D1
	MULU #160*16,D0
	MULU #3*8,D1
	ADD.W D1,D0
	LEA 0(A0,D0.W),A0
	REPT 15
	MOVEM.L (A0),D0-D5
	MOVEM.L D0-D5,(A1)
	LEA 160(A1),A1
	LEA 160(A0),A0
	ENDR
	RTS
CLS_TR	LEA $F8000,A0
	MOVEM.L BASURA,D0-D1/A1-A6
	REPT 15
	MOVEM.L D0-D1/A1-A6,(A0)
	LEA 160(A0),A0
	ENDR
	RTS
ASRI	MOVE.W D0,D4
	MOVE.W D1,D5
	MOVEA.L A1,A2
ASCR	MOVEA.L A2,A1
	MOVE.W	D4,D0
ASC1	MOVE.W	D5,D1 
ASC2	MOVE.W #0,CCR
	ROXR	(A1)
	ROXR	8(A1)
	ROXR	$10(A1)
	ROXR	$18(A1)
	ROXR	$20(A1)
	ROXR	$28(A1)
	ADDQ.L	#2,A1
	DBF	D1,ASC2
	ADDA.W	#$98,A1
	DBF	D0,ASC1
	DBF	D2,ASCR	
	RTS
SET_TEX	LEA TEXT,A0
	LEA TEXT_P,A1
SET_LE	MOVEQ.L #0,D0
	MOVE.B (A0)+,D0
	BMI YA_NE
	BSR.S LOOK_F
	MULS #15*3*2*2,D0
	MOVE.L #POSI_L,D1
	ADD.L D0,D1
	MOVE.L D1,(A1)+
	BRA.S SET_LE
YA_NE	REPT 11
	MOVE.L D1,(A1)+
	ENDR
	MOVE.L #-1,(A1)+
	RTS	
LOOK_F	LEA CHARS2,A6
	MOVEQ #0,D6
	MOVEQ #0,D7
FND_L	MOVE.B (A6)+,D7
	CMP.B D7,D0
	BEQ.S NFOUND
	ADDQ.B #1,D6
	CMPA.L #ECHARS2,A6
	BLT.S FND_L
	RTS
NFOUND	MOVE.L D6,D0
	RTS
CHARS2	DC.B 'ABCDEFGHIJKLMNOPQRSTUVWXYZ!?:;0123456789"(),-.',"' "
ECHARS2
SAM	MOVE.W (A6)+,D7
	MOVE.L REGI(PC,D7.W),D6
	MOVE.W REGI+4(PC,D7.W),D7
	MOVEP.L D6,(A5)
	MOVEP.W D7,(A5)
	RTE
REGI	INCBIN 'RED.SAM'
BTS	DS.L 1
CLETRA	DC.L TEXTCL
CPARTE	DS.W 1
PONM	DC.L POSI_SC+(15*4*8*16)
OLDR	DS.L 2	
BASURA	DS.L 16
C_SCR	DC.L TEXT_P
CER	DC.L LISTSC-4
LISTSC	DC.L PUTY1
	DC.L PUTY2
	DC.L PUTY3
	DC.L PUTY4
	DC.L PUTY5
	DC.L PUTY6
TEXT	DC.B "        YES, I'M HERE AGAIN. AT THE MOMENT I AM NEARLY HALF ASLEEP COZ IT'S 9:00 AM AND YESTERDAY I WENT TO BED AT 2:00 AM. I THINK I WON'T WORK SO MUCH TODAY BECAUSE "
	DC.B "NEXT WEEK I'VE GOT FINAL EXAMINATIONS (2 B.U.P. IF YOU WANT TO KNOW IT). THIS IS THE FIRST SCREEN IN THE "
	DC.B '"DIABOLICAL DEMO"'
	DC.B "AND HOPE YOU LIKE IT... ACTUALLY THE INTRO SCREEN PICTURE IS FINISHED AND SO IS THE MUZAK (IT WAS HACKED FROM JAMBALA) BUT ANY CODE HAS BEEN DONE YET. OK, LET'S TALK ABOUT SOMETHING ELSE. "
	DC.B "THE CREDITS:CODING , SOME ARTWORK : RED DEVIL, SAMPLING: SCRATCHIN. NOW I THINK I'M GOING AWAY THIS SUMMER, FIRST TO ENGLAND AND "
	DC.B '"IBIZA"'
	DC.B " BUT I KNOW I'LL HAVE TIME TO MAKE SOME OTHER SCREENS. ACTUALLY I ONLY CODE FROM 9:00 PM TO 11:00 DAILY, COZ I HAVE TO GO TO SCHOOL NEXT DAY (WAH). "
	DC.B "TODAY IT'S SATURDAY (1ST OR 2ND OF JUNE) I DON'T MIND IT A LOT, BUT WHAT I MIND IS THAT I START EXAMS ON 6TH OF JUNE (OOOOOHHHHH) WELL, GOTTA GO NOW... LET'S WRAP        "  
	DC.B -1
	DS.B 11
TEXT_P	DS.L TEXT_P-TEXT
PICA	INCBIN 'FONTMONT.PI1'
SAMPLE	INCBIN 'ANA.SET'
ES1	DS.B ES1-SAMPLE
ESAMPLE
	DS.B 1000
COLOR0	DC.W $000,$001,$002,$003,$004,$005,$006,$007
	DC.W $106,$205,$304,$403,$502,$601,$700,$710
	DC.W $720,$730,$740,$750,$760,$770,$771,$772
	DC.W $773,$774,$775,$776,$777
	DC.W $777,$677,$577,$477,$477,$377,$377,$277
	DC.W $277,$277,$177,$177,$177,$077,$077,$077
	DC.W $067,$067,$067,$067,$067,$057,$057,$057
	DC.W $057,$057,$047,$047,$047,$047,$047,$037
	DC.W $037,$037,$037,$037,$037,$037,$027,$027
	DS.W 3
SYNC_70	DS.B 1
OLDR7	DS.B 1
OLDR9	DS.B 1
OLDRF	DS.B 1
OLDR13	DS.B 1
OLDR17	DS.B 1
OLDR19	DS.B 1
OLDR1F	DS.B 1
COLOR_N	DS.L 1
OLDVBL	DS.L 1
OLDHBL	DS.L 1
OLDFRE	DS.L 1
TEXTC	DC.B 'HELLO!! WELLCOME TO OUR NEW DEMO, CALLED THE DEEP SCROLL DEMO. AH AH YOU SHOULD KNOW WHY IT IS CALLED LIKE THAT NO? '
	DC.B "WELL, I KNOW THIS IS HARD TO READ AND CAUSE OF SO I WON'T WRITE MORE TEXT. AH OK, LET'S TELL YOU MY ADDRESS HERE: " 
	DC.B "RED DEVIL. PO BOX 566. 03080 ALICANTE. SPAIN        OK ONCE AGAIN...."
	DC.B "RED DEVIL. PO BOX 566. 03080 ALICANTE. SPAIN        OK ONCE NO MORE...."
	DC.B "IF YOU CAN REALLY READ THIS YOU MAY BE CRAZY COZ IT'S ALMOST IMPOSSIBLE TO READ. OK LET'S WRAP NOW !!!! "
	DC.B -1
	DS.B 2
TEXTCL	DS.L TEXTCL-TEXTC
COLOR_B	DS.W (200/LINES)*16	
COLORAB	DS.W (200/LINES)*16	
PIC	INCBIN 'CASTILLO.PI1'
FONT	INCBIN 'FONT.COL'
W_AREA	
POSI_L	EQU W_AREA
POSI_SC	EQU POSI_L+LEF*FIL*15*3*2*2
OKMMMM	EQU POSI_SC+15*8*4*16
