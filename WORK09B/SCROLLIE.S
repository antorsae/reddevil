** SCROLLIE V 2.01

** ES LA VERSION + AVANZADA DEL 
** CLASICO SCROLL NANO. AQUI PARECE
** IR MUCHO + RAPIDO, PUES CREO 
** HABERLO DEPURADO BASTANTE.....

** (C) 1990 RED DEVIL

	CLR.L -(A7)
	MOVE.W #$20,-(A7)
	TRAP #1
	LEA 6(A7),A7
	BSR CLS
	BSR STEXT
	BSR ROTY		 
ME	MOVE.W #$25,-(A7)
	TRAP #14
	LEA 2(A7),A7
	*MOVE.W #$700,$FFFF8240.W
	BSR SCLL
	MOVE.W #$777,$FFFF8240.W
	BRA.S ME
	CLR.L -(A7)
	TRAP #1
CTEXT	DC.L ZTEXT
VEL	EQU 2 ;(POSIBLES 1,2,4,8)
PAS	DC.W 7*4*(8-VEL)
SCLL	LEA SFONT(PC),A3
	MOVEA.L CTEXT,A4
	MOVE.W PAS,D6
	MOVEA.L SCREEN,A6
	REPT 20
	MOVE.W (A4)+,D0
	ADD.W D6,D0
	LEA (A3,D0.W),A0
	MOVE.W (A4)+,D0
	ADD.W D6,D0
	LEA (A3,D0.W),A1
	MOVE.W (A4),D0
	ADD.W D6,D0
	LEA (A3,D0.W),A2
	BSR PM1
	ADDQ.L #8,A6
	ENDR
	SUB.W #7*4*VEL,PAS
	BPL.S NO_VUL
	MOVE.W #7*4*(8-VEL),PAS
	ADDQ.L #2,CTEXT
	MOVE.W 2(A4),D6
	BPL.S NO_VUL
	MOVE.L #ZTEXT,CTEXT
NO_VUL	RTS
PM1	
INS1	MACRO LI
	MOVE.W 2+(4*\1)+(7*4*8)(A0),D7
	OR.W (4*\1)(A1),D7
	OR.W (4*\1)+(7*4*8)(A2),D7
	MOVE.W D7,(160*\1)(A6)
	ENDM
	MOVE.W 2+(7*4*8)(A0),D7
	OR.W (A1),D7
	OR.W (7*4*8)(A2),D7
	MOVE.W D7,(A6)
	INS1 1
	INS1 2
	INS1 3
	INS1 4
	INS1 5
	INS1 6
	RTS	
CLS	LEA $F8000,A0
	MOVE.W #(32000/8)-1,D0
KKLL	CLR.L (A0)+
	CLR.L (A0)+
	DBF D0,KKLL
	RTS
ROTY	LEA SFONT,A6
	MOVE.W #80-1,D7
GETTL	BSR CLD
	MOVE.W #80-1,D0
	SUB.W D7,D0
	LEA FONT,A0
	LEA $F8000,A1
	BSR TODOL
	MOVE.W #16-1,D6
SALB	BSR SALVA
	BSR ROTA1
	DBF D6,SALB
	DBF D7,GETTL
	RTS
CLD	LEA $F8000,A0
	CLR.W (A0)
	CLR.W 160*1(A0)
	CLR.W 160*2(A0)
	CLR.W 160*3(A0)
	CLR.W 160*4(A0)
	CLR.W 160*5(A0)
	CLR.W 160*6(A0)
	CLR.W 8(A0)
	CLR.W 8+(160*1)(A0)
	CLR.W 8+(160*2)(A0)
	CLR.W 8+(160*3)(A0)
	CLR.W 8+(160*4)(A0)
	CLR.W 8+(160*5)(A0)
	CLR.W 8+(160*6)(A0)
	RTS
SALVA	LEA $F8000,A0
	MOVE.W (A0),(A6)+
	MOVE.W 8(A0),(A6)+
	MOVE.W 160*1(A0),(A6)+
	MOVE.W 8+(160*1)(A0),(A6)+
	MOVE.W 160*2(A0),(A6)+
	MOVE.W 8+(160*2)(A0),(A6)+
	MOVE.W 160*3(A0),(A6)+
	MOVE.W 8+(160*3)(A0),(A6)+
	MOVE.W 160*4(A0),(A6)+
	MOVE.W 8+(160*4)(A0),(A6)+
	MOVE.W 160*5(A0),(A6)+
	MOVE.W 8+(160*5)(A0),(A6)+
	MOVE.W 160*6(A0),(A6)+
	MOVE.W 8+(160*6)(A0),(A6)+
	RTS
ROTA1	LEA $F8000,A0
	MOVE.W #0,CCR
	ROXR (A0)
	ROXR 8(A0)
	ROXR 160(A0)
	ROXR 8+160(A0)
	ROXR 160*2(A0)
	ROXR 8+(160*2)(A0)
	ROXR 160*3(A0)
	ROXR 8+(160*3)(A0)
	ROXR 160*4(A0)
	ROXR 8+(160*4)(A0)
	ROXR 160*5(A0)
	ROXR 8+(160*5)(A0)
	ROXR 160*6(A0)
	ROXR 8+(160*6)(A0)
	RTS
TODOL	MULU #8,D0
	LEA 0(A0,D0.W),A0
COPA	MOVE.B (A0)+,(A1)
	MOVE.B (A0)+,160*1(A1)
	MOVE.B (A0)+,160*2(A1)
	MOVE.B (A0)+,160*3(A1)
	MOVE.B (A0)+,160*4(A1)
	MOVE.B (A0)+,160*5(A1)
	MOVE.B (A0)+,160*6(A1)
	MOVE.B (A0)+,160*7(A1)
	RTS
STEXT	LEA TEXT,A0
	LEA ZTEXT,A1
TERR	MOVEQ #0,D0
	MOVE.B (A0)+,D0
	BEQ.S YAUNEG
	BSR SSER
	MULU #7*4*16,D0
	MOVE.W D0,(A1)+
	BRA.S TERR
YAUNEG	MOVE.W #-1,(A1)
	RTS
SSER	LEA CH,A2
	MOVEQ #-1,D1
SCLOP	MOVE.B (A2)+,D2
	BMI.S NFOUND
	ADDQ.W #1,D1
	CMP.B D0,D2
	BNE.S SCLOP
NFOUND	MOVEQ #0,D0
	MOVE.W D1,D0
	RTS
CH	DC.B "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789:!. \_+-=?(),'",'"',-1  
BASURA	DS.L 16
SCREE	DS.L 1
CDC	DS.W 1
TEXT	DC.B '                                           '
	DC.B 'HOLA, QUE HAY? V 2.01 DEL SCROLLIE QUE UNA VEZ HICE EN MI JUVENTUD, LA VERDAD ES QUE ESTA VERSION '
	DC.B 'LE DA 1000 PATADAS A LAS ANTERIORES, Y QUIZA AUN LA PUEDA MEJORAR +, OK, ME PARECE QUE ME VOY; UN '
	DC.B 'CORDIAL SALUDO DE... RED DEVIL (!!) '
	DC.B '                                           '
	DC.B 0
ZTEXT	DS.W (ZTEXT-TEXT)
SCREEN	DC.L $F8000
FONT	INCBIN 'FONT8P3.RED'
SFONT	DS.B 80*7*4*16  
EFONT
  